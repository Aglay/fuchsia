# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/go/go_binary.gni")
import("//build/go/go_test.gni")
import("//packages/package.gni")

package("netstack") {
  deps = [
    ":netstack_bin",
    "//garnet/go/src/netstack/getaddrtest",
    "//garnet/go/src/netstack/ifconfig",
    "//garnet/go/src/netstack/ifinfo",
    "//garnet/public/lib/netstack/fidl",
  ]

  binaries = [
    {
      name = "netstack"
    }, {
      name = "ifconfig"
    }, {
      name = "ifinfo"
    }, {
      name = "getaddrtest"
    },
  ]
}

package("netstack_gotests") {
  testonly = true

  deps = [
    ":netstack_netiface_test",
  ]

  tests = [ {
        name = "netstack_netiface_test"
      } ]
}

go_binary("netstack_bin") {
  output_name = "netstack"

  gopackage = "netstack"

  go_dependencies = [
    {
      package = "netstack"
      source = "//garnet/go/src/netstack"
    },
    {
      package = "application/lib/app"
      source = "//application/lib/app/go/src/app"
    },
    {
      package = "application/lib/svc"
      source = "//application/lib/svc/go/src/svc"
    },
    {
      package = "fidl"
      source = "//garnet/public/lib/fidl/go/src/fidl"
    },
    {
      package = "github.com/google/netstack"
      source = "//third_party/netstack"
    },
  ]

  deps = [
    "//garnet/public/lib/netstack/fidl",
  ]
}

go_test("netstack_netiface_test") {
  gopackage = "netstack/netiface"
  go_dependencies = [
    {
      package = "netstack"
      source = "//garnet/go/src/netstack"
    },
    {
      package = "github.com/google/netstack"
      source = "//third_party/netstack"
    },
  ]
}
