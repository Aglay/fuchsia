# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/lto/config.gni")

config("lto") {
  if (use_thinlto) {
    cflags = [ "-flto=thin" ]
    ldflags = cflags + [
                # ThinLTO driver launches number of threads in parallel whose
                # number is by default equivalent to the number of cores. We
                # need to limit the parallelism to avoid aggressive competition
                # between different linker jobs.
                "-Wl,--thinlto-jobs=$thinlto_jobs",

                # Set ThinLTO cache directory which is used to cache native
                # object files for ThinLTO incremental builds. This directory
                # is not managed by Ninja and has to be cleaned manually, but
                # it is periodically garbage collected by ThinLTO driver.
                "-Wl,--thinlto-cache-dir=$thinlto_cache_dir",
              ]
  } else {
    cflags = [
      "-flto",

      # Enable whole-program devirtualization and virtual constant propagation.
      "-fwhole-program-vtables",
    ]
    ldflags = cflags
  }
}
