# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/zircon.gni")
import("//build/testing/environments.gni")

# Wrapper to declare a Zircon host test in the GN build.
#
# Not to be used manually.
template("zircon_host_test") {
  assert(defined(invoker.spec), "Must provide a spec")
  spec = invoker.spec

  is_disabled = defined(spec.disabled) && spec.disabled != ""

  # No current test uses this capability, so prevent any future use to simplify
  # migrations.
  if (defined(spec.runtime_deps)) {
    entries = read_file("$zircon_root_build_dir/${spec.runtime_deps}", "json")
    assert(entries == [], "Runtime deps not supported")
  }

  # Copy the test executable to the GN build's output directory.
  # Use the same path as in the ZN build in order to keep the spec's path
  # correct.
  copy(target_name) {
    testonly = true

    sources = [ "$zircon_root_build_dir/${spec.path}" ]

    outputs = [ "$root_build_dir/${spec.path}" ]

    # The metadata will ensure the test is included in the JSON file listing
    # all tests.
    metadata = {
      # Don't include the tests if it is currently disabled.
      if (!is_disabled) {
        tests = [
          {
            test = {
              forward_variables_from(spec,
                                     "*",
                                     [
                                       "disabled",
                                       "runtime_deps",
                                     ])
              label = string_replace(label, "//", "//zircon/")
            }

            # We only run host tests from x64 builds to avoid duplication.
            if (target_cpu == "x64") {
              environments = [ host_env ]
            } else {
              environments = []
            }
          },
        ]
      }
    }
  }
}
