# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# Verifies that two zbi files are identical.
#
# Parameters
#
#   original (required)
#     Path to the original zbi file.
#
#   updated (required)
#     Path to the updated zbi file.
#
#   deps, testonly, visibility
#     See `gn help`.

template("verify_zbi") {
  assert(defined(invoker.original), "Need to specify an original zbi file")
  assert(defined(invoker.updated), "Need to specify an updated zbi file")

  zbi_label = "//zircon/public/tool/zbi($host_toolchain)"
  tool_out_dir = get_label_info(zbi_label, "root_out_dir")
  zbi_tool = "$tool_out_dir/zbi"

  action(target_name) {
    script = "//build/unification/future/images/compare_zbis.py"

    forward_variables_from(invoker,
                           [
                             "testonly",
                             "visibility",
                           ])

    deps = [ zbi_label ]
    if (defined(invoker.deps)) {
      deps += invoker.deps
    }

    inputs = [
      invoker.original,
      invoker.updated,
      zbi_tool,
    ]
    outputs = [ "$target_gen_dir/$target_name.stamp" ]

    args = [
      "--original-zbi",
      rebase_path(inputs[0], root_build_dir),
      "--updated-zbi",
      rebase_path(inputs[1], root_build_dir),
      "--tool",
      rebase_path(inputs[2], root_build_dir),
      "--stamp",
      rebase_path(outputs[0], root_build_dir),
    ]
  }
}
