# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/zircon_images.gni")

bootsvc_flavors = []

foreach(image, zircon_images) {
  if (image.type == "manifest" && image.cpu == target_cpu &&
      image.name == "bootsvc") {
    _name = "bootsvc"
    toolchain = get_label_info(image.label, "toolchain")
    if (string_replace(toolchain, "asan-ubsan", "") != toolchain) {
      _name += ".asan-ubsan"
    } else if (string_replace(toolchain, "ubsan", "") != toolchain) {
      _name += ".ubsan"
    } else if (string_replace(toolchain, "asan", "") != toolchain) {
      _name += ".asan"
    } else if (string_replace(toolchain, "gcc", "") != toolchain) {
      _name += ".gcc"
    }
    bootsvc_flavors += [
      {
        name = _name
        path = image.path
      }
    ]
  }
}
