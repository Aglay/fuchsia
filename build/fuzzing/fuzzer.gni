# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/host.gni")
import("//build/package.gni")

# TODO(aarongreen): SEC-224.  Add tests to catch fuzzer building/packaging
# regressions.

# Defines a fuzzer binary.
#
# The fuzzer template is used to create components containing binaries
# which leverage LLVM's libFuzzer to perform fuzz testing.
#
# Parameters
#
#   sources (optional)
#     Usual GN meaning. If present, the list must include a file defining
#     `LLVMFuzzerTestOneInput`.
#
#   binary (optional)
#     [path] The path to the primary binary for the component. This can be
#     used to indicate a fuzzer binary built in a different phase of the
#     build, e.g. in zircon.
#
#     NOTE: Exactly one of sources or binary must be set.
#
#   options (optional)
#     [list of strings] Each option is of the form "key=value" and indicates
#     command line options that the fuzzer should be invoked with. Valid keys
#     are libFuzzer options (https://llvm.org/docs/LibFuzzer.html#options).
#
#   dictionary (optional)
#     [file] If specified, a file containing inputs, one per line, that the
#     fuzzer will use to generate new mutations.
#
template("fuzzer") {
  assert(
      (defined(invoker.binary) && !defined(invoker.sources)) ||
          (!defined(invoker.binary) || defined(invoker.sources)),
      "Exactly one of `binary` or `sources` must be defined for $target_name")
  fuzzer_name = target_name

  # If not specified, generate the component binary
  if (!defined(invoker.binary)) {
    executable(fuzzer_name) {
      # Explicitly forward visibility, implicitly forward everything else.
      # See comment in //build/config/BUILDCONFIG.gn for details on this pattern.
      forward_variables_from(invoker, [ "visibility" ])
      forward_variables_from(invoker,
                             "*",
                             [
                               "dictionary",
                               "options",
                               "visibility",
                             ])
      testonly = true
      _target_type = "fuzzed_executable"
    }
  }

  # Generate the component manifest
  fuzzer_cmx = "${target_gen_dir}/${fuzzer_name}.cmx"
  action("${fuzzer_name}_cmx") {
    script = "//build/fuzzing/gen_fuzzer_manifest.py"
    outputs = [
      fuzzer_cmx,
    ]
    args = [
      "--out",
      rebase_path(outputs[0]),
      "--bin",
    ]
    if (defined(invoker.binary)) {
      args += [ invoker.binary ]
    } else {
      args += [ fuzzer_name ]
    }
    if (defined(invoker.cmx)) {
      args += [
        "--cmx",
        rebase_path(invoker.cmx),
      ]
    }
  }

  # Generate data files needed at runtime
  output_dictionary = "${target_gen_dir}/${fuzzer_name}/dictionary"
  if (defined(invoker.dictionary)) {
    copy("${fuzzer_name}_dictionary") {
      sources = [
        invoker.dictionary,
      ]
      outputs = [
        output_dictionary,
      ]
    }
  } else {
    generated_file("${fuzzer_name}_dictionary") {
      contents = []
      outputs = [
        output_dictionary,
      ]
    }
  }
  generated_file("${fuzzer_name}_options") {
    contents = []
    if (defined(invoker.options)) {
      contents = invoker.options
    }
    outputs = [
      "${target_gen_dir}/${fuzzer_name}/options",
    ]
  }

  # Create the fuzzer component
  deprecated_fuchsia_component("${fuzzer_name}_component") {
    testonly = true
    manifest = fuzzer_cmx
    if (defined(invoker.binary)) {
      binary = invoker.binary
    } else {
      binary = "${fuzzer_name}"
    }
    resources = [
      {
        path = "${target_gen_dir}/${fuzzer_name}/dictionary"
        dest = "${fuzzer_name}/dictionary"
      },
      {
        path = "${target_gen_dir}/${fuzzer_name}/options"
        dest = "${fuzzer_name}/options"
      },
    ]
    deps = []
    forward_variables_from(invoker, [ "visibility" ])
    if (!defined(invoker.binary)) {
      # deps, public_deps, and data_deps are forwarded to ${fuzzer_name}
      deps += [ ":${fuzzer_name}" ]
    } else {
      forward_variables_from(invoker,
                             [
                               "deps",
                               "public_deps",
                               "data_deps",
                             ])
    }
    deps += [
      ":${fuzzer_name}_cmx",
      ":${fuzzer_name}_dictionary",
      ":${fuzzer_name}_options",
    ]
  }
}

set_defaults("fuzzer") {
  configs = default_executable_configs +
            [ "//build/fuzzing:fuzzing_build_mode_unsafe_for_production" ]
}

# Defines a package of fuzzers
#
# The fuzzers_package template is used to bundle several fuzzers and their
# associated data into a single Fuchsia package.
#
# Parameters
#
#   fuzzers (required)
#     [list of labels] The fuzzer() labels to include in this package.
#
#   sanitizers (optional)
#     [list of variants] A set of sanitizer variants.  The resulting package
#     will contain binaries for each sanitizer/fuzzer combination. Defaults to
#     [ "asan", "ubsan" ]
#
#   fuzz_host (optional)
#     [boolean] Indicates whether to also build fuzzer binaries on host.
#     Defaults to false.
#
#   meta (optional)
#   binaries (optional)
#   components (optional)
#   tests (optional)
#   drivers (optional)
#   loadable_modules (optional)
#   resources (optional)
#   extra (optional)
#     Same meanings as in //build/package.gni
#
#   deps (optional)
#   public_deps (optional)
#   data_deps (optional)
#   testonly (optional)
#     Usual GN meanings.
template("fuzzers_package") {
  assert(defined(invoker.fuzzers), "fuzzers must be defined for $target_name}")

  if (defined(invoker.sanitizers)) {
    sanitizers = invoker.sanitizers
  } else {
    sanitizers = [
      "asan",
      "ubsan",
    ]
  }

  if (defined(invoker.fuzz_host)) {
    fuzz_host = invoker.fuzz_host
  } else {
    fuzz_host = false
  }

  # Collect the selected fuzzers listed in this package based on the variants
  # selected in args.gn and filtered by the package's list of supported
  # sanitizers.
  selected = false
  selected_fuzzers = []
  foreach(fuzzer, invoker.fuzzers) {
    selected = false
    foreach(sanitizer, sanitizers) {
      foreach(selector, select_variant_canonical) {
        if (!selected && selector.variant == "${sanitizer}-fuzzer" &&
            ((defined(selector.target_type) &&
              selector.target_type == [ "fuzzed_executable" ]) ||
             (defined(selector.name) && selector.name == [ fuzzer ]) ||
             (defined(selector.output_name) &&
              selector.output_name == [ fuzzer ]))) {
          selected = true
          selected_fuzzers += [ fuzzer ]
        }
      }
    }
  }

  # Assemble the Fuchsia package
  if (selected_fuzzers != []) {
    package(target_name) {
      metadata = {
        fuzz_spec = [
          {
            fuzzers_package = target_name
            fuzzers = []
            foreach(selected_target, selected_fuzzers) {
              fuzzers += [ get_label_info(selected_target, "name") ]
            }
            fuzz_host = fuzz_host
          },
        ]
        if (defined(invoker.metadata)) {
          forward_variables_from(invoker.metadata, "*", [ "fuzz_spec" ])
        }
      }
      deps = []
      components = []
      forward_variables_from(invoker, [ "visibility" ])
      forward_variables_from(invoker,
                             "*",
                             [
                               "fuzzers",
                               "sanitizers",
                               "visibility",
                               "fuzz_host",
                             ])
      testonly = true
      foreach(selected_target, selected_fuzzers) {
        components += [ "${selected_target}_component" ]
      }
      if (fuzz_host) {
        deps += [ ":host_${target_name}" ]
      }
    }

    # Treat host fuzzers as tools. If we get to the point of having name
    # collisions, we'll need to extend `install_host_tools` to allow copying to
    # specific subdirectories of `host_tools_dir`
    if (fuzz_host) {
      install_host_tools("host_${target_name}") {
        testonly = true
        deps = selected_fuzzers
        outputs = []
        foreach(target, selected_fuzzers) {
          outputs += [ get_label_info(target, "name") ]
        }
      }
    }
  } else {
    # Dummy package for non-fuzzed builds
    group(target_name) {
    }
    not_needed(invoker, "*")
    not_needed([
                 "fuzz_host",
                 "selected",
               ])
  }
}

# TODO(aarongreen): Complete soft transition and remove.
# These templates are the old names for `fuzzer` and `fuzzers_package`. They
# are included to allow third_party projects to transition softly.
template("fuzz_target") {
  source_set(target_name) {
  }
  not_needed(invoker, "*")
}

set_defaults("fuzz_target") {
  configs = []
}

template("fuzz_package") {
  group(target_name) {
  }
  not_needed(invoker, "*")
}
