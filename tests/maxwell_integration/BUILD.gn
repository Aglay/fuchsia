# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

group("maxwell_integration") {
  testonly = true

  public_deps = [
    ":context_engine_test",
    ":suggestion_engine_test",
  ]
}

executable("context_engine_test") {
  testonly = true

  sources = [
    "context_engine_test.cc",
  ]

  public_deps = [
    "//garnet/public/lib/app/cpp",
  ]

  deps = [
    ":context_common",
    "//garnet/public/lib/test_runner/cpp:gtest_main",
    "//peridot/bin/context_engine:scope_utils",
    "//peridot/lib/testing:entity_resolver_fake",
    "//peridot/public/lib:fidl",
    "//peridot/public/lib/context/cpp:context_helper",
    "//peridot/public/lib/context/cpp:context_metadata_builder",
    "//peridot/public/lib/context/cpp:formatting",
  ]
}

executable("suggestion_engine_test") {
  testonly = true

  sources = [
    "suggestion_engine_test.cc",
  ]

  public_deps = [
    "//garnet/public/lib/app/cpp",
  ]

  deps = [
    ":context_common",
    ":test_suggestion_listener",
    "//garnet/public/lib/test_runner/cpp:gtest_main",
    "//peridot/bin/acquirers/mock:gps",
    "//peridot/bin/agents:carmen_sandiego",
    "//peridot/bin/agents:ideas",
    "//peridot/lib/rapidjson",
    "//peridot/lib/testing:story_provider_mock",
    "//peridot/lib/testing:wait_until_idle",
    "//peridot/public/lib:fidl",
    "//peridot/public/lib/context/cpp:context_helper",
  ]
}

# TODO(rosswang): move utils to a subdirectory

source_set("context_common") {
  testonly = true

  sources = [
    "context_engine_test_base.cc",
    "context_engine_test_base.h",
  ]

  deps = [
    ":common",
    "//peridot/lib/testing:wait_until_idle",
  ]

  public_deps = [
    "//peridot/public/lib:fidl",
    "//third_party/googletest:gtest",
  ]
}

source_set("common") {
  testonly = true

  sources = [
    "test.cc",
    "test.h",
  ]

  deps = [
    "//garnet/public/lib/fsl",
    "//peridot/bin/user:agent_launcher",
    "//peridot/lib/environment_host",
    "//peridot/lib/testing:component_context_fake",
    "//peridot/lib/testing:entity_resolver_fake",
  ]

  public_deps = [
    "//garnet/lib/gtest",
    "//garnet/public/lib/app/cpp",
    "//third_party/googletest:gtest",
  ]
}

source_set("test_suggestion_listener") {
  testonly = true

  sources = [
    "test_suggestion_listener.cc",
    "test_suggestion_listener.h",
  ]

  deps = [
    "//garnet/public/lib/fxl",
    "//peridot/public/lib:fidl",
    "//peridot/public/lib/suggestion/cpp:formatting",
  ]

  public_deps = [
    "//third_party/googletest:gtest",
  ]
}
