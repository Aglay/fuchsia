# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# trace is a helper library for talking to the
# mojo tracing service (see tracing.mojo).
source_set("types") {
  sources = [
    "internal/types.h",
  ]
}

source_set("trace") {
  sources = [
    "event.h",
    "internal/categories_matcher.h",
    "internal/categories_matcher.cc",
    "internal/writer.h",
    "internal/writer.cc",
  ]

  deps = [
    ":types",
    "//lib/ftl",
    "//lib/mtl",
    "//lib/mtl/vmo",
  ]

  public_deps = [
    "//magenta/system/ulib/mx",
  ]
}

source_set("provider") {
  sources = [
    "provider.h",
    "provider.cc",
  ]

  deps = [
    "//apps/modular/lib/app",
    "//apps/tracing/services",
    "//lib/ftl",
    "//lib/mtl",
  ]

  public_deps = [
    ":trace",
  ]
}

source_set("reader") {
  sources = [
    "reader.h",
    "reader.cc",
  ]

  deps = [
    ":types",
    "//lib/ftl",
  ]
}

executable("trace_tests") {
  testonly = true

  sources = [
    "tests/categories_matcher_unittest.cc",
    "tests/writer_unittest.cc",
  ]

  deps = [
    ":trace",
    ":reader",
    "//apps/tracing/services",
    "//lib/mtl/test",
    "//third_party/gtest",
    "//third_party/rapidjson",
  ]
}
