# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//packages/package.gni")
import("//garnet/lib/magma/gnbuild/magma.gni")

group("vulkan") {
  public_deps = [
    "//third_party/vulkan_loader_and_validation_layers:vulkan",
  ]
}

package("magma") {
  if (target_cpu == "x64") {
    deps = [
      ":libmagma",
      ":libvulkan",
    ]

    libraries = [
      {
        name = "libmagma.so"
      },
      {
        name = "libvulkan_intel.so"
      },
    ]

    resources = [ {
          path = rebase_path("icd/magma_icd.json")
          dest = "vulkan/icd.d/magma_icd.json"
        } ]

    if (target_cpu == "x64") {
      deps += [ "$msd_intel_gen_build_root:msd_intel" ]

      drivers = [ {
            # This driver is needed early in bootup (i.e. in the /boot partition)
            # so that devmgr correctly chooses it over the intel-i915.so driver on
            # hardware with supported intel GPUs.
            early_boot_driver = true
            name = "libmsd_intel.so"
          } ]
    }
  }
}

package("magma-dev") {
  if (target_cpu == "x64") {
    testonly = true
    deps = [
      ":libmagma",
      ":libvulkan",
    ]

    libraries = [
      {
        name = "libmagma.so"
      },
      {
        name = "libvulkan_intel.so"
      },
    ]

    resources = [ {
          path = rebase_path("icd/magma_icd.json")
          dest = "vulkan/icd.d/magma_icd.json"
        } ]

    if (target_cpu == "x64") {
      deps += [ "$msd_intel_gen_build_root:msd_intel_test" ]

      drivers = [ {
            # This driver is needed early in bootup (i.e. in the /boot partition)
            # so that devmgr correctly chooses it over the intel-i915.so driver on
            # hardware with supported intel GPUs.
            early_boot_driver = true
            name = "libmsd_intel_test.so"
          } ]
    }
  }
}

package("magma-tools") {
  if (target_cpu == "x64") {
    deps = [
      ":tools",
    ]

    binaries = [ {
          name = "magma_info"
        } ]
  }
}

package("vkcube") {
  if (target_cpu == "x64") {
    deps = [
      "//garnet/lib/magma/tests/vkcube",
    ]

    binaries = [ {
          name = "vkcube"
        } ]
  }
}

# This config allows inclusion of headers via relative paths from src
config("magma_src_include_config") {
  include_dirs = [ "src" ]
}

# This config allows inclusion of headers via relative paths from tests
config("magma_tests_include_config") {
  include_dirs = [ "tests" ]
}

group("libmagma") {
  public_deps = [
    "src/libmagma",
  ]
}

if (target_cpu == "x64") {
  group("libvulkan") {
    public_deps = [
      "$mesa_build_root:magma_vulkan",
    ]
  }

  # internal only
  source_set("libmsd") {
    deps = [
      "src/sys_driver",
    ]
    public_deps = [
      "include:msd_abi",
    ]
  }

  group("tools") {
    public_deps = [
      "src/tools",
    ]
  }
}
