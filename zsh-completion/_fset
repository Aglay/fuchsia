#compdef fset

_fset() {
  # _packages completes a comma separated list of packages
  _packages() {
    _values -s , $FUCHSIA_DIR/packages/gn/^*.*(.:t)
  }

  # list of supported fuchsia architectures
  local -a archs
  archs=(
    'x86-64:64 bit Intel'
    'arm64:64 bit ARM'
    'rpi3:rpi3 ARM'
  )

  # arguments to fset
  _arguments '1:arch:{_describe "arch" archs}' '--release[release build]' \
    '--packages[packages to build]:packages:_packages' \
    '--goma[use goma]' "--no-goma[don't use goma]" \
    "--no-ensure-goma[don't make sure goma is running]" \
    "--ccache[use ccache]" "--no-ccache[don't use ccache]" \
    '--goma-dir[goma directory to use]:directory:_files -/'
}

_fset
