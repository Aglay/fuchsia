# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//packages/package.gni")

group("mtl") {
  public_deps = [
    ":mtl_lib",
    "//magenta/system/ulib/fbl",
    "//magenta/system/ulib/fs",
    "//magenta/system/ulib/mx",
  ]
}

shared_library("mtl_lib") {
  output_name = "mtl"

  public_deps = [
    "//garnet/public/lib/mtl/handles",
    "//garnet/public/lib/mtl/io",
    "//garnet/public/lib/mtl/socket",
    "//garnet/public/lib/mtl/tasks",
    "//garnet/public/lib/mtl/threading",
    "//garnet/public/lib/mtl/vfs",
    "//garnet/public/lib/mtl/vmo",
    "//garnet/public/lib/mtl/waiter",
  ]
}

executable("mtl_unittests") {
  testonly = true

  sources = [
    "handles/object_info_unittest.cc",
    "io/redirection_unittest.cc",
    "socket/blocking_drain_unittest.cc",
    "socket/files_unittest.cc",
    "socket/socket_drainer_unittest.cc",
    "socket/strings_unittest.cc",
    "tasks/fd_waiter_unittest.cc",
    "tasks/message_loop_unittest.cc",
    "threading/create_thread_unittest.cc",
    "threading/thread_unittest.cc",
    "vmo/file_unittest.cc",
    "vmo/shared_vmo_unittest.cc",
    "vmo/strings_unittest.cc",
    "vmo/vector_unittest.cc",
  ]

  deps = [
    ":mtl",
    "//garnet/public/lib/mtl/test",
    "//third_party/gtest",
  ]
}

package("package") {
  testonly = true

  package_name = "mtl"

  deps = [
    ":mtl",
    ":mtl_unittests",
  ]

  libraries = [ {
        name = "libmtl.so"
      } ]

  tests = [ {
        name = "mtl_unittests"
      } ]
}
