# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/go/go_binary.gni")
import("//build/go/go_library.gni")
import("//build/go/go_test.gni")
import("//build/go/toolchain.gni")
import("//build/host.gni")
import("//build/testing/host_test_data.gni")
import("//zircon/tools/fidl/testdata/targets.gni")

go_library("gopkg") {
  name = "go.fuchsia.dev/fuchsia/tools/fidl/fidlgen_go/..."
  deps = [
    "//garnet/go/src/fidl/compiler/backend/common",
    "//garnet/go/src/fidl/compiler/backend/types",
  ]
  sources = [
    "codegen/bits.tmpl.go",
    "codegen/codegen.go",
    "codegen/codegen_test.go",
    "codegen/enum.tmpl.go",
    "codegen/ir.go",
    "codegen/library.tmpl.go",
    "codegen/protocol.tmpl.go",
    "codegen/struct.tmpl.go",
    "codegen/table.tmpl.go",
    "codegen/union.tmpl.go",
    "main.go",
  ]
}

go_binary("fidlgen_go") {
  gopackage = "go.fuchsia.dev/fuchsia/tools/fidl/fidlgen_go"

  sdk_category = "partner"
  deps = [ ":gopkg" ]
}

install_host_tools("host") {
  deps = [ ":fidlgen_go" ]
  outputs = [ "fidlgen_go" ]
}

if (is_host) {
  go_test("fidlgen_go_test") {
    gopackages = [ "go.fuchsia.dev/fuchsia/tools/fidl/fidlgen_go/codegen" ]
    args = [
      "--test_data_dir",
      rebase_path("$root_out_dir/test_data/fidlgen", root_build_dir),
    ]
    deps = [
      ":gopkg",
      "//garnet/go/src/fidl/compiler/backend/typestest",
    ]
  }
}

group("goldens") {
  testonly = true
  deps = []
  foreach(target, fidl_testdata_targets) {
    deps += [ "$target($go_toolchain)" ]
  }
}

group("tests") {
  testonly = true
  deps = [
    ":fidlgen_go_test($host_toolchain)",
    ":goldens",
  ]
}
