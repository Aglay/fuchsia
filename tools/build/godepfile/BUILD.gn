# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

action("godepfile") {
  outputs = [
    "$root_out_dir/godepfile",
  ]
  script = "//tools/build/godepfile/bootstrap.sh"

  # godepfile_pkg_path should be relative to the fuchsia root directory.
  godepfile_pkg_path = rebase_path("//tools/build/godepfile/cmd", "//")
  go_bin = rebase_path("//prebuilt/third_party/go/${host_platform}/bin/go")

  if (host_os == "linux") {
    go_os = "linux"
  } else if (host_os == "mac") {
    go_os = "darwin"
  }
  if (host_cpu == "x64") {
    go_arch = "amd64"
  } else if (host_cpu == "arm64") {
    go_arch = "arm64"
  }

  go_path = rebase_path(target_gen_dir)
  go_cache = rebase_path(target_gen_dir)

  # TODO(fxbug.dev/37708): Use long-options when getopt is available on macs.
  args = [
    "-o",
    rebase_path(outputs[0]),
    "-g",
    godepfile_pkg_path,
    "-a",
    go_arch,
    "-O",
    go_os,
    "-b",
    go_bin,
    "-p",
    go_path,
    "-c",
    go_cache,
  ]

  metadata = {
    tool_paths = [
      {
        cpu = host_cpu
        label = get_label_info(":$target_name", "label_with_toolchain")
        name = target_name
        os = host_os
        path = rebase_path(outputs[0], root_build_dir)
      },
    ]
  }
}
