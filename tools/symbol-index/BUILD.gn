# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/host.gni")
import("//build/test.gni")

if (is_host) {
  source_set("src") {
    sources = [
      "command_line_options.cc",
      "symbol_index.cc",
    ]
    deps = [
      "//src/lib/fxl",
      "//zircon/public/lib/cmdline",
    ]
  }

  executable("symbol-index") {
    sources = [ "main.cc" ]
    deps = [
      ":src",
      "//src/lib/fxl",
    ]
  }

  test("symbol_index_tests") {
    sources = [
      "command_line_options_test.cc",
      "symbol_index_test.cc",
    ]
    deps = [
      ":src",
      "//src/lib/files",
      "//src/lib/fxl/test:gtest_main",
      "//third_party/googletest:gtest",
    ]
  }
}

install_host_tools("symbol_index_host") {
  deps = [ ":symbol-index" ]
  outputs = [ "symbol-index" ]
}

group("tests") {
  testonly = true
  deps = [ ":symbol_index_tests($host_toolchain)" ]
}
