# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/sdk/sdk_atom.gni")

# A source set that can be exported to an SDK.
#
# An equivalent to the built-in source_set which adds an SDK atom declaration to
# allow the set to be included in an SDK.

template("sdk_source_set") {

  main_target_name = target_name

  source_set(main_target_name) {
    forward_variables_from(invoker, "*")
  }

  sdk_atom("${main_target_name}_sdk") {
    name = "cpp:$main_target_name"

    files = []
    if (defined(invoker.sources)) {
      foreach(source_file, invoker.sources) {
        files += [{
          source = source_file
        }]
      }
    }

    deps = []
    if (defined(invoker.public_deps)) {
      foreach(dep, invoker.public_deps) {
        full_label = get_label_info(dep, "label_no_toolchain")
        deps += [
          "${full_label}_sdk",
        ]
      }
    }

    non_sdk_deps = [
      ":$main_target_name",
    ]
  }
}
