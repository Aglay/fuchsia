# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//peridot/build/executable_package.gni")

executable_package("context_engine") {
  sources = [
    "context_engine_main.cc",
  ]

  deps = [
    ":context_engine_impl",
    "//garnet/public/lib/app/cpp",
    "//garnet/public/lib/fsl",
    "//peridot/public/lib:fidl",
    "//peridot/public/lib/app_driver/cpp",
  ]
}

package("context_engine_unittests") {
  testonly = true

  deps = [
    ":context_index_unittest",
    ":context_repository_unittest",
  ]

  tests = [
    {
      name = "context_index_unittest"
    },
    {
      name = "context_repository_unittest"
    }
  ]
}

source_set("context_index") {
  sources = [
    "index.cc",
    "index.h",
  ]

  deps = [
    "//garnet/public/lib/fxl",
    "//peridot/public/lib:fidl",
  ]
}

executable("context_index_unittest") {
  testonly = true

  sources = [
    "index_unittest.cc",
  ]

  deps = [
    ":context_index",
    "//garnet/public/lib/fsl",
    "//peridot/public/lib:fidl",
    "//peridot/public/lib/context/cpp:context_helper",
    "//peridot/public/lib/context/cpp:formatting",
    "//third_party/googletest:gtest_main",
  ]
}

source_set("context_reader_impl") {
  sources = [
    "context_reader_impl.cc",
    "context_reader_impl.h",
  ]

  deps = [
    ":context_repository",
    "//peridot/public/lib:fidl",
    "//peridot/public/lib/context/cpp:formatting",
  ]
}

source_set("context_repository") {
  sources = [
    "context_repository.cc",
    "context_repository.h",
    "debug.cc",
    "debug.h",
  ]

  public_deps = [
    "//peridot/lib/util:idle_waiter",
  ]

  deps = [
    ":context_index",
    "//garnet/public/lib/fsl",
    "//peridot/lib/bound_set",
    "//peridot/lib/rapidjson",
    "//peridot/public/lib:fidl",
    "//peridot/public/lib/context/cpp:formatting",
    "//third_party/rapidjson",
  ]
}

executable("context_repository_unittest") {
  testonly = true

  sources = [
    "context_repository_unittest.cc",
  ]

  deps = [
    ":context_repository",
    "//garnet/public/lib/fsl",
    "//peridot/public/lib:fidl",
    "//peridot/public/lib/context/cpp:context_helper",
    "//peridot/public/lib/context/cpp:context_metadata_builder",
    "//peridot/public/lib/context/cpp:formatting",
    "//third_party/googletest:gtest_main",
  ]
}

source_set("context_writer_impl") {
  sources = [
    "context_writer_impl.cc",
    "context_writer_impl.h",
  ]

  deps = [
    ":context_repository",
    "//garnet/public/lib/fxl",
    "//peridot/lib/bound_set",
    "//peridot/public/lib:fidl",
    "//peridot/public/lib/async/cpp:future",
    "//peridot/public/lib/context/cpp:formatting",
    "//peridot/public/lib/entity/cpp",
    "//peridot/public/lib/entity/cpp:json",
    "//third_party/rapidjson",
  ]
}

source_set("context_engine_impl") {
  sources = [
    "context_engine_impl.cc",
    "context_engine_impl.h",
  ]

  public_deps = [
    "//garnet/public/lib/app/cpp",
  ]

  deps = [
    ":context_reader_impl",
    ":context_repository",
    ":context_writer_impl",
    "//garnet/public/lib/fidl/cpp",
    "//peridot/public/lib:fidl",
  ]
}

source_set("scope_utils") {
  sources = [
    "scope_utils.cc",
    "scope_utils.h",
  ]

  deps = [
    "//garnet/public/lib/fxl",
    "//peridot/public/lib:fidl",
    "//third_party/boringssl",  # for sha256
  ]
}
