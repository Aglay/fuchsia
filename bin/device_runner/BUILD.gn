# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")

import("//third_party/flatbuffers/flatbuffer.gni")

package("device_runner") {
  deprecated_system_image = true

  deps = [
    ":device_runner_bin",
  ]

  binaries = [
    {
      name = "device_runner"
    },
  ]
}

package("dev_device_shell") {
  deps = [
    ":dev_device_shell_bin",
  ]

  binary = "dev_device_shell"
}

flatbuffer("users") {
  sources = [
    "users.fbs",
  ]
}

executable("device_runner_bin") {
  output_name = "device_runner"

  sources = [
    "device_runner.cc",
  ]

  deps = [
    ":lib",
    "//garnet/public/lib/app/cpp",
    "//garnet/public/lib/app/fidl",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/fxl",
    "//garnet/public/lib/ui/presentation/fidl",
    "//garnet/public/lib/ui/views/fidl:v1",
    "//peridot/bin/device_runner/cobalt",
    "//peridot/lib/common:async_holder",
    "//peridot/lib/common:teardown",
    "//peridot/lib/fidl:app_client",
    "//peridot/lib/fidl:array_to_string",
    "//peridot/lib/fidl:clone",
    "//peridot/lib/fidl:scope",
    "//peridot/lib/ledger_client:constants",
    "//peridot/lib/util:filesystem",
    "//peridot/public/lib:fidl",
    "//peridot/public/lib:fidl_internal",
    "//peridot/public/lib/auth/fidl",
    "//zircon/public/lib/trace-provider",
  ]
}

source_set("lib") {
  sources = [
    "user_controller_impl.cc",
    "user_controller_impl.h",
    "user_provider_impl.cc",
    "user_provider_impl.h",
  ]

  deps = [
    ":users",
    "//garnet/public/lib/app/cpp",
    "//garnet/public/lib/app/fidl",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/fxl",
    "//garnet/public/lib/ui/presentation/fidl",
    "//garnet/public/lib/ui/views/fidl:v1",
    "//peridot/lib/common:async_holder",
    "//peridot/lib/common:names",
    "//peridot/lib/common:teardown",
    "//peridot/lib/common:xdr",
    "//peridot/lib/fidl:app_client",
    "//peridot/lib/fidl:array_to_string",
    "//peridot/lib/fidl:clone",
    "//peridot/lib/fidl:json_xdr",
    "//peridot/lib/fidl:scope",
    "//peridot/lib/util:filesystem",
    "//peridot/public/lib:fidl",
    "//peridot/public/lib:fidl_internal",
    "//peridot/public/lib/auth/fidl",
    "//zircon/public/lib/trace",
  ]
}

executable("dev_device_shell_bin") {
  output_name = "dev_device_shell"

  sources = [
    "dev_device_shell.cc",
  ]

  deps = [
    "//garnet/public/lib/callback",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/fxl",
    "//garnet/public/lib/ui/views/fidl:v1",
    "//peridot/lib/fidl:single_service_app",
    "//peridot/lib/testing",
    "//peridot/public/lib:fidl",
    "//peridot/public/lib/app_driver/cpp",
  ]
}

source_set("unittests") {
  testonly = true

  sources = [
    "user_controller_impl_unittest.cc",
  ]

  deps = [
    ":lib",
    "//garnet/public/lib/app/cpp",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/fxl",
    "//garnet/public/lib/gtest",
    "//peridot/lib/fidl:clone",
    "//peridot/lib/testing:fake_application_launcher",
    "//third_party/googletest:gtest",
  ]
}

package("config") {
  testonly = true
  deprecated_system_image = true
  package_name = "device_runner_config"
  resources = [
    {
      dest = "sysmgr/device_runner.config"
      path = rebase_path("config/device_runner.config")
    },
  ]
}
