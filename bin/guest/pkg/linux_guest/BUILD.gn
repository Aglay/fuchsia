# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//garnet/bin/guest/pkg/guest_package.gni")

linux_image = "$target_out_dir/linux/Image"
disk_image = "$target_out_dir/linux/disk.img"

copy("block") {
  visibility = [ ":*" ]
  sources = [ "images/${target_cpu}/disk.img" ]
  outputs = [ disk_image ]
}

copy("kernel") {
  visibility = [ ":*" ]
  sources = [ "images/${target_cpu}/Image" ]
  outputs = [ linux_image ]
}

guest_package("linux_guest") {
  deps = [
    ":block",
    ":kernel",
  ]

  if (target_cpu == "arm64") {
    cmdline = "earlycon=pl011,0x808300000"
  } else if (target_cpu == "x64") {
    cmdline = "earlycon=uart,io,0x3f8"
  }
  cmdline += " console=hvc0 console=tty0 root=/dev/vda"

  linux = "$target_out_dir/linux/Image"

  disk_name = "filesystem.img"
  resources = [
    {
      path = rebase_path("$target_out_dir/linux/disk.img")
      dest = "${disk_name}"
    },
  ]
  block_devices = [ "/guest/data/${disk_name},ro,volatile" ]
}
