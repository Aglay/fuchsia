# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")

visibility = [ "//peridot/*" ]

package("cloud_provider_firebase") {
  deps = [
    "app",
    "//peridot/bin/ledger/cobalt:generate_ledger_config",
  ]

  resources = [
    {
      path = rebase_path(
              get_label_info(
                             "//peridot/bin/ledger/cobalt:generate_ledger_config",
                             "target_gen_dir")
              + "/cobalt_config.binproto")
      dest = "firebase_auth_cobalt_config.binproto"
    },
  ]

  binary = "cloud_provider_firebase"
}

executable("cloud_provider_firebase_unittests") {
  testonly = true

  deps = [
    "//peridot/bin/cloud_provider_firebase/app:unittests",
    "//peridot/bin/cloud_provider_firebase/device_set:unittests",
    "//peridot/bin/cloud_provider_firebase/gcs:unittests",
    "//peridot/bin/cloud_provider_firebase/page_handler/impl:unittests",
  ]
}

group("tests") {
  testonly = true

  public_deps = [
    ":cloud_provider_firebase_unittests",
    "//peridot/bin/cloud_provider_firebase/testing/server",
    "//peridot/bin/cloud_provider_firebase/validation",
  ]
}
