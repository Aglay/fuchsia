# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

visibility = [ "//peridot/bin/cloud_provider_firebase/*" ]

executable("app") {
  output_name = "cloud_provider_firebase"

  sources = [
    "app.cc",
  ]

  deps = [
    ":lib",
    "//garnet/public/lib/app/cpp",
    "//garnet/public/lib/fidl/cpp",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/fxl",
    "//peridot/public/lib:fidl",
    "//zircon/public/lib/trace-provider",
  ]
}

source_set("lib") {
  sources = [
    "cloud_provider_impl.cc",
    "cloud_provider_impl.h",
    "convert_status.cc",
    "convert_status.h",
    "device_set_impl.cc",
    "device_set_impl.h",
    "factory_impl.cc",
    "factory_impl.h",
    "page_cloud_impl.cc",
    "page_cloud_impl.h",
  ]

  public_deps = [
    "//garnet/public/lib/fidl/cpp",
    "//garnet/public/lib/fxl",
    "//peridot/bin/cloud_provider_firebase/device_set",
    "//peridot/bin/cloud_provider_firebase/fidl",
    "//peridot/bin/cloud_provider_firebase/page_handler",
    "//peridot/lib/firebase_auth",
    "//peridot/public/lib/auth/fidl",
    "//peridot/public/lib/cloud_provider/fidl",
  ]

  deps = [
    "//garnet/public/lib/backoff",
    "//garnet/public/lib/callback",
    "//peridot/lib/convert",
  ]
}

source_set("unittests") {
  testonly = true

  sources = [
    "cloud_provider_impl_unittest.cc",
    "device_set_impl_unittest.cc",
    "page_cloud_impl_unittest.cc",
  ]

  deps = [
    ":lib",
    "//garnet/public/lib/fidl/cpp",
    "//garnet/public/lib/fxl:fxl_printers",
    "//garnet/public/lib/network_wrapper:fake",
    "//garnet/public/lib/test_runner/cpp:gtest_main",
    "//peridot/bin/cloud_provider_firebase/device_set/testing",
    "//peridot/bin/cloud_provider_firebase/fidl",
    "//peridot/bin/cloud_provider_firebase/page_handler/testing",
    "//peridot/bin/ledger/testing:lib",
    "//peridot/lib/firebase_auth/testing",
    "//peridot/public/lib/auth/fidl",
    "//peridot/public/lib/cloud_provider/fidl",
  ]
}
