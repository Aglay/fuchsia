# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//peridot/build/executable_package.gni")

executable_package("module_resolver") {
  meta = [
    {
      path = "meta/module_resolver.cmx"
      dest = "module_resolver.cmx"
    },
  ]

  sources = [
    "module_resolver_main.cc",
  ]

  deps = [
    ":local_module_resolver",
    "//garnet/public/lib/app/cpp",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/fxl",
    "//garnet/public/lib/net/oldhttp/fidl",
    "//peridot/lib/module_manifest_source:firebase_source",
    "//peridot/lib/module_manifest_source:module_package_source",
    "//peridot/public/lib:fidl",
    "//peridot/public/lib/app_driver/cpp:app_driver",
    "//peridot/public/lib/entity/cpp:json",
  ]
}

package("module_resolver_unittests") {
  testonly = true

  deps = [
    ":local_module_resolver_unittest",
  ]

  tests = [
    {
      name = "local_module_resolver_unittest"
    },
  ]
}

source_set("local_module_resolver") {
  sources = [
    "local_module_resolver.cc",
    "local_module_resolver.h",
    "type_inference.cc",
    "type_inference.h",
  ]

  deps = [
    "//garnet/public/lib/fxl",
    "//peridot/lib/module_manifest_source",
    "//peridot/public/lib:fidl",
    "//peridot/public/lib/async/cpp:operation",
    "//peridot/public/lib/context/cpp:context_helper",
    "//peridot/public/lib/entity/cpp:json",
  ]
}

executable("local_module_resolver_unittest") {
  testonly = true

  sources = [
    "local_module_resolver_unittest.cc",
  ]

  deps = [
    ":local_module_resolver",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/fxl",
    "//garnet/public/lib/gtest",
    "//peridot/lib/testing:entity_resolver_fake",
    "//peridot/public/lib/entity/cpp:json",
    "//peridot/public/lib/module_resolver/cpp:formatting",
    "//third_party/googletest:gtest_main",
  ]
}
