# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//garnet/bin/run_test_component/test_package.gni")

source_set("lib") {
  testonly = true

  sources = [
    "run_test_component.cc",
    "run_test_component.h",
  ]

  public_deps = [
    "//garnet/lib/pkg_url",
    "//garnet/public/lib/component/cpp",
    "//garnet/public/lib/fxl",
  ]
}

executable("bin") {
  testonly = true
  output_name = "run_test_component"

  sources = [
    "main.cc",
  ]

  deps = [
    ":lib",
    "//garnet/public/fidl/fuchsia.sys",
    "//garnet/public/lib/component/cpp",
    "//garnet/public/lib/component/cpp/testing",
    "//zircon/public/lib/async-loop-cpp",
  ]
}

package("run_test_component") {
  testonly = true
  deprecated_system_image = true

  deps = [
    ":bin",
  ]

  binaries = [
    {
      name = "run_test_component"
    },
  ]
}

executable("test_bin") {
  testonly = true
  output_name = "run_test_component_unittests"

  sources = [
    "run_test_component_unittest.cc",
  ]

  deps = [
    ":lib",
    "//garnet/public/lib/fxl/test:gtest_main",
  ]
}

test_package("run_test_component_unittests") {
  deps = [
    ":test_bin",
  ]

  tests = [
    {
      name = "run_test_component_unittests"
    },
  ]
}
