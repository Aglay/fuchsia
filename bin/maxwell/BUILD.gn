# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//peridot/build/executable_package.gni")

executable_package("maxwell") {
  resources = [
    {
      path = rebase_path("//peridot/bin/maxwell/resources/default_config.json")
      dest = "maxwell/default_config.json"
    },
    {
      path = rebase_path("//peridot/bin/maxwell/resources/test_config.json")
      dest = "maxwell/test_config.json"
    },
  ]

  sources = [
    "maxwell_main.cc",
  ]

  deps = [
    ":config",
    ":user_intelligence_provider",
    "//garnet/public/lib/component/cpp",
    "//peridot/public/fidl/fuchsia.modular",
    "//third_party/rapidjson",
    "//zircon/public/lib/async-loop-cpp",
  ]
}

source_set("config") {
  sources = [
    "config.cc",
    "config.h",
  ]
}

source_set("user_intelligence_provider") {
  sources = [
    "user_intelligence_provider_impl.cc",
    "user_intelligence_provider_impl.h",
  ]

  public_deps = [
    "//garnet/public/lib/component/cpp",
  ]

  deps = [
    ":agent_launcher",
    ":config",
    ":intelligence_services",
    "//garnet/public/fidl/fuchsia.bluetooth.le",
    "//garnet/public/fidl/fuchsia.net.oldhttp",
    "//peridot/lib:maxwell_internal",
    "//peridot/lib/util:rate_limited_retry",
    "//peridot/public/fidl/fuchsia.modular",
    "//zircon/public/fidl/fuchsia-cobalt",
  ]
}

source_set("intelligence_services") {
  sources = [
    "intelligence_services_impl.cc",
    "intelligence_services_impl.h",
  ]

  deps = [
    "//peridot/public/fidl/fuchsia.modular",
  ]
}

source_set("agent_launcher") {
  sources = [
    "agent_launcher.cc",
    "agent_launcher.h",
  ]

  public_deps = [
    "//garnet/public/lib/component/cpp",
  ]

  deps = [
    "//peridot/lib/environment_host",
  ]
}
