# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/dart/toolchain.gni")
import("//build/toolchain/clang_toolchain.gni")

# A toolchain dedicated to processing and analyzing Dart packages.
toolchain(get_label_info(dart_toolchain, "name")) {
  # Tools in this toolchain were extracted from:
  # //build/toolchain/clang_toolchain.gni.

  tool("stamp") {
    command = "touch {{output}}"
    description = "STAMP {{output}}"
  }

  tool("copy") {
    # We use link instead of copy; the way "copy" tool is being used is
    # compatible with links since Ninja is tracking changes to the source.
    command = "ln -f {{source}} {{output}} 2>/dev/null || (rm -rf {{output}} && cp -af {{source}} {{output}})"
    description = "COPY {{source}} {{output}}"
  }
}

# The toolchain used to build the host version of the Dart SDK.
clang_toolchain_suite(get_label_info(dart_sdk_host_toolchain, "name")) {
  toolchain_cpu = host_cpu
  toolchain_os = host_os

  with_shared = false
  use_strip = false
}
