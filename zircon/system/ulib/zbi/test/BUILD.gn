# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxbug.dev/36139.                   #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/test.gni")
import("//build/testing/bootfs_test.gni")

group("test") {
  testonly = true
  deps = [ ":zbi" ]
}

test("zbi") {
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  output_name = "libzbi-test"
  sources = [ "zbi.cc" ]
  deps = [
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/pretty",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/zbi",
  ]
  if (is_fuchsia) {
    deps += [
      "//sdk/lib/fdio",
      "//zircon/public/lib/zx",
    ]
  } else {
    output_dir = "$root_out_dir/zircon-migrated"
  }
}

bootfs_test("zbi-bootfs-test") {
  name = "libzbi-test"
  deps = [ ":zbi" ]
}
