# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/unification/zx_library.gni")

zx_library("fs") {
  sdk = "source"
  sdk_headers = [
    "fs/dir_test_util.h",
    "fs/internal/connection.h",
    "fs/internal/directory_connection.h",
    "fs/internal/fidl_transaction.h",
    "fs/internal/file_connection.h",
    "fs/internal/node_connection.h",
    "fs/internal/remote_file_connection.h",
    "fs/internal/stream_file_connection.h",
    "fs/lazy_dir.h",
    "fs/locking.h",
    "fs/managed_vfs.h",
    "fs/mount_channel.h",
    "fs/pseudo_dir.h",
    "fs/pseudo_file.h",
    "fs/queue.h",
    "fs/ref_counted.h",
    "fs/remote_container.h",
    "fs/remote_dir.h",
    "fs/remote_file.h",
    "fs/service.h",
    "fs/synchronous_vfs.h",
    "fs/ticker.h",
    "fs/trace.h",
    "fs/tracked_remote_dir.h",
    "fs/vfs_types.h",
    "fs/vfs.h",
    "fs/vmo_file.h",
    "fs/vnode.h",
    "fs/watcher.h",
  ]
  host = true
  static = true

  public_deps = [
    "//sdk/lib/fdio",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fit",
  ]

  deps = [
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/zircon-internal",
  ]

  # Uncomment to enable some debug log messages
  # defines = [ "FS_TRACE_DEBUG_ENABLED" ]

  sources = [
    "debug.cc",
    "vfs.cc",
    "vnode.cc",
  ]

  if (is_fuchsia) {
    sources += [
      "connection.cc",
      "directory_connection.cc",
      "fidl_transaction.cc",
      "file_connection.cc",
      "lazy_dir.cc",
      "managed_vfs.cc",
      "mount.cc",
      "mount_channel.cc",
      "node_connection.cc",
      "paged_vfs.cc",
      "paged_vnode.cc",
      "pager_thread_pool.cc",
      "pseudo_dir.cc",
      "pseudo_file.cc",
      "remote_container.cc",
      "remote_dir.cc",
      "remote_file.cc",
      "remote_file_connection.cc",
      "service.cc",
      "stream_file_connection.cc",
      "synchronous_vfs.cc",
      "tracked_remote_dir.cc",
      "unmount.cc",
      "vfs_types.cc",
      "vmo_file.cc",
      "watcher.cc",
    ]
    public_deps += [
      # <fs/vnode.h> has #include <fuchsia/io/llcpp/fidl.h>.
      "//sdk/fidl/fuchsia.io:fuchsia.io_llcpp",
      "//zircon/public/lib/async",

      # <fs/tracked_remote_dir.h> has #include <lib/async/cpp/wait.h>.
      "//zircon/public/lib/async-cpp",
      "//zircon/public/lib/fit",
      "//zircon/public/lib/zx",

      # <fs/trace.h> has #include <lib/trace/event.h>.
      "//zircon/public/lib/zxc",
      "//zircon/system/ulib/trace",
    ]
    deps += [
      "//sdk/fidl/fuchsia.cobalt:fuchsia.cobalt_c",
      "//sdk/fidl/fuchsia.io:fuchsia.io_llcpp",
      "//sdk/fidl/fuchsia.mem:fuchsia.mem_llcpp",
      "//sdk/lib/fdio",
      "//zircon/public/lib/async",
      "//zircon/public/lib/fbl",
      "//zircon/public/lib/fidl",
      "//zircon/public/lib/fit",
      "//zircon/public/lib/sync",
      "//zircon/public/lib/zx",
      "//zircon/system/ulib/fzl",
      "//zircon/system/ulib/trace",
    ]
  }

  configs += [
    # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
    "//build/config:Wno-conversion",
  ]
}

group("tests") {
  testonly = true
  deps = [
    "journal:tests",
    "metrics:tests",
    "test:tests",
    "transaction:tests",
  ]
}
