# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/zircon/migrated_targets.gni")

zx_library("zxtest") {
  host = true
  sdk = "source"
  sdk_headers = [
    "zxtest/base/assertion.h",
    "zxtest/base/environment.h",
    "zxtest/base/event-broadcaster.h",
    "zxtest/base/log-sink.h",
    "zxtest/base/observer.h",
    "zxtest/base/test-case.h",
    "zxtest/base/test-driver.h",
    "zxtest/base/test-info.h",
    "zxtest/base/test-internal.h",
    "zxtest/base/test.h",
    "zxtest/base/types.h",
    "zxtest/base/runner.h",
  ]
  sdk_migrated = true
  sources = [
    "assertion.cc",
    "c-wrappers.cc",
    "event-broadcaster.cc",
    "file-log-sink.cc",
    "reporter.cc",
    "runner-options.cc",
    "runner.cc",
    "test-case.cc",
    "test-info.cc",
    "test.cc",
    "zxtest-main.cc",
  ]
  public_deps = [
    # Needed for cpp17::string_view.
    # Needed for <fbl/string.h>, <fbl/vector.h> and <fbl/function.h>
    "//sdk/lib/stdcompat",
    "//zircon/system/ulib/fbl:headers",
    "//zircon/system/ulib/fit:headers",
    "//zircon/system/ulib/zircon:headers",
  ]
  deps = [
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fit",
  ]
  if (is_fuchsia) {
    sources += [ "death-statement.cc" ]

    public_deps += [
      # Needed for <zircon/status.h>
      "//zircon/system/ulib/zircon:headers",

      # Needed for <lib/zx/*>, death-statements.h
      "//zircon/system/ulib/fit:headers",
      "//zircon/system/ulib/zx:headers",
    ]

    deps += [
      "//zircon/system/ulib/zircon",
      "//zircon/system/ulib/zx",
    ]
  }
  metadata = {
    # Used by the fuchsia_test_component_manifest() template.
    test_component_manifest_cml = [
      {
        include = [ "src/sys/test_runners/elf/elf_test_runner.shard.cml" ]
      },
    ]
  }
}

group("tests") {
  testonly = true
  deps = [ "test:tests" ]
}
