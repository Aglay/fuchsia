# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxbug.dev/36548.                   #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/unification/zx_library.gni")

zx_library("fvm") {
  sdk = "source"
  sdk_headers = [
    "fvm/sparse-reader.h",
    "fvm/client.h",
    "fvm/format.h",
    "fvm/fvm-check.h",
    "fvm/fvm-sparse.h",
    "fvm/fvm.h",
    "fvm/metadata.h",
  ]
  host = true
  sources = [
    "format.cc",
    "fvm-check.cc",
    "fvm.cc",
    "metadata.cc",
    "sparse-reader.cc",
  ]
  deps = [
    "//third_party/boringssl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fit",
    "//zircon/system/ulib/digest",
    "//zircon/third_party/ulib/lz4",
    "//zircon/third_party/ulib/safemath",
  ]
  public_deps = [
    "//zircon/public/lib/zxc",
    "//zircon/system/ulib/digest",
    "//zircon/system/ulib/gpt",
    "//zircon/third_party/ulib/lz4",
  ]
  if (is_fuchsia) {
    sources += [ "client.cc" ]

    deps += [
      "//sdk/fidl/fuchsia.hardware.block.volume:fuchsia.hardware.block.volume_c",
      "//zircon/public/lib/sync",
      "//zircon/public/lib/zx",
      "//zircon/system/ulib/block-client",
      "//zircon/system/ulib/fzl",
      "//zircon/system/ulib/gpt",
    ]
  } else {
    # fvm-check.cpp has #include <zircon/status.h> even when built for host.
    public_deps += [ "//zircon/system/ulib/zircon:zircon-headers" ]
  }
  public_configs = [
    # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
    "//build/config:Wno-conversion",
  ]
}

group("tests") {
  testonly = true
  deps = [ "test:tests" ]
}
