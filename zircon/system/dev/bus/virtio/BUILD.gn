# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

zx_driver("virtio_block") {
  sources = [ "block_driver.cc" ]
  deps = [
    ":block_lib",
    ":common",
    "$zx/system/banjo/ddk.protocol.block",
    "$zx/system/ulib/ddktl",
  ]
}

source_set("block_lib") {
  sources = [ "block.cc" ]
  public_deps = [
    ":common",
    "$zx/system/banjo/ddk.protocol.block",
    "$zx/system/ulib/ddktl",
  ]
}

zx_driver("virtio_gpu") {
  sources = [ "gpu_driver.cc" ]
  deps = [
    ":common",
    ":gpu_lib",
    "$zx/system/ulib/ddktl",
  ]
}

source_set("gpu_lib") {
  sources = [ "gpu.cc" ]
  public_deps = [
    ":common",
    "$zx/system/banjo/ddk.protocol.display.controller",
    "$zx/system/banjo/ddk.protocol.sysmem",
    "$zx/system/fidl/fuchsia-sysmem:llcpp",
    "$zx/system/ulib/ddktl",
    "$zx/system/ulib/image-format",
  ]
}

zx_driver("virtio_input") {
  sources = [
    "input.cc",
    "input_driver.cc",
    "input_kbd.cc",
    "input_touch.cc",
  ]
  deps = [
    ":common",
    "$zx/system/banjo/ddk.protocol.hidbus",
    "$zx/system/fidl/fuchsia-hardware-pty:c",
    "$zx/system/ulib/ddktl",
    "$zx/system/ulib/hid",
  ]
}

zx_driver("virtio_console") {
  sources = [
    "console.cc",
    "console_driver.cc",
  ]
  deps = [
    ":common",
    "$zx/system/fidl/fuchsia-hardware-pty:c",
    "$zx/system/fidl/fuchsia-hardware-virtioconsole:llcpp",
    "$zx/system/ulib/async-loop:async-loop-cpp",
    "$zx/system/ulib/async-loop:async-loop-default.static",
    "$zx/system/ulib/ddktl",
    "$zx/system/ulib/fs:static",
    "$zx/system/ulib/fs-pty:static",
  ]

  # TODO(ZX-2863): This driver violates the allowed shlib deps policy.
  deprecated_inhibit_driver_shlib_allowlist = true
}

zx_driver("virtio_ethernet") {
  sources = [
    "ethernet.cc",
    "ethernet_driver.cc",
  ]
  deps = [
    ":common",
    "$zx/system/banjo/ddk.protocol.ethernet",
    "$zx/system/dev/lib/dev-operation",
    "$zx/system/ulib/ddktl",
  ]
}

zx_driver("virtio_rng") {
  sources = [
    "rng.cc",
    "rng_driver.cc",
  ]
  deps = [
    ":common",
    "$zx/system/ulib/ddktl",
  ]
}

zx_driver("virtio_scsi") {
  sources = [ "scsi_driver.cc" ]
  deps = [
    ":common",
    ":scsi_lib",
    "$zx/system/ulib/ddktl",
  ]
}

source_set("scsi_lib") {
  sources = [ "scsi.cc" ]
  public_deps = [
    ":common",
    "$zx/system/banjo/ddk.protocol.block",
    "$zx/system/dev/lib/scsi",
    "$zx/system/ulib/ddktl",
  ]
}

zx_driver("virtio_socket") {
  sources = [
    "socket.cc",
    "socket_driver.cc",
  ]
  deps = [
    ":common",
    "$zx/system/fidl/fuchsia-hardware-vsock:c",
    "$zx/system/ulib/async:async-cpp",
    "$zx/system/ulib/async-loop:async-loop-cpp",
    "$zx/system/ulib/async-loop:async-loop-default.static",
    "$zx/system/ulib/ddktl",
  ]

  # TODO(ZX-2863): This driver violates the allowed shlib deps policy.
  deprecated_inhibit_driver_shlib_allowlist = true
}

source_set("common") {
  visibility = [ ":*" ]
  sources = [
    "backends/pci.cc",
    "backends/pci_legacy.cc",
    "backends/pci_modern.cc",
    "device.cc",
    "driver_utils.cc",
    "driver_utils.h",
    "ring.cc",
  ]
  public_deps = [
    "$zx/system/banjo/ddk.protocol.pci",
    "$zx/system/dev/lib/device-protocol-pci",
    "$zx/system/dev/lib/mmio",
    "$zx/system/ulib/ddk",
    "$zx/system/ulib/ddktl",
    "$zx/system/ulib/fbl",
    "$zx/system/ulib/fit",
    "$zx/system/ulib/hid",
    "$zx/system/ulib/hwreg",
    "$zx/system/ulib/pretty",
    "$zx/system/ulib/sync",
    "$zx/system/ulib/virtio",
    "$zx/system/ulib/zircon",
    "$zx/system/ulib/zircon-internal",
    "$zx/system/ulib/zx",
  ]
}

zx_test("virtio-test") {
  sources = [
    "block_test.cc",
    "gpu_test.cc",
    "scsi_test.cc",
  ]
  deps = [
    ":block_lib",
    ":common",
    ":gpu_lib",
    ":scsi_lib",
    "$zx/system/dev/lib/fake-bti",
    "$zx/system/dev/lib/fake_ddk",
    "$zx/system/ulib/async-loop:async-loop-cpp",
    "$zx/system/ulib/async-loop:async-loop-default.static",
    "$zx/system/ulib/driver",
    "$zx/system/ulib/fidl-async:fidl-async-cpp",
    "$zx/system/ulib/unittest",
    "$zx/system/ulib/zxtest",
  ]
}
