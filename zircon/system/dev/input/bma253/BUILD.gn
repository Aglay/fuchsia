# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxb/36139.                         #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/unification/images/migrated_manifest.gni")

driver_module("bma253") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "bma253.cc" ]
  deps = [
    "//src/devices:driver",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/ddktl",
    "//zircon/public/lib/device-protocol-i2c",
    "//zircon/public/lib/device-protocol-i2c-channel",
    "//zircon/public/lib/device-protocol-pdev",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/hid",
    "//zircon/public/lib/mmio",
    "//zircon/public/lib/simplehid",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
    "//zircon/system/banjo/ddk.protocol.clock",
    "//zircon/system/banjo/ddk.protocol.gpio",
    "//zircon/system/banjo/ddk.protocol.hidbus",
    "//zircon/system/banjo/ddk.protocol.i2c",
  ]
}

test("bma253-test") {
  # Dependent manifests unfortunately cannot be marked as `testonly`.
  # TODO(44278): Remove when converting this file to proper GN build idioms.
  testonly = false
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "bma253-test"
  sources = [
    "bma253-test.cc",
    "bma253.cc",
  ]
  deps = [
    "//src/devices:driver",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/ddktl",
    "//zircon/public/lib/device-protocol-i2c",
    "//zircon/public/lib/device-protocol-i2c-channel",
    "//zircon/public/lib/device-protocol-pdev",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/hid",
    "//zircon/public/lib/mmio",
    "//zircon/public/lib/mock-hidbus-ifc",
    "//zircon/public/lib/mock-i2c",
    "//zircon/public/lib/simplehid",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/banjo/ddk.protocol.gpio:ddk.protocol.gpio_mock",
    "//zircon/system/banjo/ddk.protocol.i2c",
  ]
}

migrated_manifest("bma253-manifest") {
  deps = [ ":bma253" ]
}

migrated_manifest("bma253-test-manifest") {
  deps = [ ":bma253-test" ]
}
