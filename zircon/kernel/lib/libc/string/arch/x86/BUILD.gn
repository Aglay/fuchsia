# Copyright 2019 The Fuchsia Authors
#
# Use of this source code is governed by a MIT-style
# license that can be found in the LICENSE file or at
# https://opensource.org/licenses/MIT

import("//build/zircon/migrated_targets.gni")

source_set("x86") {
  sources = [
    "memcpy.S",
    "memset.S",
  ]
  deps = [ "//zircon/kernel/lib/code_patching:headers" ]

  if (toolchain.environment != "kernel") {
    # TODO(mcgrathr): Make the assembly compatible with phys.
    # For now, just use the C.
    sources = []
    sources = [
      "//zircon/kernel/lib/libc/string/memcpy.c",
      "//zircon/kernel/lib/libc/string/memset.c",
    ]
    deps = []
    deps = [ "//zircon/kernel/lib/libc:headers" ]
  }
}

# Depended upon by //zircon/kernel:code-patching.
source_set("code-patching") {
  sources = [ "selector.cc" ]
  deps = [
    "//zircon/kernel/lib/arch",
    "//zircon/kernel/lib/code_patching:headers",
  ]
}

source_set("tests") {
  # TODO(mcgrathr): Make this optional somehow and enable testonly taint.
  # testonly = true
  sources = [ "tests.cc" ]
  deps = [
    "//zircon/kernel/lib/arch",
    "//zircon/kernel/lib/unittest",
  ]
}
