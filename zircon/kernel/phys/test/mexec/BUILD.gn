# Copyright 2020 The Fuchsia Authors
#
# Use of this source code is governed by a MIT-style
# license that can be found in the LICENSE file or at
# https://opensource.org/licenses/MIT

import("//src/bringup/lib/mexec/testing/zbi_test.gni")
import("../common_tests.gni")

# NOTE: The rules below are out of //zircon/kernel/phys/test/BUILD.gn
# since the latter expects to be parsed in a kernel.phys toolchain
# environment exclusively (and will assert otherwise), while this file
# expects to be parsed in the default toolchain instead.

# We create mexec-chainloading variations of all of the phys ZBI tests.
# Once there is no longer a separate zircon build, we can define these targets
# directly, instead of sifting through zircon's zbi_tests.json to synthesize
# targets in this build.
test_deps = []
foreach(test, common_tests) {
  mexec_zbi_test("mexec-$test") {
    child_zbi = "//zircon/kernel/phys/test:zbi-$test(//zircon/kernel/phys:kernel.phys_$current_cpu)"
    child_zbi_file =
        get_label_info(child_zbi, "target_out_dir") + "/zbi-$test.zbi"
  }

  test_deps += [ ":mexec-$test" ]
}

group("mexec") {
  testonly = true
  deps = test_deps
}
