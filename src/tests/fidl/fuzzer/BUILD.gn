# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/dist/distribution_manifest.gni")
import("//build/fuzzing/fuzzer.gni")
import("//build/fuzzing/fuzzer_package.gni")
import("//src/tests/fidl/conformance_suite/gidl-conformance-suite.gni")
import("//tools/fidl/gidl/gidl.gni")

#
# Fuzzer
#

fuzzer("conformance-fuzzer") {
  sources = [ "fuzzer.cc" ]
  deps = [
    "//src/tests/fidl/conformance_suite:conformance_fidl_libfuzzer",
    "//zircon/system/public",
  ]

  corpus = ":distribution_entries_file"
}

fuzzer_package("fidl-fuzzers") {
  fuzzers = [ ":conformance-fuzzer" ]
}

#
# Corpus
#

conformance_test_source = "$target_gen_dir/corpus.distribution_entries"

gidl("generate_conformance_test") {
  testonly = true
  type = "conformance"
  language = "fuzzer_corpus"
  inputs = conformance_suite_gidl_files
  fidl = conformance_suite_fidl_target
  output = conformance_test_source
  extra_args = [
    "--fuzzer-corpus-dir",
    rebase_path("$target_gen_dir/corpus", root_build_dir),
  ]
}

distribution_entries_file("distribution_entries_file") {
  testonly = true
  file = conformance_test_source
  deps = [ ":generate_conformance_test" ]
}

group("tests") {
  testonly = true
  deps = [ ":fidl-fuzzers" ]
}
