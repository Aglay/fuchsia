# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/go/go_library.gni")
import("//build/go/go_test.gni")
import("//build/testing/host_test_data.gni")

# IMPORTANT: The femu_test host test depends on a device target which builds
# fuchsia.zbi and cannot be part of the dependency chain reachable from //src:tests
# so put it in the one reachable from //src:host_tests instead.

group("host_tests") {
  testonly = true
  deps = []

  # //src/testing/emulator only works on linux.
  if (is_linux) {
    deps += [ ":femu_test($host_toolchain)" ]
  }
}

if (is_host) {
  go_library("lib") {
    testonly = true
    sources = [ "femu_test.go" ]
    deps = [ "//src/testing/emulator" ]
  }

  go_test("femu_test") {
    gopackages = [ "go.fuchsia.dev/fuchsia/src/tests/femu" ]
    deps = [ ":lib" ]

    # This host test depends on fuchsia.zbi being available at runtime.
    data_deps = [ "//build/images:fuchsia($default_toolchain)" ]
  }
}
