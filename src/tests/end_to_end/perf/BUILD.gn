# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/dart/test.gni")
import("//build/testing/environments.gni")

# This directory contains Dart wrappers for running performance tests
# that are defined elsewhere in the Fuchsia tree.
#
# The test wrappers in this directory are split into multiple test
# executables (dart_test targets here) because the Fuchsia Infra tools
# for retrying tests and tracking flakes operate on the granularity of
# test executables.

test_list = [
  "ledger_perf_test",
  "microbenchmarks_test",
  "rust_inspect_benchmarks_test",
  "tspec_example_test",
]
foreach(target_name, test_list) {
  dart_test(target_name) {
    sources = [
      "$target_name.dart",
    ]

    deps = [
      "//sdk/testing/sl4f/client",
      "//third_party/dart-pkg/pub/test",
    ]

    non_dart_deps = [ ":runtime_deps" ]

    environments = [
      # Runs on "main" builders (try and ci) in NUC environments.
      {
        dimensions = {
          device_type = "Intel NUC Kit NUC7i5DNHE"
        }
      },
    ]
  }
}

copy("runtime_deps") {
  _data_dir = "$target_gen_dir/runtime_deps"

  sources = [
    "$root_out_dir/catapult_converter",
  ]

  outputs = [
    "$_data_dir/{{source_file_part}}",
  ]

  metadata = {
    test_runtime_deps = [ "$_data_dir/catapult_converter" ]
  }

  deps = [
    "//garnet/bin/catapult_converter:converter_bin",
  ]
}

# Packages required by the Dart test wrappers in this directory.
group("package_deps") {
  testonly = true
  public_deps = [
    "//garnet/bin/zircon_benchmarks:zircon_benchmarks_perftestmode",
    "//garnet/examples/benchmark",
    "//src/ledger:bin",
    "//src/ledger/bin/tests/benchmark",
    "//src/lib/inspect/rust/fuchsia-inspect:benchmarks",
  ]
}

group("test") {
  testonly = true

  deps = [
    ":ledger_perf_test($host_toolchain)",
    ":microbenchmarks_test($host_toolchain)",
    ":rust_inspect_benchmarks_test($host_toolchain)",
    ":tspec_example_test($host_toolchain)",
  ]
}
