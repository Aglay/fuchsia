# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/dart/test.gni")
import("//build/testing/environments.gni")

dart_test("perf_test") {
  sources = [
    "perf_test.dart",
  ]

  deps = [
    "//sdk/testing/sl4f/client",
    "//third_party/dart-pkg/pub/test",
  ]

  non_dart_deps = [ ":runtime_deps" ]

  environments = [
    # Runs on "main" builders (try and ci) in NUC environments.
    {
      dimensions = {
        device_type = "Intel NUC Kit NUC7i5DNHE"
      }
    },
  ]
}

copy("runtime_deps") {
  _data_dir = "$target_gen_dir/runtime_deps"

  sources = [
    "$root_out_dir/catapult_converter",
  ]

  outputs = [
    "$_data_dir/{{source_file_part}}",
  ]

  metadata = {
    test_runtime_deps = [ "$_data_dir/catapult_converter" ]
  }

  deps = [
    "//garnet/bin/catapult_converter:converter_bin",
  ]
}

group("test") {
  testonly = true

  deps = [
    ":perf_test($host_toolchain)",
  ]
}
