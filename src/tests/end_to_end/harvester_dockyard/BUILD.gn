# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/dart/test.gni")
import("//build/host.gni")
import("//build/testing/environments.gni")

dart_test("harvester_dockyard_test") {
  sources = [ "harvester_dockyard_test.dart" ]
  deps = [
    "//sdk/testing/sl4f/client",
    "//third_party/dart-pkg/pub/test",
  ]

  # Do some set up that isn't Dart specific.
  non_dart_deps = [ ":runtime_deps($host_toolchain)" ]

  # Tell the trybots how to set up the test for specific environments.
  environments = [
    nuc_env,

    # The astro environment has tags as it is still to be run on an FYI builder.
    {
      dimensions = {
        device_type = "Astro"
      }
      tags = [ "e2e-fyi" ]
    },
  ]
}

if (is_host) {
  # Copy the dockyard_host to a directory that the test can easily find.
  copy("runtime_deps") {
    _data_dir = "$target_gen_dir/runtime_deps"
    sources = [ "$host_tools_dir/dockyard_host" ]
    outputs = [ "$_data_dir/{{source_file_part}}" ]
    deps = [ "//src/developer/system_monitor/bin/dockyard_host:host_tools" ]

    # This is believed to have value for infra.
    metadata = {
      test_runtime_deps = [ "$_data_dir/dockyard_host" ]
    }
  }
}

group("tests") {
  testonly = true
  deps = [ ":harvester_dockyard_test($host_toolchain)" ]
}
