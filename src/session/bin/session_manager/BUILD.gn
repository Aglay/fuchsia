# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/package.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")
import("//build/test/test_package.gni")
import("//third_party/cobalt/metrics_registry.gni")

metrics_registry("session_framework_metrics_registry") {
  # This must match the ID of our Cobalt project as specified in:
  # third_party/cobalt_config/projects.yaml
  project_id = 2550112954
  generate_rust = true
  namespace = "cobalt_registry"
  generate_binarypb = false
}

rustc_library("lib") {
  name = "session_manager_lib"
  with_unit_tests = true
  edition = "2018"

  visibility = [
    "//src/session/bin/session_manager/*",
    "//src/session/tests/session_manager/*",
    "//src/session/examples/*",
    "//src/session/tools/*",
  ]

  deps = [
    ":session_framework_metrics_registry_rustlib",
    "//sdk/fidl/fuchsia.cobalt:fuchsia.cobalt-rustc",
    "//sdk/fidl/fuchsia.component:fuchsia.component-rustc",
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//sdk/fidl/fuchsia.session:fuchsia.session-rustc",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/syslog/rust:syslog",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/session/lib/realm_management",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:argh",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:serde",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:thiserror",
  ]

  sources = [
    "src/cobalt.rs",
    "src/lib.rs",
    "src/service_management.rs",
    "src/startup.rs",
  ]
}

rustc_binary("bin") {
  name = "session_manager"
  edition = "2018"

  deps = [
    ":lib",
    "//src/lib/fuchsia-async",
    "//src/lib/syslog/rust:syslog",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:thiserror",
  ]

  sources = [ "src/main.rs" ]
}

package("session_manager") {
  deps = [
    ":bin",
    ":lib",
  ]

  meta = [
    {
      path = rebase_path("meta/session_manager.cml")
      dest = "session_manager.cm"
    },
  ]

  binaries = [
    {
      name = "session_manager"
    },
  ]
}

unittest_package("session_manager_tests") {
  deps = [ ":lib_test" ]

  tests = [
    {
      name = "session_manager_lib_lib_test"
      dest = "session_manager_tests"
      environments = basic_envs
    },
  ]
}

config_data("session_manager.config") {
  for_pkg = "sysmgr"
  sources = [ "//src/session/bin/session_manager/meta/session_manager.config" ]
  outputs = [ "session_manager.config" ]
}
