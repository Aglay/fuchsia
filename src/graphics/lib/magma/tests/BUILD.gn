# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")
import("//src/graphics/lib/magma/gnbuild/magma.gni")

group("tests") {
  testonly = true
  public_deps = [
    ":magma_abi_conformance_tests",
    ":magma_info_test",
    ":magma_nonhardware_tests",
    "codegen",
  ]

  # Add driver dependencies:
  public_deps += [
    "//garnet/drivers/gpu/msd-img-rgx/tests/integration:msd_img_rgx_integration_tests",
    "//garnet/drivers/gpu/msd-img-rgx/tests/unit_tests:msd_img_rgx_nonhardware_tests",
    "//garnet/lib/magma/gnbuild/magma-arm-mali:msd-arm-mali-integration-tests",
    "//garnet/lib/magma/gnbuild/magma-arm-mali:msd_arm_mali_nonhardware_tests",
    "//garnet/lib/magma/gnbuild/magma-img-rgx:magma-vulkan-no-hardware",
    "//garnet/lib/magma/gnbuild/magma-vsl-gc:msd_vsl_gc_nonhardware_tests",
    "//garnet/lib/magma/tests",
    "//garnet/packages/prod:vulkan",
  ]
  if (target_cpu == "x64") {
    public_deps += [
      "//garnet/lib/magma/gnbuild/magma-intel-gen:magma_intel_gen_integration_tests",
      "//garnet/lib/magma/gnbuild/magma-intel-gen:magma_intel_gen_nonhardware_tests",
    ]
  }

  # Ensure the test MSDs are built even when not deployed
  public_deps += [
    "//garnet/drivers/gpu/msd-arm-mali:msd_arm_test",
    "//garnet/drivers/gpu/msd-intel-gen:msd_intel_test",
    "//garnet/drivers/gpu/msd-vsl-gc:msd_vsl_test",
  ]
}

# Tests that require a working libvulkan to run.
group("vulkan-tests") {
  testonly = true
  public_deps = [
    # We also need run-test-component to run these tests.
    "//garnet/packages/testing:run_test_component",
    "//src/graphics/tests:vulkan-tests",
  ]
}

# Packages needed for L1 tests from //doc/the-book/magma/contributing.md
group("l1") {
  testonly = true
  deps = [
    ":tests",
    "//garnet/packages/examples:vkcube-on-fb",
    "//garnet/packages/examples:vkcube-on-scenic",
    "//garnet/packages/prod:scenic",
    "//garnet/packages/tests:image_pipe_swapchain",
    "//garnet/packages/tests:magma",
    "//src/graphics/lib/magma/src/tools",
    "//src/lib/vulkan/tests:image-pipe-swapchain-tests",
  ]
}

test_package("magma_nonhardware_tests") {
  deps = [ "unit_tests" ]

  tests = [
    {
      name = "magma_unit_tests"
      environments = basic_envs
    },
  ]
}

test_package("magma_abi_conformance_tests") {
  deps = [ "integration" ]

  tests = [
    {
      name = "magma_abi_conformance_tests"
      environments = magma_hardware_envs
    },
  ]
}

# Run magma_info as a test to make sure it doesn't crash.
test_package("magma_info_test") {
  deps = [ "//src/graphics/lib/magma/src/tools:magma_info" ]

  tests = [
    {
      name = "magma_info"
      dest = "magma_info_test"
      environments = magma_hardware_envs
    },
  ]
}
