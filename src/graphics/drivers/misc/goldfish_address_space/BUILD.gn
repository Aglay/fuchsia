# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/driver_package.gni")
import("//build/test.gni")
import("//build/test/test_package.gni")

common_deps = [
  "//sdk/banjo/ddk.protocol.goldfish.addressspace",
  "//sdk/banjo/ddk.protocol.pci",
  "//sdk/fidl/fuchsia.hardware.goldfish:fuchsia.hardware.goldfish_llcpp",
  "//src/devices/lib/driver",
  "//src/devices/lib/mmio",
  "//src/devices/pci/lib/device-protocol-pci",
  "//src/lib/ddk",
  "//src/lib/ddktl",
  "//zircon/public/lib/fbl",
  "//zircon/public/lib/fidl",
  "//zircon/public/lib/zircon-internal",
  "//zircon/public/lib/zx",
  "//zircon/system/ulib/fidl-async",
  "//zircon/system/ulib/fidl-utils",
  "//zircon/system/ulib/trace",
  "//zircon/system/ulib/trace:trace-driver",
]

driver_module("address_space_driver") {
  output_name = "goldfish_address_space"

  sources = [ "address_space_device.cc" ]

  deps = common_deps

  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [
    "//build/config/fuchsia:enable_zircon_asserts",
    "//build/config/fuchsia:static_cpp_standard_library",
  ]

  configs += [ "//build/config:all_source" ]
}

driver_package("goldfish_address_space") {
  deps = [ ":address_space_driver" ]

  drivers = [
    {
      name = "goldfish_address_space.so"
    },
  ]
}

test("goldfish-address-space-test") {
  output_name = "goldfish-address-space-test"
  sources = [
    "address_space_device.cc",
    "address_space_device_test.cc",
  ]
  deps = common_deps + [
           "//sdk/banjo/ddk.protocol.pci:ddk.protocol.pci_mock",
           "//src/devices/testing/fake-bti",
           "//src/devices/testing/fake_ddk",
           "//zircon/public/lib/mock-function",
           "//zircon/public/lib/sync",
           "//zircon/public/lib/zircon-internal",
           "//zircon/public/lib/zx",
           "//zircon/public/lib/zxtest",
         ]

  # TODO(fxb/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

unittest_package("goldfish-address-space-test-package") {
  package_name = "goldfish-address-space-test"
  deps = [ ":goldfish-address-space-test" ]

  tests = [
    {
      name = "goldfish-address-space-test"
      environments = basic_envs
    },
  ]
}

group("tests") {
  testonly = true
  deps = []

  if (target_cpu == "x64") {
    deps += [ ":goldfish-address-space-test-package" ]
  }
}
