# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/driver_package.gni")
import("//src/graphics/lib/magma/gnbuild/magma.gni")

VSI_VIP_DEVICE_ID = "PDEV_DID_VSI_VIP"

driver_package("msd-vsi-vip") {
  deps = [ ":msd_vsi" ]
  drivers = [
    {
      name = "libmsd_vsi.so"
    },
  ]
}

driver_package("msd-vsi-vip-test") {
  testonly = true
  deps = [ ":msd_vsi_test" ]
  drivers = [
    {
      name = "libmsd_vsi_test.so"
    },
  ]
}

driver_module("msd_vsi") {
  output_name = "libmsd_vsi"
  deps = [
    ":msd_vsi_entry",
    "$magma_build_root/src/magma_util/platform:logger_for_system_drivers",
    "src",
  ]

  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
}

driver_module("msd_vsi_test") {
  testonly = true
  output_name = "libmsd_vsi_test"
  deps = [
    ":msd_vsi_test_entry",
    "$magma_build_root/src/magma_util/platform:logger_for_system_drivers",
    "src",
  ]

  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
}

magma_pdev_entry("msd_vsi_entry") {
  pdev_device_id = VSI_VIP_DEVICE_ID
}

magma_pdev_entry("msd_vsi_test_entry") {
  pdev_device_id = VSI_VIP_DEVICE_ID
  test_entry = true
  disable_autobind = true
  deps = [
    "tests/unit_tests:hardware_tests",
    "//src/graphics/lib/magma/tests/unit_tests:magma_platform_hardware_tests",
    "//src/graphics/lib/magma/tests/unit_tests:msd_abi_conformance_tests",
  ]
}
