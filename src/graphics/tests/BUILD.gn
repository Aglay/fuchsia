# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test/test_package.gni")
import("//src/graphics/lib/magma/gnbuild/magma.gni")
import("//src/lib/vulkan/layers.gni")

group("tests") {
  testonly = true

  deps = [
    ":hw_tests",
    "goldfish_benchmark",
    "goldfish_test",
    "vkreadback:linux_arm64",
  ]
}

group("hw_tests") {
  testonly = true

  deps = [
    ":vulkan-tests",
    "../examples/vkprimer",
    "../examples/vkprimer/cmd-buf-benchmark",
    "../examples/vkprimer/test",
    "../examples/vkprimer/transaction-elimination-test",
    "benchmark",
    "vkvalidation:test-vk-validation",
    "//src/lib/vulkan/tests:image-pipe-swapchain-tests",
  ]
}

protected_libvulkan_envs = []

# Filter out everything that's not astro or sherlock.
foreach(env, magma_libvulkan_hardware_envs) {
  if (env == sherlock_env || env == astro_env) {
    protected_libvulkan_envs += [ env ]
  }
}

test_package("vulkan-tests") {
  deps = [
    "common:vkcontext",
    "icd_load",
    "vk_timer_query",
    "vkext",
    "vkloop",
    "vkpriority",
    "vkreadback",
  ]
  loadable_modules = vulkan_validation_layers.loadable_modules
  resources = vulkan_validation_layers.resources
  public_deps = vulkan_validation_layers.public_deps

  tests = [
    {
      name = "icd_load"
      environments = magma_libvulkan_hardware_envs
    },
    {
      name = "vkreadback"

      # TODO(fxbug.dev/47373): Restore astro_env when fxbug.dev/44570 is fixed.
      environments = [ aemu_env ] + magma_libvulkan_hardware_envs +
                     [ astro_env ] - [ astro_env ]
    },
    {
      name = "vkloop"
      environments = magma_libvulkan_hardware_envs
    },
    {
      name = "vkext"
      dest = "vkext_unprotected"
      environments = magma_libvulkan_hardware_envs
      log_settings = {
        max_severity = "ERROR"
      }
    },
    {
      name = "vkext"
      dest = "vkext_protected"
      environments = protected_libvulkan_envs
      log_settings = {
        max_severity = "ERROR"
      }
    },
    {
      name = "vkpriority"

      # No available hardware to run it on; cannot run with an Intel GPU.
      disabled = true
    },
    {
      name = "vk_timer_query_tests"
      environments = magma_libvulkan_arm_hardware_envs
    },
    {
      name = "vkcontext"
      environments = [ aemu_env ] + magma_libvulkan_hardware_envs
    },
  ]

  resources += [
    {
      path = "vkloop/comp.spv"
      dest = "vkloop.spv"
    },
  ]
}
