# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/unification/images/migrated_manifest.gni")

source_set("shared") {
  visibility = [ ":*" ]
  sources = [ "simple-display.cc" ]
  deps = [
    "//src/devices/lib/driver",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/ddktl",
    "//zircon/public/lib/device-protocol-pci",
    "//zircon/public/lib/mmio",
    "//zircon/system/banjo/ddk.protocol.display.controller",
    "//zircon/system/banjo/ddk.protocol.pci",
  ]
}

simple_drivers = [
  "bochs",
  "amd-kaveri",
  "nv",
  "intel",
]

foreach(driver, simple_drivers) {
  driver_module("simple.$driver") {
    defines = [ "_ALL_SOURCE" ]
    configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
    configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
    configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
    configs += [ "//build/unification/config:zircon-migrated" ]
    sources = [ "simple-$driver.c" ]
    deps = [
      ":shared",
      "//zircon/public/lib/ddk",
      "//zircon/public/lib/device-protocol-pci",
      "//zircon/system/banjo/ddk.protocol.display.controller",
      "//zircon/system/banjo/ddk.protocol.pci",
    ]
  }
  migrated_manifest("simple-$driver-manifest") {
    deps = [ ":simple.$driver" ]
  }
}
