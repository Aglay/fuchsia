# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test.gni")
import("//build/test/test_package.gni")
import("//build/unification/images/migrated_manifest.gni")

test("display-core-unittests") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  defines = [ "_ALL_SOURCE" ]
  sources = [
    "display-test.cc",
    "fence-test.cc",
    "image-test.cc",
    "integration-test.cc",
  ]
  include_dirs = [ ".." ]
  deps = [
    ":base",
    "//sdk/banjo/ddk.protocol.composite",
    "//sdk/banjo/ddk.protocol.platform.device",
    "//sdk/banjo/ddk.protocol.sysmem",
    "//sdk/fidl/fuchsia.hardware.display:fuchsia.hardware.display_llcpp",
    "//zircon/public/lib/async-testing",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zircon-internal",
    "//zircon/system/dev/lib/fake_ddk",
    "//zircon/system/fidl/fuchsia-sysmem:llcpp",
  ]
}

static_library("base") {
  defines = [ "_ALL_SOURCE" ]
  sources = [
    "base.cc",
    "fidl_client.cc",
    "fidl_client.h",
  ]
  deps = [
    "//sdk/banjo/ddk.protocol.composite",
    "//sdk/banjo/ddk.protocol.platform.bus",
    "//sdk/banjo/ddk.protocol.platform.device",
    "//sdk/banjo/ddk.protocol.sysmem",
  ]
  public_deps = [
    "//sdk/fidl/fuchsia.hardware.display:fuchsia.hardware.display_llcpp",
    "//src/devices/sysmem/drivers/sysmem:sysmem_common_srcs",
    "//src/graphics/display/drivers/display:shared",
    "//src/graphics/display/drivers/fake:fake-lib",
    "//zircon/public/lib/async-testing",
    "//zircon/public/lib/zxtest",
    "//zircon/system/dev/lib/fake-bti",
    "//zircon/system/dev/lib/fake_ddk",
    "//zircon/system/fidl/fuchsia-sysmem:llcpp",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":display-core-unittests-package" ]
}

unittest_package("display-core-unittests-package") {
  package_name = "display-core-unittests"
  deps = [ ":display-core-unittests" ]

  tests = [
    {
      name = "display-core-unittests"
      environments = basic_envs
    },
  ]
}
