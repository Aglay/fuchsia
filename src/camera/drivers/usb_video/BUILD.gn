# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/driver_package.gni")
import("//build/test.gni")
import("//build/test/test_package.gni")

driver_module("usb_video_driver") {
  output_name = "usb_video"

  sources = [
    "camera_control_impl.cc",
    "camera_control_impl.h",
    "usb-video-stream.cc",
    "usb-video-stream.h",
    "usb-video.cc",
    "usb-video.h",
    "uvc_format.cc",
    "uvc_format.h",
    "video-util.c",
    "video-util.h",
  ]

  deps = [
    "//sdk/banjo/ddk.protocol.usb",
    "//sdk/fidl/fuchsia.camera",
    "//sdk/fidl/fuchsia.hardware.camera:fuchsia.hardware.camera_c",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/devices/lib/driver",
    "//src/devices/usb/lib/usb",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/async-default",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fzl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
  ]

  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
}

driver_package("usb_video") {
  deps = [ ":usb_video_driver" ]

  drivers = [
    {
      name = "usb_video.so"
    },
  ]
}

test("usb-video-test") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "usb-video-test"
  sources = [
    "uvc_format.cc",
    "uvc_format_test.cc",
  ]
  deps = [
    "//sdk/banjo/ddk.protocol.i2c",
    "//sdk/fidl/fuchsia.camera",
    "//sdk/fidl/fuchsia.hardware.camera:fuchsia.hardware.camera_c",
    "//sdk/fidl/fuchsia.hardware.i2c:fuchsia.hardware.i2c_llcpp",
    "//src/devices/i2c/lib/device-protocol-i2c",
    "//src/devices/testing/fake_ddk",
    "//src/devices/usb/lib/usb",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fzl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
  ]
}

unittest_package("usb-video-test-package") {
  package_name = "usb-video-test"
  deps = [ ":usb-video-test" ]

  tests = [
    {
      name = "usb-video-test"
      environments = basic_envs
    },
  ]
}

group("tests") {
  testonly = true
  deps = [ ":usb-video-test-package" ]
}
