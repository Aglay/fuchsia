# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/driver_package.gni")

bind_rules("aml_mipi_bind") {
  rules = "aml_mipi.bind"
  output = "aml_mipi-bind.h"
  tests = "tests.json"
  deps = [ "//src/devices/bind/fuchsia.platform" ]
}

group("tests") {
  testonly = true
  deps = [ ":aml_mipi_bind_test" ]
}

driver_module("aml-mipicsi-driver") {
  output_name = "aml-mipicsi"
  sources = [
    "aml-mipi-adap.cc",
    "aml-mipi-debug.cc",
    "aml-mipi-regs.h",
    "aml-mipi.cc",
    "aml-mipi.h",
  ]
  deps = [
    ":aml_mipi_bind",
    "//sdk/banjo/ddk.protocol.mipicsi",
    "//sdk/banjo/ddk.protocol.platform.bus",
    "//sdk/banjo/ddk.protocol.platform.device",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/bus/lib/device-protocol-platform-device",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/fzl",
  ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

driver_package("aml-mipicsi") {
  deps = [ ":aml-mipicsi-driver" ]

  drivers = [
    {
      name = "aml-mipicsi.so"
    },
  ]
}
