# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/fidl/fidl.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")
import("//src/sys/build/components.gni")

fidl("fuchsia.time") {
  sources = [ "utc.fidl" ]
  public_deps = [ "//zircon/vdso/zx" ]
}

fuchsia_package("timekeeper") {
  components = [ ":service" ]
}

fuchsia_component("service") {
  component_name = "timekeeper"
  manifest = "meta/service.cmx"
  deps = [ ":bin" ]
}

config_data("timekeeper_config") {
  for_pkg = "sysmgr"
  outputs = [ "timekeeper.config" ]
  sources = [ "service.config" ]
}

rustc_binary("bin") {
  name = "timekeeper"
  edition = "2018"
  with_unit_tests = true

  deps = [
    ":fuchsia.time-rustc",
    "//sdk/fidl/fuchsia.deprecatedtimezone:fuchsia.deprecatedtimezone-rustc",
    "//sdk/fidl/fuchsia.net:fuchsia.net-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/inspect/rust/fuchsia-inspect",
    "//src/lib/syslog/rust:syslog",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:chrono",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:lazy_static",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:parking_lot",
    "//third_party/rust_crates:thiserror",
  ]
}

fuchsia_component("unittests") {
  component_name = "timekeeper-unittests"
  manifest = "meta/unittests.cmx"
  testonly = true
  deps = [ ":bin_test" ]
  resources = [
    {
      source = "$root_out_dir/timekeeper_bin_test"
      destination = "bin/unittests"
    },
    {
      source = "test/y2k"
    },
    {
      source = "test/end-of-unix-time"
    },
  ]
}

fuchsia_test_package("timekeeper-tests") {
  test_components = [ ":unittests" ]
}

group("tests") {
  testonly = true
  deps = [ ":timekeeper-tests" ]
}
