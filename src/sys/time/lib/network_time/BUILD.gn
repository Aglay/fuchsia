# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

group("tests") {
  testonly = true
  deps = [ ":network-time-tests" ]
}

test_package("network-time-tests") {
  deps = [ ":network_time_unittests" ]

  tests = [
    {
      name = "network_time_tests"
      environments = basic_envs
    },
  ]
}

source_set("lib") {
  sources = [
    "roughtime_server.cc",
    "roughtime_server.h",
    "system_time_updater.cc",
    "system_time_updater.h",
    "time_server_config.cc",
    "time_server_config.h",
  ]

  deps = [
    ":time_util_lib",
    "//sdk/fidl/fuchsia.hardware.rtc",
    "//sdk/lib/fdio",
    "//sdk/lib/syslog/cpp",
    "//sdk/lib/syslog/cpp:backend_legacy",
    "//src/lib/fxl",
    "//third_party/boringssl",
    "//third_party/roughtime:client_lib",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/zx",

    # TODO(57392): Move it back to //third_party once unification completes.
    "//zircon/third_party/rapidjson",
  ]
}

static_library("time_util_lib") {
  output_name = "time_util_lib"
  sources = [
    "time_util.cc",
    "time_util.h",
  ]
  deps = [ "//sdk/fidl/fuchsia.hardware.rtc" ]
}

executable("network_time_unittests") {
  output_name = "network_time_tests"

  testonly = true

  sources = [
    "roughtime_server_test.cc",
    "time_server_config_test.cc",
  ]

  deps = [
    ":lib",
    "//src/lib/files",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/boringssl",
    "//third_party/roughtime:client_lib",
    "//zircon/public/lib/zx",
  ]
}
