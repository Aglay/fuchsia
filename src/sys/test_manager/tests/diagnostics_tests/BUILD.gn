# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")
import("//src/sys/build/components.gni")

rustc_binary("inspect_publisher_bin") {
  name = "inspect_publisher"
  source_root = "inspect_publisher.rs"
  deps = [
    "//src/lib/diagnostics/inspect/rust",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
  ]

  sources = [ "inspect_publisher.rs" ]
}

rustc_test("diagnostics_test_bin") {
  name = "diagnostics_test"
  source_root = "main.rs"
  edition = "2018"
  deps = [
    "//src/lib/diagnostics/inspect/rust",
    "//src/lib/diagnostics/reader/rust",
    "//src/lib/fuchsia-async",
  ]

  sources = [ "main.rs" ]
}

fuchsia_component("diagnostics-test-driver") {
  testonly = true
  manifest = "meta/driver.cml"
  deps = [ ":diagnostics_test_bin" ]
}

fuchsia_component("inspect-publisher") {
  component_name = "inspect-publisher"
  testonly = true
  manifest = "meta/inspect-publisher.cml"
  deps = [ ":inspect_publisher_bin" ]
}

fuchsia_component("test-root") {
  component_name = "test-root"
  testonly = true
  manifest = "meta/test-root.cml"
}

fuchsia_test_package("test-manager-diagnostics-tests") {
  test_components = [ ":test-root" ]

  deps = [
    ":diagnostics-test-driver",
    ":inspect-publisher",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":test-manager-diagnostics-tests" ]
}
