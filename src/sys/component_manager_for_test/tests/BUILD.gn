# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/component/config.gni")
import("//build/rust/rustc_test.gni")
import("//src/sys/build/components.gni")

rustc_test("bin") {
  name = "component_manager_for_test_integration_test"
  edition = "2018"
  source_root = "src/main.rs"

  deps = [
    "//sdk/fidl/fuchsia.sys:fuchsia.sys-rustc",
    "//sdk/fidl/fuchsia.test:fuchsia.test-rustc",
    "//sdk/fidl/fuchsia.test.manager:fuchsia.test.manager-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:anyhow",
  ]

  sources = [ "src/main.rs" ]
}

component_config("component_manager_config") {
  sources = [ "config.json" ]
}

resource("component_manager_config_resource") {
  deps = [ ":component_manager_config" ]
  sources = [ "${target_gen_dir}/component_manager_config" ]
  outputs = [ "data/component_manager_config" ]
}

fuchsia_component("component_manager_for_test_integration_test") {
  testonly = true
  manifest = "meta/component_manager_for_test_integration_test.cmx"
  deps = [
    ":bin",
    ":component_manager_config_resource",
    "//examples/tests:passing-test-example",
    "//src/sys/component_manager_for_test:component_manager_for_test_cmp",
  ]
}

rustc_test("debug_data_test_bin") {
  testonly = true
  name = "debug_data_test"
  source_root = "src/debug_data_test.rs"

  deps = [
    "//sdk/fidl/fuchsia.debugdata:fuchsia.debugdata-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/syslog/rust:syslog",
    "//src/lib/zircon/rust:fuchsia-zircon-status",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:matches",
    "//third_party/rust_crates:thiserror",
  ]

  sources = [ "src/debug_data_test.rs" ]
}

fuchsia_component("debug_data_test") {
  testonly = true
  manifest = "meta/debug_data_test.cml"
  deps = [ ":debug_data_test_bin" ]
}

fuchsia_test_package("component-manager-for-test-integration-test") {
  test_components = [
    ":component_manager_for_test_integration_test",
    ":debug_data_test",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":component-manager-for-test-integration-test",
    "//src/sys/component_manager_for_test",
  ]
}
