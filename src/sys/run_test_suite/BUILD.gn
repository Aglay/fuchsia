# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package/component.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")
import("//build/test/test_package.gni")

rustc_library("lib") {
  name = "run_test_suite_lib"
  with_unit_tests = true
  edition = "2018"

  # Only to be used by internal bin and tests.
  visibility = [
    ":*",
    "tests/*",
    "//src/sys/test_adapters/gtest/tests/*",
  ]

  deps = [
    "//garnet/public/rust/fuchsia-component",
    "//sdk/fidl/fuchsia.sys:fuchsia.sys-rustc",
    "//src/lib/fuchsia-async",
    "//src/lib/test_executor/rust:test_executor",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:thiserror",
  ]
}

rustc_binary("bin") {
  name = "run_test_suite_bin"
  with_unit_tests = true
  edition = "2018"

  deps = [
    ":lib",
    "//src/lib/fuchsia-async",
  ]
}

package("run_test_suite") {
  testonly = true
  deps = [
    ":bin",
  ]

  binaries = [
    {
      name = "run-test-suite"
      source = "run_test_suite_bin"
      shell = true
    },
  ]
}

group("tests") {
  testonly = true
  deps = [
    "tests",
  ]
}
