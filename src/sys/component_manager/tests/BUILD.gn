# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package/component.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")
import("//build/rust/rustc_test.gni")
import("//build/test/test_package.gni")

rustc_library("test_utils") {
  name = "test_utils"
  edition = "2018"
  source_root = "test_utils.rs"
  deps = [
    "//garnet/public/rust/fdio",
    "//garnet/public/rust/fuchsia-component",
    "//garnet/public/rust/fuchsia-runtime",
    "//sdk/fidl/fuchsia.sys:fuchsia.sys-rustc",
    "//third_party/rust_crates:failure",
  ]
}

rustc_test("routing_integration_test_bin") {
  name = "routing_integration_test"
  edition = "2018"
  source_root = "routing_integration_test.rs"
  deps = [
    ":test_utils",
    "//garnet/lib/rust/cm_fidl_translator",
    "//garnet/public/rust/fdio",
    "//garnet/public/rust/fuchsia-async",
    "//garnet/public/rust/fuchsia-component",
    "//garnet/public/rust/fuchsia-runtime",
    "//sdk/fidl/fuchsia.sys:fuchsia.sys-rustc",
    "//third_party/rust_crates:failure",
  ]
}

rustc_test("shutdown_integration_test_bin") {
  name = "shutdown_integration_test"
  edition = "2018"
  source_root = "shutdown_integration.rs"
  deps = [
    ":test_utils",
    "//garnet/public/rust/fuchsia-async",
    "//third_party/rust_crates:failure",
  ]
}

rustc_binary("system_controller_client") {
  name = "system_controller_client"
  edition = "2018"
  source_root = "system_controller_client.rs"
  deps = [
    "//garnet/public/rust/fuchsia-async",
    "//garnet/public/rust/fuchsia-component",
    "//garnet/public/rust/fuchsia-syslog",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2-rustc",
    "//third_party/rust_crates:failure",
  ]
}

rustc_binary("mock_pkg_resolver_bin") {
  name = "mock_pkg_resolver"
  edition = "2018"
  source_root = "mock_pkg_resolver.rs"
  deps = [
    "//garnet/public/lib/fidl/rust/fidl",
    "//garnet/public/rust/fdio",
    "//garnet/public/rust/fuchsia-async",
    "//garnet/public/rust/fuchsia-component",
    "//garnet/public/rust/fuchsia-syslog",
    "//garnet/public/rust/fuchsia-zircon",
    "//sdk/fidl/fuchsia.pkg:fuchsia.pkg-rustc",
    "//src/sys/lib/fuchsia_url:fuchsia_url",
    "//third_party/rust_crates:failure",
    "//third_party/rust_crates:futures-preview",
    "//third_party/rust_crates:lazy_static",
    "//zircon/system/fidl/fuchsia-io:fuchsia-io-rustc",
  ]
}

test_package("shutdown_integration_test") {
  deps = [
    ":mock_pkg_resolver_bin",
    ":shutdown_integration_test_bin",
    ":system_controller_client",
    ":test_utils",
  ]

  components =
      [ "//src/sys/component_manager:component_manager_as_v1_component" ]

  meta = [
    {
      path = rebase_path(
              "meta/mock_pkg_resolver_for_shutdown_integration_test.cmx")
      dest = "mock_pkg_resolver.cmx"
    },
    {
      path = rebase_path("meta/shutdown_integration_root.cml")
      dest = "shutdown_integration_root.cm"
    },
    {
      path = rebase_path("meta/system_controller_consumer.cml")
      dest = "system_controller_consumer.cm"
    },
  ]

  binaries = [
    {
      name = "mock_pkg_resolver"
    },
    {
      name = "system_controller_client"
    },
  ]

  tests = [
    {
      name = "shutdown_integration_test"
    },
  ]
}

test_package("routing_integration_test") {
  deps = [
    ":mock_pkg_resolver_bin",
    ":routing_integration_test_bin",
    ":test_utils",
    "//examples/components/routing/echo_client",
    "//examples/components/routing/echo_server",
  ]

  components =
      [ "//src/sys/component_manager:component_manager_as_v1_component" ]

  meta = [
    {
      path =
          rebase_path("meta/mock_pkg_resolver_for_routing_integration_test.cmx")
      dest = "mock_pkg_resolver.cmx"
    },
    {
      path = rebase_path("//examples/components/routing/meta/echo_server.cml")
      dest = "echo_server.cm"
    },
    {
      path = rebase_path("//examples/components/routing/meta/echo_client.cml")
      dest = "echo_client.cm"
    },
    {
      path = rebase_path("meta/echo_realm.cml")
      dest = "echo_realm.cm"
    },
  ]

  binaries = [
    {
      name = "mock_pkg_resolver"
    },
    {
      name = "echo_server"
    },
    {
      name = "echo_client"
    },
  ]

  tests = [
    {
      name = "routing_integration_test"
    },
  ]
}

rustc_test("elf_runner_test_bin") {
  name = "elf_runner_test"
  edition = "2018"
  source_root = "elf_runner_test.rs"
  deps = [
    "//garnet/lib/rust/cm_fidl_translator",
    "//garnet/public/rust/fdio",
    "//garnet/public/rust/fuchsia-async",
    "//garnet/public/rust/fuchsia-component",
    "//garnet/public/rust/fuchsia-runtime",
    "//garnet/public/rust/fuchsia-zircon",
    "//sdk/fidl/fuchsia.data:fuchsia.data-rustc",
    "//sdk/fidl/fuchsia.sys:fuchsia.sys-rustc",
    "//third_party/rust_crates:failure",
  ]
}

test_package("elf_runner_test") {
  deps = [
    ":elf_runner_test_bin",
    ":mock_pkg_resolver_bin",
    "//examples/components/basic:echo_args_bin",
  ]

  components =
      [ "//src/sys/component_manager:component_manager_as_v1_component" ]

  meta = [
    {
      path = rebase_path("meta/mock_pkg_resolver_for_elf_runner_test.cmx")
      dest = "mock_pkg_resolver.cmx"
    },
    {
      path = rebase_path("meta/echo_no_args.cml")
      dest = "echo_no_args.cm"
    },
    {
      path = rebase_path("//examples/components/basic/meta/echo_args.cml")
      dest = "echo_args.cm"
    },
  ]

  binaries = [
    {
      name = "mock_pkg_resolver"
    },
    {
      name = "echo_args"
    },
  ]

  tests = [
    {
      name = "elf_runner_test"
    },
  ]
}

rustc_test("no_pkg_resolver_test_bin") {
  name = "no_pkg_resolver_test"
  edition = "2018"
  source_root = "no_pkg_resolver_test.rs"
  deps = [
    "//garnet/public/rust/fuchsia-async",
    "//src/sys/component_manager:lib",
  ]
  non_rust_deps = [
    "//zircon/public/lib/async",
    "//zircon/public/lib/async-default",
    "//zircon/public/lib/async-loop",
    "//zircon/public/lib/async-loop-default",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/memfs",
  ]
}

test_package("no_pkg_resolver_test") {
  deps = [
    ":no_pkg_resolver_test_bin",
  ]

  tests = [
    {
      name = "no_pkg_resolver_test"
    },
  ]
}

group("tests") {
  testonly = true

  deps = [
    ":elf_runner_test",
    "hub:hub_integration_test",
    ":no_pkg_resolver_test",
    ":routing_integration_test",
    ":shutdown_integration_test",
    "collections:collections_integration_test",
    "destruction:destruction_integration_test",
    "storage:storage_integration_test",
  ]
}
