# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/rust/rustc_library.gni")
import("//src/sys/build/components.gni")

# `ota-dependency-checker` is conceptually a binary, but it built as a lib here so
# it can be aggregated into //src/sys/pkg/bin/grand-swd-binary to save disk space.
rustc_library("lib") {
  name = "ota-dependency-checker"
  with_unit_tests = true
  edition = "2018"

  deps = [
    "//sdk/fidl/fuchsia.paver:fuchsia.paver-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/syslog/rust:syslog",

    # TODO(fxbug.dev/63642) remove the health check library, move all logic and testing here.
    "//src/sys/pkg/lib/system-health-check",
    "//third_party/rust_crates:anyhow",
  ]
  source_root = "src/main.rs"
  sources = [ "src/main.rs" ]
  visibility = [
    ":*",
    "//src/sys/pkg/bin/grand-swd-binary:*",
  ]
}

fuchsia_component("ota-dependency-checker") {
  manifest = "meta/ota-dependency-checker.cml"
  deps = [ ":lib" ]
}

# TODO(fxbug.dev/64137) migrate to the `fuchsia_package` template. It's currently possible to
# do this by using the `resource` and `fuchsia_component` templates. However, doing so would put
# two copies of the binary (the original grand_swd_binary and the copied ota-dependency-checker)
# in the package. It would be surprising to see both binaries in the meta/contents, so we are
# sticking with the legacy template for now.
package("package") {
  package_name = "ota-dependency-checker"

  deps = [
    ":ota-dependency-checker",
    "//src/sys/pkg/bin/grand-swd-binary",
  ]

  binaries = [
    {
      name = "ota_dependency_checker"
      source = "grand_swd_binary"
      dest = "ota-dependency-checker"
    },
  ]

  meta = [
    {
      path = rebase_path("meta/ota-dependency-checker.cml")
      dest = "ota-dependency-checker.cm"
    },
  ]
}

fuchsia_component("test-component") {
  testonly = true
  component_name = "ota-dependency-checker-test"
  manifest = "meta/ota-dependency-checker-test.cml"
  deps = [ ":lib_test" ]
}

fuchsia_test_package("test-package") {
  package_name = "ota-dependency-checker-tests"
  test_components = [ ":test-component" ]
}

group("tests") {
  testonly = true
  public_deps = [ ":test-package" ]
}
