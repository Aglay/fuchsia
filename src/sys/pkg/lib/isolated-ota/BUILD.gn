# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can
# found in the LICENSE file.

import("//build/package.gni")
import("//build/rust/rustc_library.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

rustc_library("isolated-ota") {
  version = "0.1.0"
  edition = "2018"
  with_unit_tests = true

  deps = [
    "//src/lib/fdio/rust:fdio",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-runtime",
    "//src/lib/scoped_task",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:thiserror",
    "//zircon/system/fidl/fuchsia-io:fuchsia-io-rustc",
  ]

  test_deps = [
    "//src/lib/fuchsia-async",
    "//src/sys/pkg/lib/fuchsia-pkg-testing",
    "//src/sys/pkg/lib/pkgfs",
    "//src/sys/pkg/testing/blobfs-ramdisk",
    "//third_party/rust_crates:matches",
    "//third_party/rust_crates:openat",
  ]
}

# Include blobfs in the build, which comes from Zircon
generate_manifest("blobfs.manifest") {
  visibility = [ ":*" ]
  args = []
  foreach(pattern, [ "bin/blobfs" ]) {
    args += [ "--binary=" + pattern ]
  }
}
blobfs_manifest_outputs = get_target_outputs(":blobfs.manifest")
blobfs_manifest = blobfs_manifest_outputs[0]

test_package("isolated-ota-tests") {
  extra = [ blobfs_manifest ]
  deps = [
    ":blobfs.manifest",
    ":isolated-ota_test",
    "//garnet/go/src/grand_unified_binary",
  ]

  binaries = [
    {
      name = "pkgsvr"
      source = "grand_unified_binary"
    },
  ]

  tests = [
    {
      name = "isolated_ota_lib_test"
      dest = "isolated-ota-tests"
    },
  ]
}

group("tests") {
  testonly = true
  public_deps = [ ":isolated-ota-tests" ]
}
