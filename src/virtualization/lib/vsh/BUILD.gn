# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test/test_package.gni")

group("vsh") {
  testonly = true

  deps = [
    ":client",
    ":tests",
    ":util",
  ]
}

group("tests") {
  testonly = true

  deps = [ ":vsh_tests" ]
}

source_set("util") {
  sources = [
    "util.cc",
    "util.h",
  ]
  deps = [
    "//src/lib/fxl",
    "//third_party/protobuf:protobuf_lite",
    "//zircon/public/lib/zx",
  ]
}

source_set("client") {
  sources = [
    "client.cc",
    "client.h",
    "command_runner.cc",
    "command_runner.h",
  ]
  deps = [
    ":util",
    "//sdk/fidl/fuchsia.virtualization",
    "//src/lib/fxl",
    "//src/virtualization/packages/biscotti_guest/third_party/protos:vsh_cc_proto",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/zx",
  ]
}

executable("vsh_unittest") {
  visibility = [ "*:*" ]
  testonly = true

  sources = [
    "command_runner_unittest.cc",
    "util_unittest.cc",
  ]

  deps = [
    ":client",
    ":util",
    "//garnet/public/lib/gtest",
    "//sdk/fidl/fuchsia.virtualization",
    "//sdk/lib/sys/cpp/testing:unit",
    "//sdk/lib/virtualization/testing",
    "//src/lib/fxl/test:gtest_main",
    "//src/virtualization/packages/biscotti_guest/third_party/protos:vsh_cc_proto",
  ]

  # TODO(47042): UBSan has found an instance of undefined behavior in this target.
  # Disable UBSan for this target temporarily until it is migrated into CI/CQ.
  configs += [ "//build/config:temporarily_disable_ubsan_do_not_use" ]
}

test_package("vsh_tests") {
  deps = [ ":vsh_unittest" ]

  tests = [
    {
      name = "vsh_unittest"
      environments = basic_envs
    },
  ]
}
