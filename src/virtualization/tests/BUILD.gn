# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

group("tests") {
  testonly = true
  deps = [
    ":bin",
    ":guest_integration_tests",
    ":guest_integration_tests_utils",
    "virtio_block_test_util",
    "virtio_net_test_util",
    "virtio_rng_test_util",
    "virtio_vsock_test_util",
  ]
}

executable("bin") {
  visibility = [ ":*" ]
  output_name = "guest_integration_tests"
  testonly = true

  sources = [
    "enclosed_guest.cc",
    "enclosed_guest.h",
    "fake_input_only_scenic.cc",
    "fake_input_only_scenic.h",
    "guest_console.cc",
    "guest_console.h",
    "guest_test.h",
    "logger.cc",
    "logger.h",
    "main.cc",
    "mock_netstack.cc",
    "mock_netstack.h",
    "virtio_block_tests.cc",
    "virtio_input_tests.cc",
    "virtio_net_tests.cc",
    "virtio_vsock_tests.cc",
  ]

  deps = [
    "//garnet/lib/inet",
    "//garnet/public/lib/svc/cpp",
    "//sdk/fidl/fuchsia.netstack",
    "//sdk/fidl/fuchsia.ui.scenic",
    "//sdk/fidl/fuchsia.virtualization",
    "//sdk/lib/sys/cpp/testing:integration",
    "//src/lib/fxl",
    "//src/lib/fxl/test:test_settings",
    "//src/virtualization/bin/vmm/device",
    "//src/virtualization/bin/vmm/device:qcow_test_data",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/fbl",
  ]
}

test_package("guest_integration_tests") {
  deps = [
    ":bin",
  ]

  tests = [
    {
      name = "guest_integration_tests"
      environments = [
        nuc_env,
        vim2_env,
      ]
    },
  ]
}

package("guest_integration_tests_utils") {
  deps = [
    "virtio_block_test_util",
    "virtio_net_test_util",
    "virtio_rng_test_util",
    "virtio_vsock_test_util",
  ]

  meta = [
    {
      path =
          rebase_path("virtio_block_test_util/meta/virtio_block_test_util.cmx")
      dest = "virtio_block_test_util.cmx"
    },
    {
      path = rebase_path("virtio_rng_test_util/meta/virtio_rng_test_util.cmx")
      dest = "virtio_rng_test_util.cmx"
    },
    {
      path = rebase_path("virtio_net_test_util/meta/virtio_net_test_util.cmx")
      dest = "virtio_net_test_util.cmx"
    },
    {
      path =
          rebase_path("virtio_vsock_test_util/meta/virtio_vsock_test_util.cmx")
      dest = "virtio_vsock_test_util.cmx"
    },
  ]

  binaries = [
    {
      name = "virtio_block_test_util"
    },
    {
      name = "virtio_net_test_util"
    },
    {
      name = "virtio_rng_test_util"
    },
    {
      name = "virtio_vsock_test_util"
    },
  ]
}
