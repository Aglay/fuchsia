# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")
import("//build/test/test_package.gni")

rustc_binary("bin") {
  name = "regulatory_region"
  edition = "2018"
  deps = [
    "//garnet/public/rust/fuchsia-async",
    "//garnet/public/rust/fuchsia-component",
    "//garnet/public/rust/fuchsia-syslog",
    "//sdk/fidl/fuchsia.location:fuchsia.location.namedplace-rustc",
    "//third_party/rust_crates:failure",
    "//third_party/rust_crates:futures-preview",
  ]
}

rustc_library("lib") {
  name = "regulatory_region_lib"
  edition = "2018"
  deps = [
    "//garnet/public/rust/fuchsia-async",
    "//third_party/rust_crates:futures-test-preview",
  ]
  with_unit_tests = true
}

group("tests") {
  testonly = true
  deps = [
    ":integration_tests",
    ":unit_tests",
  ]
}

group("unit_tests") {
  testonly = true
  deps = [
    ":regulatory_region_tests",
  ]
}

group("integration_tests") {
  testonly = true
  deps = [
    "tests:tests",
  ]
}

package("regulatory_region") {
  deps = [
    ":bin",
  ]

  binaries = [
    {
      name = "regulatory_region"
    },
  ]

  meta = [
    {
      path = rebase_path("meta/regulatory_region.cmx")
      dest = "regulatory_region.cmx"
    },
  ]
}

unittest_package("regulatory_region_tests") {
  deps = [
    ":lib_test",
  ]

  tests = [
    {
      name = "regulatory_region_lib_lib_test"
    },
  ]
}
