# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

group("tests") {
  testonly = true
  public_deps = [
    ":weavestack_unittests",
    "//src/connectivity/network/netstack:debug",
  ]
}

test_package("weavestack_unittests") {
  deps = [
    ":config",
    ":unittests",
  ]

  tests = [
    {
      name = "weavestack_unittests"
      environments = basic_envs
    },
  ]

  resources = [
    {
      path = rebase_path("../data/default_environment_schema.json")
      dest = "default_environment_schema.json"
    },
  ]
}

executable("unittests") {
  output_name = "weavestack_unittests"

  testonly = true

  sources = [
    "app_test.cc",
    "stack_impl_test.cc",
    "stack_utils_test.cc",
  ]

  public_deps = [
    "//sdk/lib/sys/cpp/testing:unit",
    "//sdk/lib/syslog/cpp",
    "//src/connectivity/weave/weavestack:lib",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

config_data("config") {
  for_pkg = "weavestack_unittests"
  sources = [
    "testdata/default_environment.json",
    "testdata/device_info.json",
  ]
}
