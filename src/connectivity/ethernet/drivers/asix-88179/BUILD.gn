# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/fidl/fidl.gni")
import("//build/test.gni")
import("//build/unification/images/migrated_manifest.gni")

group("test") {
  testonly = true
  deps = [ ":usb-virtual-bus-asix-88179" ]
}

driver_module("asix-88179") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "asix-88179.cc" ]
  deps = [
    "//sdk/banjo/ddk.protocol.ethernet",
    "//sdk/banjo/ddk.protocol.usb",
    "//sdk/banjo/ddk.protocol.usb.request",
    "//src/devices/lib/dev-operation",
    "//src/devices/lib/driver",
    "//src/devices/usb/lib/usb",
    "//zircon/public/lib/cksum",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/pretty",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
  ]
}

fidl("asix-88179-test-helper") {
  name = "fuchsia.hardware.ax88179"
  sources = [ "hooks.test.fidl" ]
}

driver_module("fake-asix-88179") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "asix-88179-function.cc" ]
  deps = [
    ":asix-88179-test-helper",
    ":asix-88179-test-helper_llcpp",
    "//sdk/banjo/ddk.protocol.ethernet",
    "//sdk/banjo/ddk.protocol.usb",
    "//sdk/banjo/ddk.protocol.usb.function",
    "//sdk/banjo/ddk.protocol.usb.request",
    "//src/devices/lib/driver",
    "//src/devices/usb/lib/usb",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/system/ulib/inet6",
  ]
}

test("usb-virtual-bus-asix-88179") {
  # Dependent manifests unfortunately cannot be marked as `testonly`.
  # Remove when converting this file to proper GN build idioms.
  testonly = false
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "asix-88179-test.cc" ]
  deps = [
    ":asix-88179-test-helper_llcpp",
    "//sdk/fidl/fuchsia.hardware.ethernet:fuchsia.hardware.ethernet_llcpp",
    "//sdk/fidl/fuchsia.hardware.usb.virtual.bus:fuchsia.hardware.usb.virtual.bus_llcpp",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/fdio-caller",
    "//zircon/public/lib/fzl",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/fidl/fuchsia-device:llcpp",
    "//zircon/system/fidl/fuchsia-hardware-usb-peripheral:llcpp",
    "//zircon/system/ulib/hid",
    "//zircon/system/ulib/usb-virtual-bus-launcher",
  ]
}

migrated_manifest("asix-88179-manifest") {
  deps = [ ":asix-88179" ]
}

migrated_manifest("fake-asix-88179-manifest") {
  deps = [ ":fake-asix-88179" ]
}

migrated_manifest("usb-virtual-bus-asix-88179-manifest") {
  deps = [ ":usb-virtual-bus-asix-88179" ]
}
