# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/test/test_package.gni")

driver_module("ethernet") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "ethernet.cc" ]
  deps = [
    "//sdk/banjo/ddk.protocol.ethernet",
    "//sdk/fidl/fuchsia.hardware.ethernet:fuchsia.hardware.ethernet_c",
    "//src/devices/lib/dev-operation",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fidl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/system/ulib/fidl-utils",
    "//zircon/system/ulib/fzl",
  ]
}

test("ethernet-unittest") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "ethernet-unittest"
  sources = [
    "ethernet-test.cc",
    "ethernet.cc",
  ]
  deps = [
    "//sdk/banjo/ddk.protocol.ethernet",
    "//sdk/fidl/fuchsia.hardware.ethernet:fuchsia.hardware.ethernet_c",
    "//src/devices/lib/dev-operation",
    "//src/devices/testing/fake_ddk",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/fidl-utils",
    "//zircon/system/ulib/fzl",
  ]

  # TODO(46872): Fix the leaks and remove this.
  deps += [ "//build/config/sanitizers:suppress-lsan.DO-NOT-USE-THIS" ]
}

unittest_package("ethernet-unittest-package") {
  package_name = "ethernet-unittest"
  deps = [ ":ethernet-unittest" ]

  tests = [
    {
      name = "ethernet-unittest"
      environments = basic_envs
    },
  ]
}

group("tests") {
  testonly = true
  deps = [ ":ethernet-unittest-package" ]
}
