# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_library.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

rustc_library("hoist") {
  with_unit_tests = true
  edition = "2018"
  deps = [
    "//sdk/fidl/fuchsia.overnet:fuchsia.overnet-rustc",
    "//sdk/fidl/fuchsia.overnet.protocol:fuchsia.overnet.protocol-rustc",
    "//src/lib/fidl/rust/fidl",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:thiserror",
  ]
  test_deps = [ "//third_party/rust_crates:parking_lot" ]
  non_rust_deps = [ "//third_party/boringssl" ]
  if (is_fuchsia) {
    deps += [
      "//src/lib/fuchsia-async",
      "//src/lib/fuchsia-component",
      "//src/lib/syslog/rust:syslog",
    ]
  } else {
    deps += [
      "//src/connectivity/overnet/lib/core",
      "//src/lib/zircon/rust:fuchsia-zircon-status",
      "//third_party/rust_crates:async-std",
      "//third_party/rust_crates:chrono",
      "//third_party/rust_crates:lazy_static",
      "//third_party/rust_crates:log",
      "//third_party/rust_crates:rand",
      "//third_party/rust_crates:slab",
      "//third_party/rust_crates:tempfile",
    ]
  }
}

unittest_package("overnet-hoist-tests") {
  deps = [ ":hoist_test" ]

  tests = [
    {
      name = "hoist_lib_test"
      environments = [ emu_env ]
    },
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":hoist_test($host_toolchain)",
    ":overnet-hoist-tests",
  ]
}
