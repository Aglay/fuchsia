# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_library.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

rustc_library("integration_tests") {
  testonly = true
  with_unit_tests = true
  edition = "2018"
  deps = [
    "//garnet/examples/fidl/services:echo-rustc",
    "//sdk/fidl/fuchsia.overnet:fuchsia.overnet-rustc",
    "//sdk/fidl/fuchsia.overnet.protocol:fuchsia.overnet.protocol-rustc",
    "//src/connectivity/overnet/lib/core",
    "//src/connectivity/overnet/lib/hoist",
    "//src/lib/fidl/rust/fidl",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:parking_lot",
    "//third_party/rust_crates:thiserror",
    "//third_party/rust_crates:timebomb",
  ]
  non_rust_deps = [ "//third_party/boringssl" ]
  if (is_fuchsia) {
    deps += [
      "//src/lib/fuchsia-async",
      "//src/lib/zircon/rust:fuchsia-zircon",
    ]
  } else {
    deps += [ "//third_party/rust_crates:tokio" ]
  }
}

unittest_package("overnet-integration-tests") {
  deps = [ ":integration_tests_test" ]

  tests = [
    {
      name = "integration_tests_lib_test"
      environments = basic_envs
    },
  ]

  # TODO(ctiller): This is clearly insanity and we should not be hardcoding certificates
  # and private keys into the overnetstack package. Design and implement security.
  resources = [
    {
      path = rebase_path("//third_party/rust-mirrors/quiche/examples/cert.crt")
      dest = "cert.crt"
    },
    {
      path = rebase_path("//third_party/rust-mirrors/quiche/examples/cert.key")
      dest = "cert.key"
    },
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":integration_tests_test($host_toolchain)",
    ":overnet-integration-tests",
  ]
}
