# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/fuchsia/rules.gni")
import("//build/package.gni")

source_set("source") {
  public = [
    "convert.h",
    "device.h",
    "driver.h",
  ]

  sources = [
    "convert.cpp",
    "device.cpp",
    "driver.cpp",
  ]

  public_deps = [
    "//garnet/lib/wlan/fidl",
    "//garnet/lib/wlan/protocol",
    "//src/connectivity/wlan/drivers/lib",
    "//src/connectivity/wlan/lib/common/cpp:common",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/driver",
  ]
}

driver_module("wlanif_driver") {
  output_name = "wlanif"

  deps = [
    ":source",
  ]

  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [
    "//build/config/fuchsia:enable_zircon_asserts",
    "//build/config/fuchsia:static_cpp_standard_library",
  ]
}

package("wlanif") {
  deprecated_system_image = true

  deps = [
    ":wlanif_driver",
  ]

  drivers = [
    {
      name = "wlanif.so"
    },
  ]
}
