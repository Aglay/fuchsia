# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_test.gni")
import("//build/testing/environments.gni")
import("//src/sys/build/components.gni")

rustc_test("http_client_integration_test") {
  edition = "2018"

  deps = [
    "//sdk/fidl/fuchsia.mem:fuchsia.mem-rustc",
    "//sdk/fidl/fuchsia.net.http:fuchsia.net.http-rustc",
    "//sdk/fidl/fuchsia.sys:fuchsia.sys-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-runtime",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:rouille",
    "//third_party/rust_crates:thiserror",
  ]

  sources = [ "src/lib.rs" ]

  # TODO(58494): Fix the leaks and remove this.
  deps += [ "//build/config/sanitizers:suppress-lsan.DO-NOT-USE-THIS" ]
}

fuchsia_component("component") {
  component_name = "http-client-integration-test"
  testonly = true
  manifest = "meta/http_client_integration_test.cmx"
  deps = [ ":http_client_integration_test" ]
}

fuchsia_test_package("http-client-integration-test") {
  test_components = [ ":component" ]
  deps = [
    # The component under test.
    "../:component",
  ]
  test_specs = {
    environments = [ emu_env ]
  }
}

group("tests") {
  testonly = true
  deps = [ ":http-client-integration-test" ]
}
