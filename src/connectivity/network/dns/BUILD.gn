# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")
import("//build/test/test_package.gni")

rustc_library("lib") {
  name = "dns"
  with_unit_tests = true
  edition = "2018"
  deps = [
    "//src/lib/fuchsia-async",
    "//third_party/rust_crates:async-trait",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:trust-dns-proto",
    "//third_party/rust_crates:trust-dns-resolver",
  ]
}

test_package("dns_tests") {
  deps = [ ":lib_test" ]

  tests = [
    {
      name = "dns_lib_test"
      environments = [ qemu_env ]
    },
  ]
}

rustc_binary("bin") {
  name = "dns_resolver"
  with_unit_tests = true
  edition = "2018"

  deps = [
    ":lib",
    "//garnet/lib/rust/fidl_fuchsia_net_ext",
    "//src/lib/fuchsia-component",
    "//src/lib/syslog/rust:syslog",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:async-trait",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:trust-dns-proto",
    "//third_party/rust_crates:trust-dns-resolver",
  ]
}

package("dns_resolver") {
  deps = [ ":bin" ]
  binaries = [
    {
      name = "dns_resolver"
      dest = "dns_resolver"
    },
  ]
  meta = [
    {
      path = rebase_path("meta/dns_resolver.cmx")
      dest = "dns_resolver.cmx"
    },
  ]
}

unittest_package("dns_resolver_tests") {
  deps = [ ":bin_test" ]

  tests = [
    {
      name = "dns_resolver_bin_test"
      environments = [ qemu_env ]
    },
  ]
}

group("dns") {
  testonly = true
  public_deps = [
    ":dns_resolver",
    "//src/connectivity/network/dns/config",
  ]
}
