# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")
import("//src/sys/build/components.gni")

rustc_library("lib") {
  name = "dhcp"
  with_unit_tests = true
  edition = "2018"
  deps = [
    "//garnet/lib/rust/never",
    "//sdk/fidl/fuchsia.net:fuchsia.net-rustc",
    "//sdk/fidl/fuchsia.net.dhcp:fuchsia.net.dhcp-rustc",
    "//sdk/fidl/fuchsia.stash:fuchsia.stash-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/network/fidl_fuchsia_hardware_ethernet_ext",
    "//src/lib/network/fidl_fuchsia_net_ext",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:bytes",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:num-derive",
    "//third_party/rust_crates:num-traits",
    "//third_party/rust_crates:rand",
    "//third_party/rust_crates:serde",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:thiserror",
  ]

  test_deps = [ "//src/connectivity/lib/net-declare" ]

  sources = [
    "src/configuration.rs",
    "src/lib.rs",
    "src/protocol.rs",
    "src/server.rs",
    "src/stash.rs",
  ]
}

fuchsia_unittest_package("dhcp-tests") {
  manifest = "meta/dhcp_lib_test.cmx"
  deps = [ ":lib_test" ]
}

rustc_binary("bin") {
  name = "dhcpd"
  with_unit_tests = true
  edition = "2018"

  deps = [
    ":lib",
    "//sdk/fidl/fuchsia.net:fuchsia.net-rustc",
    "//sdk/fidl/fuchsia.net.dhcp:fuchsia.net.dhcp-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/syslog/rust:syslog",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:argh",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:getopts",
    "//third_party/rust_crates:libc",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:socket2",
    "//third_party/rust_crates:thiserror",
    "//third_party/rust_crates:void",
  ]

  test_deps = [
    "//sdk/fidl/fuchsia.stash:fuchsia.stash-rustc",
    "//src/connectivity/lib/net-declare",
    "//third_party/rust_crates:matches",
    "//third_party/rust_crates:rand",
  ]

  sources = [ "src/main.rs" ]
}

fuchsia_unittest_package("dhcpd-tests") {
  manifest = "meta/dhcpd_bin_test.cmx"
  deps = [ ":bin_test" ]
}

config_data("config-default") {
  for_pkg = "dhcpd"
  sources = [ "data/default_config.json" ]
  outputs = [ "config.json" ]
}

fuchsia_component("component") {
  component_name = "dhcpd"
  manifest = "meta/dhcpd.cmx"
  deps = [ ":bin" ]
}

fuchsia_package("dhcpd") {
  deps = [ ":component" ]
}

group("dhcp") {
  public_deps = [
    ":config-default",
    ":dhcpd",
    "sysmgr:config",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":dhcp-tests",
    ":dhcpd-tests",
  ]
}

config_data("configs-testing") {
  testonly = true
  for_pkg = "dhcpd-testing"
  sources = [
    "data/default_config.json",
    "data/test_config.json",
  ]
  outputs = [ "{{source_file_part}}" ]
}

fuchsia_package("package-testing") {
  testonly = true
  package_name = "dhcpd-testing"
  deps = [ ":component" ]
}

# Used by dhcpd-cli tests. Ideally we use fuchsia_component but dhcpd-cli
# is a shell binary which is not yet supported.
group("dhcpd-testing") {
  testonly = true
  deps = [
    ":configs-testing",
    ":package-testing",
  ]
}
