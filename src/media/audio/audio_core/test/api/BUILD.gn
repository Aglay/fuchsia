# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# This directory contains two kinds of audio_core integration tests, in two
# different binaries:
#
#   audio-core-api-tests
#     Validates the "control plane" subset of the API. These tests do not
#     check how data flows through the system.
#
#   audio-core-api-pipeline-tests
#     Validates the flow of audio data. Verifying correct data flow requires
#     timers, so these tests are run on real hardware only.
#     TODO(50645): this binary doesn't exist yet
#
# For convenience, both tests are available in the same package.

import("//build/test/test_package.gni")

test_package("audio-core-api-tests") {
  deps = [ ":audio-core-api-tests-bin" ]

  tests = [
    {
      name = "audio-core-api-tests"
      environments = basic_envs
    },
  ]
}

executable("audio-core-api-tests-bin") {
  testonly = true
  output_name = "audio-core-api-tests"

  sources = [
    "activity_reporter_test.cc",
    "audio_capturer_test.cc",
    "audio_renderer_test.cc",
    "audio_tuner_test.cc",
    "gain_control_test.cc",
    "usage_gain_reporter_test.cc",
    "usage_reporter_test.cc",
    "volume_control.cc",
  ]

  deps = [
    "//sdk/fidl/fuchsia.media",
    "//sdk/fidl/fuchsia.virtualaudio",
    "//sdk/lib/media/audio/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/media/audio/lib/clock:clone_mono",
    "//src/media/audio/lib/clock/testing",
    "//src/media/audio/lib/logging",
    "//src/media/audio/lib/test:hermetic_audio_test",
    "//third_party/googletest:gmock",
  ]
}
