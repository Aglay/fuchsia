# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be found in the LICENSE file.

import("//src/sys/build/components.gni")

source_set("constants") {
  sources = [ "constants.h" ]

  deps = [ "//src/media/audio/lib/format" ]
}

source_set("mixer") {
  sources = [
    "channel_strip.h",
    "coefficient_table.h",
    "filter.cc",
    "filter.h",
    "gain.cc",
    "gain.h",
    "mixer.cc",
    "mixer.h",
    "mixer_utils.h",
    "no_op.cc",
    "no_op.h",
    "output_producer.cc",
    "output_producer.h",
    "point_sampler.cc",
    "point_sampler.h",
    "position_manager.cc",
    "position_manager.h",
    "sinc_sampler.cc",
    "sinc_sampler.h",
  ]

  public_deps = [
    ":constants",
    "//sdk/fidl/fuchsia.media",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/ffl",
    "//zircon/system/ulib/trace",
  ]

  deps = [
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl:fxl",
    "//src/media/audio/lib/format",
    "//src/media/audio/lib/timeline",
  ]

  # Some of these components can see massive slow downs without the benefit of optimizations. The
  # additional CPU load caused by running these without optimizations enabled can result in audio
  # glitches on debug builds. This results in adding proper optimizations while maintaining other
  # aspects of the debug configuration (ex: DCHECK is still enabled).
  configs -= [ "//build/config:default_optimize" ]
  configs += [ "//build/config:optimize_speed" ]

  # Allow some of our loops to be vectorized by the compiler.
  cflags = [
    "-ffast-math",

    # Allow inf/nan until we sanitize these out of streams (not normally allowed with -ffast-math).
    "-fhonor-infinities",
    "-fhonor-nans",
    "-fno-finite-math-only",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":audio_mixer_unittests",
    "test",
  ]
}

fuchsia_unittest_package("audio_mixer_unittests") {
  deps = [ ":unittest_bin" ]
}

executable("unittest_bin") {
  testonly = true
  output_name = "audio_mixer_unittests"

  sources = [
    "bookkeeping_unittest.cc",
    "channel_strip_unittest.cc",
    "coefficient_table_unittest.cc",
    "filter_unittest.cc",
    "gain_unittest.cc",
    "mixer_utils_unittest.cc",
    "no_op_unittest.cc",
    "output_producer_unittest.cc",
    "point_sampler_unittest.cc",
    "position_manager_unittest.cc",
    "sinc_sampler_unittest.cc",
    "source_info_unittest.cc",
  ]

  deps = [
    ":mixer",
    "//src/lib/fxl/test:gtest_main",
    "//src/media/audio/lib/format",
    "//third_party/googletest:gmock",
    "//zircon/public/lib/fbl",
  ]
}

group("tools") {
  testonly = true
  deps = [ "tools" ]
}
