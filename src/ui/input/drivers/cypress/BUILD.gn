# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/test/test_package.gni")

bind_rules("cypress_cy8cmbr3108-bind") {
  rules = "cypress_cy8cmbr3108.bind"
  output = "cypress_cy8cmbr3108-bind.h"
  tests = "tests.json"
  deps = [
    "//src/devices/bind/fuchsia.composite",
    "//src/devices/bind/fuchsia.synaptics.platform",
  ]
}

driver_module("cypress") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "cy8cmbr3108.cc" ]
  deps = [
    ":cypress_cy8cmbr3108-bind",
    "//sdk/banjo/ddk.protocol.composite",
    "//sdk/banjo/ddk.protocol.gpio",
    "//sdk/banjo/ddk.protocol.hidbus",
    "//src/devices/i2c/lib/device-protocol-i2c",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/hid",
    "//zircon/system/ulib/hwreg-i2c",
  ]
}

test("cypress-touch-test") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  output_name = "cypress-touch-test"
  configs += [ "//build/config:all_source" ]
  sources = [
    "cy8cmbr3108-test.cc",
    "cy8cmbr3108.cc",
  ]
  deps = [
    ":cypress_cy8cmbr3108-bind",
    "//sdk/banjo/ddk.protocol.composite",
    "//sdk/banjo/ddk.protocol.gpio",
    "//sdk/banjo/ddk.protocol.gpio:ddk.protocol.gpio_mock",
    "//sdk/banjo/ddk.protocol.hidbus",
    "//src/devices/i2c/lib/device-protocol-i2c",
    "//src/devices/i2c/testing/mock-i2c",
    "//src/devices/testing/no_ddk",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/ui/input/testing/fake-hidbus-ifc",
    "//src/ui/input/testing/mock-hidbus-ifc",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/mock-function",
    "//zircon/public/lib/zircon-internal",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/hid",
    "//zircon/system/ulib/hwreg-i2c",
  ]
}

unittest_package("cypress-touch-test-package") {
  package_name = "cypress-touch-test"
  deps = [ ":cypress-touch-test" ]

  tests = [
    {
      name = "cypress-touch-test"
      environments = basic_envs
    },
  ]
}
group("tests") {
  testonly = true
  deps = [
    ":cypress-touch-test-package",
    ":cypress_cy8cmbr3108-bind_test",
  ]
}
