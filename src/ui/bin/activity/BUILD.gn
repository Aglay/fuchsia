# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/package.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

source_set("lib") {
  sources = [
    "activity_app.cc",
    "activity_app.h",
    "activity_state_machine.cc",
    "activity_state_machine.h",
    "activity_tracker_connection.cc",
    "activity_tracker_connection.h",
    "state_machine_driver.cc",
    "state_machine_driver.h",
  ]
  public_deps = [
    "//sdk/fidl/fuchsia.ui.activity",
    "//src/lib/fxl",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/zx",
  ]
}

executable("activity") {
  output_name = "activity"

  sources = [
    "main.cc",
  ]

  deps = [
    ":lib",
    "//sdk/lib/sys/cpp",
  ]
}

package("activity_pkg") {
  deps = [
    ":activity",
  ]

  binaries = [
    {
      name = "activity"
      shell = true
    },
  ]

  meta = [
    {
      path = rebase_path("meta/activity.cmx")
      dest = "activity.cmx"
    },
  ]
}

executable("tests") {
  testonly = true
  output_name = "activity_test"

  sources = [
    "activity_app_test.cc",
    "activity_state_machine_test.cc",
    "activity_tracker_connection_test.cc",
    "state_machine_driver_test.cc",
  ]
  deps = [
    ":lib",
    "//garnet/public/lib/gtest",
    "//third_party/googletest:gtest_main",
  ]
}

test_package("activity_test_pkg") {
  deps = [
    ":tests",
  ]
  tests = [
    {
      name = "activity_test"
    },
  ]
}
