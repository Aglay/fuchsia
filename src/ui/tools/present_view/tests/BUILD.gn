# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/sys/build/components.gni")

group("tests") {
  testonly = true

  deps = [
    ":present_view_apptests",
    ":present_view_unittests",
  ]

  # TODO(fxbug.dev/70869): Re-enable this test in profile build when it passes
  # consistently in profile build.
  if (select_variant + [ "profile" ] - [ "profile" ] == select_variant) {
    deps += [ ":present_view_integration_tests" ]
  }
}

fuchsia_unittest_package("present_view_apptests") {
  deps = [ ":present_view_apptests_bin" ]
}

fuchsia_unittest_package("present_view_integration_tests") {
  manifest = "meta/present_view_integration_tests.cmx"
  deps = [ ":present_view_integration_tests_bin" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

fuchsia_unittest_package("present_view_unittests") {
  deps = [ ":present_view_unittests_bin" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

executable("present_view_unittests_bin") {
  output_name = "present_view_unittests"
  testonly = true

  sources = [ "present_view_unittest.cc" ]

  deps = [
    "//garnet/public/lib/gtest",
    "//sdk/fidl/fuchsia.sys",
    "//sdk/fidl/fuchsia.ui.app",
    "//sdk/fidl/fuchsia.ui.policy",
    "//sdk/fidl/fuchsia.ui.views",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp/testing:unit",
    "//src/lib/fsl",
    "//src/lib/fxl/test:gtest_main",
    "//src/ui/tools/present_view:lib",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
  ]
}

executable("present_view_apptests_bin") {
  output_name = "present_view_apptests"
  testonly = true

  sources = [ "present_view_apptest.cc" ]

  deps = [
    "//garnet/public/lib/gtest",
    "//sdk/fidl/fuchsia.sys",
    "//sdk/fidl/fuchsia.ui.app",
    "//sdk/fidl/fuchsia.ui.policy",
    "//sdk/fidl/fuchsia.ui.views",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp/testing:integration",
    "//src/lib/fsl",
    "//src/lib/fxl/test:gtest_main",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
  ]
}

executable("present_view_integration_tests_bin") {
  output_name = "present_view_integration_tests"
  testonly = true

  sources = [ "present_view_integration_test.cc" ]

  deps = [
    "//garnet/public/lib/gtest",
    "//sdk/lib/sys/cpp/testing:integration",
    "//sdk/lib/sys/cpp/testing:unit",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/ui/tools/present_view",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
  ]
}
