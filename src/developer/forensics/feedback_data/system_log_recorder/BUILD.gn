# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

visibility = [ "//src/developer/forensics/feedback_data/*" ]

###############################################################################
# SOURCES
###############################################################################
source_set("main") {
  sources = [
    "main.cc",
    "main.h",
  ]

  deps = [
    ":src",
    "encoding:production",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/developer/forensics/feedback_data:constants",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/trace-provider:trace-provider-with-fdio",
  ]

  visibility += [ "//src/developer/forensics/*" ]
}

source_set("src") {
  public = [ "system_log_recorder.h" ]

  sources = [
    "log_message_store.cc",
    "log_message_store.h",
    "system_log_recorder.cc",
    "system_log_recorder.h",
    "writer.cc",
    "writer.h",
  ]

  public_deps = [
    "encoding:encoder",
    "//sdk/lib/sys/cpp",
    "//src/developer/forensics/feedback_data:archive_accessor",
    "//src/developer/forensics/utils:logs",
  ]

  deps = [
    "//sdk/fidl/fuchsia.diagnostics",
    "//sdk/lib/syslog/cpp",
    "//src/developer/forensics/feedback_data:constants",
    "//src/lib/diagnostics/stream/cpp",
    "//src/lib/fxl",
    "//zircon/system/ulib/trace",
  ]
}

# This file is not needed in system_log_recorder per se, but should be kept in sync
# with the writer.
source_set("reader") {
  sources = [
    "reader.cc",
    "reader.h",
  ]

  public_deps = [ "encoding:decoder" ]

  deps = [
    "//sdk/lib/syslog/cpp",
    "//src/developer/forensics/feedback_data:constants",
    "//src/lib/files",
    "//src/lib/fsl",
    "//src/lib/fxl",
    "//zircon/system/ulib/trace",
  ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}
