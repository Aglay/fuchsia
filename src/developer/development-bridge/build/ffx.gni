# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")

# Defines FFX
#
template("ffx") {
  ffx_deps = []
  if (defined(invoker.deps)) {
    ffx_deps += invoker.deps
  }
  if (defined(invoker.plugin_deps)) {
    ffx_deps += invoker.plugin_deps
  }

  output_name = target_name

  if (defined(invoker.name)) {
    output_name = invoker.name
  }

  rustc_binary("bin") {
    name = output_name
    deps = ffx_deps + [ ":args_lib" ]
    non_rust_deps = [ "//third_party/boringssl" ]
    forward_variables_from(invoker,
                           "*",
                           [
                             "name",
                             "deps",
                             "non_rust_deps",
                           ])
  }

  args_deps = []
  if (defined(invoker.plugin_deps)) {
    foreach(d, invoker.plugin_deps) {
      dep = d + "_args"
      args_deps += [ dep ]
    }
  }

  rustc_library("args_lib") {
    name = "ffx_args"
    source_root = "./src/args.rs"

    deps = args_deps + [
             "config:args_lib",
             "core:lib",
             "//third_party/rust_crates:argh",
           ]
    non_rust_deps = [ "//third_party/boringssl" ]
    forward_variables_from(invoker,
                           "*",
                           [
                             "name",
                             "deps",
                             "source_root",
                             "non_rust_deps",
                           ])
  }

  tests_deps = []
  if (defined(invoker.plugin_deps)) {
    foreach(d, invoker.plugin_deps) {
      args_test = d + "_args_test"
      lib_test = d + "_test"
      tests_deps += [
        args_test,
        lib_test,
      ]
    }
  }

  group("ffx_tests") {
    testonly = true
    deps = tests_deps + [
             ":bin_test($host_toolchain)",
             ":args_lib_test($host_toolchain)",
             "config:tests($host_toolchain)",
           ]
  }
}
