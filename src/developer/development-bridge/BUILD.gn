# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/host.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")
import("//build/rust/rustc_test.gni")
import("//build/testing/test_spec.gni")

group("development-bridge") {
  testonly = true

  public_deps = [
    ":ffx",
    ":tests",
  ]
}

if (host_toolchain == current_toolchain) {
  rustc_library("lib") {
    name = "ffxlib"
    edition = "2018"

    deps = [
      "core:lib",
      "//sdk/fidl/fuchsia.developer.bridge:fuchsia.developer.bridge-rustc",
      "//sdk/fidl/fuchsia.overnet:fuchsia.overnet-rustc",
      "//sdk/fidl/fuchsia.overnet.protocol:fuchsia.overnet.protocol-rustc",
      "//src/connectivity/overnet/lib/hoist",
      "//src/lib/fidl/rust/fidl",
      "//third_party/rust_crates:anyhow",
      "//third_party/rust_crates:futures",
    ]
  }

  rustc_binary("bin") {
    name = "ffx"
    edition = "2018"
    with_unit_tests = true

    deps = [
      ":lib",
      "args:lib",
      "config:lib",
      "core:lib",
      "run-component:lib",
      "test:lib",
      "//sdk/fidl/fuchsia.developer.bridge:fuchsia.developer.bridge-rustc",
      "//sdk/fidl/fuchsia.developer.remotecontrol:fuchsia.developer.remotecontrol-rustc",
      "//sdk/fidl/fuchsia.overnet:fuchsia.overnet-rustc",
      "//sdk/fidl/fuchsia.overnet.protocol:fuchsia.overnet.protocol-rustc",
      "//src/connectivity/overnet/lib/hoist",
      "//src/connectivity/overnet/tools/ascendd:lib",
      "//src/lib/fidl/rust/fidl",
      "//src/lib/mdns/rust:mdns",
      "//src/lib/network/packet",
      "//src/lib/zerocopy",
      "//third_party/rust_crates:anyhow",
      "//third_party/rust_crates:argh",
      "//third_party/rust_crates:async-std",
      "//third_party/rust_crates:async-trait",
      "//third_party/rust_crates:chrono",
      "//third_party/rust_crates:futures",
      "//third_party/rust_crates:lazy_static",
      "//third_party/rust_crates:libc",
      "//third_party/rust_crates:log",
      "//third_party/rust_crates:net2",
      "//third_party/rust_crates:proc-macro-hack",
      "//third_party/rust_crates:regex",
      "//third_party/rust_crates:serde",
      "//third_party/rust_crates:serde_json",
      "//third_party/rust_crates:signal-hook",
      "//third_party/rust_crates:simplelog",
      "//third_party/rust_crates:thiserror",
      "//third_party/rust_crates:tokio",
    ]
    non_rust_deps = [ "//third_party/boringssl" ]
  }
}

group("ffx") {
  deps = [
    ":bin($host_toolchain)",
    ":host",
    ":lib($host_toolchain)",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":bin_test($host_toolchain)",
    "args:tests($host_toolchain)",
    "config:tests($host_toolchain)",
    "run-component:tests($host_toolchain)",
    "test:tests($host_toolchain)",
  ]
}

install_host_tools("host") {
  deps = [ ":bin($host_toolchain)" ]

  outputs = [ "ffx" ]
}
