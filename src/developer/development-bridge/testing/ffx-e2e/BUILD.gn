# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/host.gni")
import("//build/package.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")
import("//build/rust/rustc_test.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")
import("//build/testing/test_spec.gni")

if (host_toolchain == current_toolchain) {
  copy("copy") {
    testonly = true
    sources = [ "$root_out_dir/ffx" ]
    outputs = [ "$root_out_dir/test_data/ffx-e2e/{{source_file_part}}" ]
    deps = [ "//src/developer/development-bridge:bin($host_toolchain)" ]

    metadata = {
      test_runtime_deps = [ "$root_out_dir/test_data/ffx-e2e/ffx" ]
    }
  }

  rustc_test("bin") {
    name = "ffx-e2e-test"
    edition = "2018"

    deps = [
      "//sdk/fidl/fuchsia.developer.remotecontrol:fuchsia.developer.remotecontrol-rustc",
      "//sdk/fidl/fuchsia.overnet:fuchsia.overnet-rustc",
      "//sdk/fidl/fuchsia.sys:fuchsia.sys-rustc",
      "//src/connectivity/overnet/lib/hoist",
      "//src/lib/fidl/rust/fidl",
      "//third_party/rust_crates:anyhow",
      "//third_party/rust_crates:futures",
      "//third_party/rust_crates:log",
      "//third_party/rust_crates:thiserror",
    ]

    non_rust_deps = [ ":copy" ]

    test_environments = [ qemu_env ]
  }

  group("e2e-test") {
    testonly = true
    deps = [ ":bin($host_toolchain)" ]
  }
}

group("test") {
  testonly = true

  deps = [ ":e2e-test($host_toolchain)" ]
}

group("ffx-e2e-deps") {
  testonly = true
  deps = [ "//src/developer/development-bridge/testing/hello-world-e2e" ]
}
