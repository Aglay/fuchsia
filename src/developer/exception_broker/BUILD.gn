# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be found in the LICENSE file.

import("//build/config.gni")
import("//build/package.gni")

source_set("lib") {
  sources = [
    "crash_report_generation.cc",
    "crash_report_generation.h",
    "exception_broker.cc",
    "exception_broker.h",
    "process_limbo_manager.cc",
    "process_limbo_manager.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.feedback",
    "//sdk/lib/sys/cpp",
    "//src/lib/fsl",
    "//src/lib/fxl",
    "//src/lib/syslog/cpp",
    "//third_party/crashpad/minidump",
    "//third_party/crashpad/snapshot",
    "//third_party/crashpad/third_party/mini_chromium/mini_chromium/base",
    "//third_party/crashpad/util",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
    "//zircon/public/lib/zx",
    "//zircon/system/fidl/fuchsia-exception",
  ]
}

executable("exe") {
  output_name = "exception_broker"

  sources = [ "main.cc" ]

  deps = [ ":lib" ]
}

package("exception_broker") {
  deps = [ ":exe" ]

  binaries = [
    {
      name = "exception_broker"
    },
  ]

  meta = [
    {
      path = "meta/exception_broker.cmx"
      dest = "exception_broker.cmx"
    },
  ]
}

config_data("enable_jitd_on_startup") {
  for_pkg = "exception_broker"

  sources = [ "configs/jitd_on_startup.json" ]

  outputs = [ "enable_jitd_on_startup.json" ]
}
