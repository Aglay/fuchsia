# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/cobalt/metrics_registry.gni")

source_set("archive") {
  sources = [
    "archive.cc",
    "archive.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.feedback",
    "//zircon/system/fidl/fuchsia-mem",
  ]

  deps = [
    "//src/lib/files",
    "//src/lib/fsl",
    "//src/lib/fxl",
    "//src/lib/syslog/cpp",
    "//third_party/zlib:minizip",
  ]

  visibility = [ "//src/developer/feedback/*" ]
}

source_set("cobalt") {
  public = [
    "cobalt.h",
    "cobalt_event.h",
  ]

  sources = [
    "cobalt.cc",
    "cobalt.h",
    "cobalt_event.cc",
    "cobalt_event.h",
  ]

  public_deps = [
    "//sdk/lib/sys/cpp",
    "//zircon/public/lib/fit",
    "//zircon/system/fidl/fuchsia-cobalt",
  ]

  deps = [
    ":metrics_registry",
    "//src/lib/fxl",
    "//src/lib/syslog/cpp",
  ]

  visibility = [ "//src/developer/feedback/*" ]
}

source_set("inspect_node_manager") {
  sources = [
    "inspect_node_manager.cc",
    "inspect_node_manager.h",
  ]

  public_deps = [
    "//sdk/lib/sys/inspect/cpp",
    "//src/lib/fxl",
  ]

  visibility = [ "//src/developer/feedback/*" ]
}

source_set("promise") {
  public = [
    "promise.h",
  ]

  sources = [
    "promise.h",
  ]

  public_deps = [
    "//zircon/public/lib/fit",
  ]

  visibility = [ "//src/developer/feedback/*" ]
}

source_set("time") {
  sources = [
    "time.cc",
    "time.h",
  ]

  public_deps = [
    "//src/lib/timekeeper",
    "//zircon/public/lib/zx",
  ]

  deps = [
    "//src/lib/fxl",
  ]

  visibility = [ "//src/developer/feedback/*" ]
}

metrics_registry("metrics_registry") {
  # This must match the name of our Cobalt project as specified in:
  # https://cobalt-analytics.googlesource.com/config/+/refs/heads/master/projects.yaml
  project_name = "feedback"
  namespace = "cobalt_registry"
  generate_cc = true
  generate_binarypb = false
}
