# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/package.gni")
import("//third_party/cobalt_config/metrics_registry.gni")

package("boot_log_checker") {
  deps = [
    ":main",
  ]

  binaries = [
    {
      name = "boot_log_checker"
    },
  ]

  meta = [
    {
      path = "meta/boot_log_checker.cmx"
      dest = "boot_log_checker.cmx"
    },
  ]
}

executable("main") {
  output_name = "boot_log_checker"

  sources = [
    "main.cc",
  ]

  deps = [
    ":src",
    "//garnet/public/lib/syslog/cpp",
    "//sdk/lib/sys/cpp",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
    "//zircon/public/lib/fit",
  ]
}

source_set("src") {
  sources = [
    "reboot_log_handler.cc",
    "reboot_log_handler.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.feedback",
    "//sdk/lib/sys/cpp",
    "//zircon/public/lib/fit",
    "//zircon/system/fidl/fuchsia-cobalt",
    "//zircon/system/fidl/fuchsia-net",
  ]

  deps = [
    ":metrics_registry",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/syslog/cpp",
    "//src/lib/fxl",
  ]
}

metrics_registry("metrics_registry") {
  # This must match the name of our Cobalt project as specified in:
  # https://cobalt-analytics.googlesource.com/config/+/refs/heads/master/projects.yaml
  project_name = "feedback"
  namespace = "cobalt_registry"
  generate_cc = true
  generate_binarypb = false
}

config_data("config") {
  for_pkg = "sysmgr"

  sources = [
    "config/config.json",
  ]

  outputs = [
    "boot_log_checker.config",
  ]
}
