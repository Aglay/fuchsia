# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

group("tests") {
  testonly = true

  public_deps = [
    ":crashpad_agent_tests",
    ":invalid_override_config",
    ":valid_override_config",

    # To run the Crashpad tests.
    "//third_party/crashpad:crashpad_test",

    # Packages for the components that we are injecting in the integration tests.
    "//src/developer/feedback/crashpad_agent",
    "//src/developer/feedback/crashpad_agent:fake_crashpad_agent_invalid_override_config",
    "//src/developer/feedback/crashpad_agent:fake_crashpad_agent_valid_override_config",
    "//src/developer/feedback/feedback_agent",
    "//src/diagnostics/archivist",
  ]
}

test_package("crashpad_agent_tests") {
  tests = [
    {
      name = "crashpad_agent_unittest"
      environments = basic_envs
    },
    {
      name = "crashpad_config_unittest"
      environments = basic_envs
    },
    {
      name = "crashpad_agent_integration_test"
      environments = basic_envs
    },
    {
      # Syntax is somewhat confusing, but the intent is to re-use the same underlying test binary,
      # defined by |name|, but with a different cmx, defined by |dest|, that injects the fake
      # package.
      name = "crashpad_agent_integration_test"
      dest = "crashpad_agent_integration_test_valid_override_config"
      environments = basic_envs
    },
    {
      # Syntax is somewhat confusing, but the intent is to re-use the same underlying test binary,
      # defined by |name|, but with a different cmx, defined by |dest|, that injects the fake
      # package.
      name = "crashpad_agent_integration_test"
      dest = "crashpad_agent_integration_test_invalid_override_config"
      environments = basic_envs
    },
    {
      name = "privacy_settings_ptr_unittest"
    },
  ]

  resources = [
    # needed for crashpad_config_unittest
    {
      path = "configs/valid_no_upload.json"
      dest = "valid_config_no_upload.json"
    },

    # needed for crashpad_config_unittest
    {
      path = "configs/valid_upload.json"
      dest = "valid_config_upload.json"
    },

    # needed for crashpad_config_unittest
    {
      path = "configs/valid_no_upload_spurious_server.json"
      dest = "valid_config_no_upload_spurious_server.json"
    },

    # needed for crashpad_config_unittest
    {
      path = "configs/bad_schema_spurious_field.json"
      dest = "bad_schema_spurious_field_config.json"
    },

    # needed for crashpad_config_unittest
    {
      path = "configs/bad_schema_missing_required_field.json"
      dest = "bad_schema_missing_required_field_config.json"
    },

    # needed for crashpad_config_unittest
    {
      path = "configs/bad_schema_missing_server.json"
      dest = "bad_schema_missing_server_config.json"
    },
  ]

  deps = [
    ":config_unittest",
    ":crashpad_agent_integration_test",
    ":crashpad_agent_unittest",
    ":privacy_settings_ptr_unittest",
  ]
}

executable("config_unittest") {
  testonly = true

  output_name = "crashpad_config_unittest"

  sources = [
    "config_unittest.cc",
  ]

  deps = [
    "//garnet/public/lib/syslog/cpp",
    "//src/developer/feedback/crashpad_agent:src",
    "//src/lib/fxl/test:test_settings",
    "//third_party/googletest:gtest",
  ]
}

executable("crashpad_agent_unittest") {
  testonly = true

  sources = [
    "crashpad_agent_unittest.cc",
    "stub_crash_server.cc",
    "stub_crash_server.h",
    "stub_feedback_data_provider.cc",
    "stub_feedback_data_provider.h",
  ]

  deps = [
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/gtest",
    "//garnet/public/lib/syslog/cpp",
    "//sdk/fidl/fuchsia.feedback",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp/testing:unit",
    "//src/developer/feedback/crashpad_agent:constants",
    "//src/developer/feedback/crashpad_agent:src",
    "//src/developer/feedback/testing:gmatchers",
    "//src/developer/feedback/testing:gpretty_printers",
    "//src/lib/fxl",
    "//src/lib/fxl/test:test_settings",
    "//third_party/crashpad/util",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/zx",
    "//zircon/system/fidl/fuchsia-crash",
    "//zircon/system/fidl/fuchsia-mem",
  ]
}

executable("crashpad_agent_integration_test") {
  testonly = true

  sources = [
    "crashpad_agent_integration_test.cc",
  ]

  deps = [
    "//garnet/public/lib/fsl",
    "//sdk/fidl/fuchsia.feedback",
    "//sdk/lib/sys/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/zx",
    "//zircon/system/fidl/fuchsia-crash",
    "//zircon/system/fidl/fuchsia-mem",
  ]
}

executable("privacy_settings_ptr_unittest") {
  testonly = true

  sources = [
    "fake_privacy_settings.cc",
    "fake_privacy_settings.h",
    "privacy_settings_ptr_unittest.cc",
  ]

  deps = [
    "//garnet/public/lib/fostr/fidl/fuchsia.settings",
    "//garnet/public/lib/gtest",
    "//garnet/public/lib/syslog/cpp",
    "//sdk/lib/sys/cpp/testing:unit",
    "//src/developer/feedback/crashpad_agent:src",
    "//src/lib/files",
    "//src/lib/fxl",
    "//src/lib/fxl/test:test_settings",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/zx",
  ]
}

config_data("valid_override_config") {
  for_pkg = "fake_crashpad_agent_valid_override_config"

  sources = [
    "configs/valid_override.json",
  ]

  outputs = [
    "override_config.json",
  ]
}

config_data("invalid_override_config") {
  for_pkg = "fake_crashpad_agent_invalid_override_config"

  sources = [
    "configs/invalid_override.json",
  ]

  outputs = [
    "override_config.json",
  ]
}
