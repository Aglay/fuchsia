# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

group("tests") {
  testonly = true

  public_deps = [
    ":crashpad_agent_tests",
    ":invalid_override_config",
    ":valid_override_config",

    # To run the Crashpad tests.
    "//third_party/crashpad:crashpad_test",

    # Packages for the components that we are injecting in the integration tests.
    "//src/cobalt/bin/testing/mock_cobalt",
    "//src/connectivity/network/netstack",
    "//src/developer/feedback/crashpad_agent",
    "//src/developer/feedback/crashpad_agent:fake_crashpad_agent_invalid_override_config",
    "//src/developer/feedback/crashpad_agent:fake_crashpad_agent_valid_override_config",
    "//src/developer/feedback/feedback_agent",
    "//src/diagnostics/archivist",
    "//src/sys/timekeeper",
  ]
}

test_package("crashpad_agent_tests") {
  tests = [
    {
      name = "crashpad_agent_unittest"
    },
    {
      # Binary name on the host. We prefix it with "crashpad_agent" to reduce collisions.
      name = "crashpad_agent_config_unittest"

      # Component and binary name on the target. Confined to the package so no risk of collisions.
      dest = "config_unittest"
    },
    {
      name = "crashpad_agent_integration_test"
    },
    {
      # Binary name on the host. Shared across all integration test components.
      name = "crashpad_agent_integration_test"

      # Component and binary name on the target. Specific to each integration test component.
      dest = "crashpad_agent_integration_test_valid_override_config"
    },
    {
      # Binary name on the host. Shared across all integration test components.
      name = "crashpad_agent_integration_test"

      # Component and binary name on the target. Specific to each integration test component.
      dest = "crashpad_agent_integration_test_invalid_override_config"
    },
    {
      # Binary name on the host. We prefix it with "crashpad_agent" to reduce collisions.
      name = "crashpad_agent_database_unittest"

      # Component and binary name on the target. Confined to the package so no risk of collisions.
      dest = "database_unittest"
    },
    {
      name = "feedback_data_provider_unittest"
    },
    {
      # Binary name on the host. We prefix it with "crashpad_agent" to reduce collisions.
      name = "crashpad_agent_inspect_manager_unittest"

      # Component and binary name on the target. Confined to the package so no risk of collisions.
      dest = "inspect_manager_unittest"
    },
    {
      name = "queue_unittest"
    },
    {
      name = "privacy_settings_ptr_unittest"
    },
  ]

  resources = [
    # needed for config_unittest
    {
      path = "configs/valid_upload_disabled.json"
      dest = "valid_config_upload_disabled.json"
    },

    # needed for config_unittest
    {
      path = "configs/valid_upload_enabled.json"
      dest = "valid_config_upload_enabled.json"
    },

    # needed for config_unittest
    {
      path = "configs/valid_upload_read_from_privacy_settings.json"
      dest = "valid_config_upload_read_from_privacy_settings.json"
    },

    # needed for config_unittest
    {
      path = "configs/valid_upload_disabled_spurious_server.json"
      dest = "valid_config_upload_disabled_spurious_server.json"
    },

    # needed for config_unittest
    {
      path = "configs/bad_schema_spurious_field.json"
      dest = "bad_schema_spurious_field_config.json"
    },

    # needed for config_unittest
    {
      path = "configs/bad_schema_missing_required_field.json"
      dest = "bad_schema_missing_required_field_config.json"
    },

    # needed for config_unittest
    {
      path = "configs/bad_schema_missing_server_upload_enabled.json"
      dest = "bad_schema_missing_server_upload_enabled_config.json"
    },

    # needed for config_unittest
    {
      path = "configs/bad_schema_missing_server_upload_read_from_privacy_settings.json"
      dest = "bad_schema_missing_server_upload_read_from_privacy_settings_config.json"
    },

    # needed for config_unittest
    {
      path = "configs/bad_schema_invalid_upload_policy.json"
      dest = "bad_schema_invalid_upload_policy_config.json"
    },
  ]

  deps = [
    ":config_unittest",
    ":crashpad_agent_integration_test",
    ":crashpad_agent_unittest",
    ":database_unittest",
    ":feedback_data_provider_unittest",
    ":inspect_manager_unittest",
    ":privacy_settings_ptr_unittest",
    ":queue_unittest",
  ]
}

executable("config_unittest") {
  testonly = true

  output_name = "crashpad_agent_config_unittest"

  sources = [ "config_unittest.cc" ]

  deps = [
    "//src/developer/feedback/crashpad_agent:src",
    "//src/developer/feedback/testing:gtest_with_syslog_main",
    "//third_party/googletest:gtest",
  ]

  # TODO(46559): Fix the leaks and remove this.
  deps += [ "//build/config/sanitizers:suppress-lsan.DO-NOT-USE-THIS" ]
}

executable("crashpad_agent_unittest") {
  testonly = true

  sources = [
    "//src/developer/feedback/utils/tests/stub_utc.cc",
    "//src/developer/feedback/utils/tests/stub_utc.h",
    "crashpad_agent_unittest.cc",
    "fake_privacy_settings.cc",
    "fake_privacy_settings.h",
    "stub_crash_server.cc",
    "stub_crash_server.h",
    "stub_feedback_data_provider.cc",
    "stub_feedback_data_provider.h",
  ]

  deps = [
    "//garnet/public/lib/timekeeper:testing",
    "//sdk/fidl/fuchsia.feedback",
    "//sdk/fidl/fuchsia.settings",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/inspect/testing/cpp",
    "//src/developer/feedback/crashpad_agent:constants",
    "//src/developer/feedback/crashpad_agent:src",
    "//src/developer/feedback/crashpad_agent/info:info",
    "//src/developer/feedback/testing:cobalt_test_fixture",
    "//src/developer/feedback/testing:gtest_with_syslog_main",
    "//src/developer/feedback/testing:unit_test_fixture",
    "//src/developer/feedback/testing/stubs:stub_cobalt_logger_factory",
    "//src/developer/feedback/utils:cobalt",
    "//src/lib/fsl",
    "//src/lib/fxl",
    "//src/sys/timekeeper:fuchsia.time",
    "//third_party/crashpad/util",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/zx",
    "//zircon/system/fidl/fuchsia-mem",
  ]

  # TODO(46559): Fix the leaks and remove this.
  deps += [ "//build/config/sanitizers:suppress-lsan.DO-NOT-USE-THIS" ]
}

executable("crashpad_agent_integration_test") {
  testonly = true

  sources = [ "crashpad_agent_integration_test.cc" ]

  deps = [
    "//sdk/fidl/fuchsia.cobalt.test",
    "//sdk/fidl/fuchsia.feedback",
    "//sdk/lib/sys/cpp",
    "//src/developer/feedback/utils:cobalt",
    "//src/lib/fsl",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/zx",
    "//zircon/system/fidl/fuchsia-cobalt",
    "//zircon/system/fidl/fuchsia-mem",
  ]

  # TODO(46559): Fix the leaks and remove this.
  deps += [ "//build/config/sanitizers:suppress-lsan.DO-NOT-USE-THIS" ]
}

executable("database_unittest") {
  testonly = true

  output_name = "crashpad_agent_database_unittest"

  sources = [
    "crashpad_database_gremlin.cc",
    "crashpad_database_gremlin.h",
    "database_unittest.cc",
  ]

  deps = [
    "//garnet/public/lib/timekeeper:testing",
    "//sdk/lib/inspect/testing/cpp",
    "//src/developer/feedback/crashpad_agent:src",
    "//src/developer/feedback/crashpad_agent/info:info",
    "//src/developer/feedback/testing:cobalt_test_fixture",
    "//src/developer/feedback/testing:gtest_with_syslog_main",
    "//src/developer/feedback/testing:unit_test_fixture",
    "//src/developer/feedback/testing/stubs:stub_cobalt_logger_factory",
    "//src/developer/feedback/utils:cobalt",
    "//src/lib/fsl:fsl",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
  ]

  # TODO(46559): Fix the leaks and remove this.
  deps += [ "//build/config/sanitizers:suppress-lsan.DO-NOT-USE-THIS" ]
}

executable("feedback_data_provider_unittest") {
  testonly = true

  sources = [
    "feedback_data_provider_unittest.cc",
    "stub_feedback_data_provider.cc",
    "stub_feedback_data_provider.h",
  ]

  deps = [
    "//src/developer/feedback/crashpad_agent:src",
    "//src/developer/feedback/testing:gtest_with_syslog_main",
    "//src/developer/feedback/testing:unit_test_fixture",
    "//src/lib/fsl:fsl",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
  ]
}

executable("inspect_manager_unittest") {
  testonly = true

  output_name = "crashpad_agent_inspect_manager_unittest"

  sources = [ "inspect_manager_unittest.cc" ]

  deps = [
    "//garnet/public/lib/timekeeper:testing",
    "//sdk/lib/inspect/testing/cpp",
    "//src/developer/feedback/crashpad_agent:constants",
    "//src/developer/feedback/crashpad_agent/info:inspect_manager",
    "//src/developer/feedback/testing:gtest_with_syslog_main",
    "//src/lib/fxl",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/zx",
  ]

  # TODO(46559): Fix the leaks and remove this.
  deps += [ "//build/config/sanitizers:suppress-lsan.DO-NOT-USE-THIS" ]
}

executable("privacy_settings_ptr_unittest") {
  testonly = true

  sources = [
    "fake_privacy_settings.cc",
    "fake_privacy_settings.h",
    "privacy_settings_ptr_unittest.cc",
  ]

  deps = [
    "//garnet/public/lib/fostr/fidl/fuchsia.settings",
    "//sdk/fidl/fuchsia.settings",
    "//src/developer/feedback/crashpad_agent:src",
    "//src/developer/feedback/testing:gtest_with_syslog_main",
    "//src/developer/feedback/testing:unit_test_fixture",
    "//src/lib/files",
    "//src/lib/fxl",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/zx",
  ]
}

executable("queue_unittest") {
  testonly = true

  sources = [
    "queue_unittest.cc",
    "stub_crash_server.cc",
    "stub_crash_server.h",
  ]

  deps = [
    "//garnet/public/lib/gtest",
    "//garnet/public/lib/timekeeper:testing",
    "//sdk/lib/inspect/testing/cpp",
    "//src/developer/feedback/crashpad_agent:constants",
    "//src/developer/feedback/crashpad_agent:src",
    "//src/developer/feedback/crashpad_agent/info:info",
    "//src/developer/feedback/testing:gtest_with_syslog_main",
    "//src/developer/feedback/testing:unit_test_fixture",
    "//src/lib/fsl:fsl",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
  ]

  # TODO(46559): Fix the leaks and remove this.
  deps += [ "//build/config/sanitizers:suppress-lsan.DO-NOT-USE-THIS" ]
}

config_data("valid_override_config") {
  for_pkg = "fake_crashpad_agent_valid_override_config"

  sources = [ "configs/valid_override.json" ]

  outputs = [ "override_config.json" ]
}

config_data("invalid_override_config") {
  for_pkg = "fake_crashpad_agent_invalid_override_config"

  sources = [ "configs/invalid_override.json" ]

  outputs = [ "override_config.json" ]
}
