# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/package.gni")

# We use a template to keep the real and fake packages in sync.
template("crashpad_agent_package") {
  package(target_name) {
    binaries = [
      {
        name = "crashpad_agent"
      },
    ]

    meta = [
      {
        path = "meta/crashpad_agent.cmx"
        dest = "crashpad_agent.cmx"
      },
    ]

    resources = [
      {
        path = "configs/default.json"
        dest = "default_config.json"
      },
    ]

    deps = [
      ":main",
    ]
  }

  # Suppress unused variable warnings as the invoker is expected to be empty.
  not_needed(invoker, "*")
}

# The real package.
crashpad_agent_package("crashpad_agent") {
}

# A fake package for integration test purposes where we want a valid override config_data() that
# only affects the fake package.
crashpad_agent_package("fake_crashpad_agent_valid_override_config") {
}

# A fake package for integration test purposes where we want an invalid override config_data() that
# only affects the fake package.
crashpad_agent_package("fake_crashpad_agent_invalid_override_config") {
}

executable("main") {
  output_name = "crashpad_agent"

  sources = [
    "main.cc",
  ]

  deps = [
    ":src",
    "//garnet/public/lib/syslog/cpp",
    "//garnet/public/lib/timekeeper:system",
    "//sdk/fidl/fuchsia.feedback",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/inspect/cpp",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
    "//zircon/system/fidl/fuchsia-crash",
  ]
}

source_set("src") {
  sources = [
    "config.cc",
    "config.h",
    "crash_report_util.cc",
    "crash_report_util.h",
    "crash_server.cc",
    "crash_server.h",
    "crashpad_agent.cc",
    "crashpad_agent.h",
    "crashpad_report_util.cc",
    "crashpad_report_util.h",
    "feedback_data_provider_ptr.cc",
    "feedback_data_provider_ptr.h",
    "inspect_manager.cc",
    "inspect_manager.h",
    "privacy_settings_ptr.cc",
    "privacy_settings_ptr.h",
    "scoped_unlink.h",
    "settings.cc",
    "settings.h",
  ]

  public_deps = [
    "//garnet/public/lib/timekeeper",
    "//sdk/fidl/fuchsia.feedback",
    "//sdk/fidl/fuchsia.settings",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/inspect/cpp",
    "//src/lib/fxl",
    "//third_party/crashpad/client",
    "//third_party/crashpad/util",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/async-default",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/zx",
    "//zircon/system/fidl/fuchsia-crash",
    "//zircon/system/fidl/fuchsia-mem",
  ]

  deps = [
    ":constants",
    "//garnet/public/lib/fostr/fidl/fuchsia.settings",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/syslog/cpp",
    "//third_party/rapidjson",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
    "//zircon/public/lib/fbl",
  ]
}

source_set("constants") {
  sources = [
    "constants.h",
  ]

  visibility = [ "//src/developer/feedback/crashpad_agent/*" ]
}

config_data("upload_to_prod_server_config") {
  for_pkg = "crashpad_agent"
  sources = [
    "configs/upload_to_prod_server.json",
  ]
  outputs = [
    "override_config.json",
  ]
}
