# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

group("tests") {
  testonly = true
  deps = [
    ":memory_monitor_inspect_integration_tests",
    ":memory_monitor_tests",
  ]
}

executable("memory_monitor_unittests") {
  testonly = true
  output_name = "memory_monitor_unittests"

  sources = [
    "high_water_unittest.cc",
    "metrics_unittest.cc",
    "monitor_fidl_unittest.cc",
  ]

  deps = [
    "//garnet/public/lib/gtest",
    "//sdk/lib/sys/cpp/testing:unit",
    "//src/cobalt/bin/testing:fake_logger_lib",
    "//src/developer/memory/metrics/tests:utils",
    "//src/developer/memory/monitor:lib",
    "//src/lib/fxl/test:gtest_main",
    "//zircon/public/lib/memfs",
  ]
}

test_package("memory_monitor_tests") {
  rootresource_svc = "//build"

  deps = [
    ":memory_monitor_unittests",
  ]

  tests = [
    {
      name = "memory_monitor_unittests"
      environments = basic_envs
    },
  ]
}

executable("bin-monitor") {
  output_name = "memory_monitor_test_app"

  sources = [
    "monitor_test_app.cc",
  ]

  deps = [
    "//sdk/lib/sys/cpp",
    "//src/developer/memory/monitor:lib",
    "//src/lib/fxl",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
  ]
}

executable("test-inspect") {
  testonly = true
  output_name = "memory_monitor_inspect_integration_tests"

  sources = [
    "monitor_inspect_test.cc",
  ]

  deps = [
    "//sdk/lib/inspect/testing/cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/cpp/testing:integration",
    "//src/lib/fxl",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
    "//zircon/public/lib/fs",
    "//zircon/public/lib/inspect",
  ]
}

test_package("memory_monitor_inspect_integration_tests") {
  deprecated_shell = "//build"
  rootresource_svc = "//build"

  deps = [
    ":bin-monitor",
    ":test-inspect",
  ]

  binaries = [
    {
      name = "memory_monitor_test_app"
    },
  ]

  meta = [
    {
      path = rebase_path("meta/memory_monitor_test_app.cmx")
      dest = "memory_monitor_test_app.cmx"
    },
  ]

  tests = [
    {
      name = "memory_monitor_inspect_integration_tests"
      environments = basic_envs
    },
  ]
}
