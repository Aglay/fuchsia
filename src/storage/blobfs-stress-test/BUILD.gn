# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")
import("//src/sys/build/components.gni")

rustc_library("lib") {
  name = "blobfs-stress-test-lib"
  edition = "2018"
  source_root = "lib/mod.rs"

  deps = [
    "//garnet/lib/rust/files_async",
    "//garnet/lib/rust/io_util",
    "//sdk/fidl/fuchsia.hardware.block.partition:fuchsia.hardware.block.partition-rustc",
    "//sdk/fidl/fuchsia.io:fuchsia.io-rustc",
    "//src/lib/fdio/rust:fdio",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/storage/fs_management",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/storage/stress-test-utils",
    "//src/sys/component_manager/testing:test_utils_lib",
    "//src/sys/pkg/lib/fuchsia-hash",
    "//src/sys/pkg/lib/fuchsia-merkle",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",  # for recording messages
    "//third_party/rust_crates:rand",
    "//third_party/rust_crates:rand_core",
  ]

  sources = [
    "lib/blob.rs",
    "lib/mod.rs",
    "lib/operator.rs",
  ]
}

rustc_binary("stressor-bin") {
  name = "stressor"
  edition = "2018"
  source_root = "main.rs"

  deps = [
    ":lib",
    "//src/lib/fuchsia-async",
    "//src/lib/syslog/rust:syslog",
    "//src/storage/stress-test-utils",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:argh",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:rand",
    "//third_party/rust_crates:rand_core",
  ]

  sources = [ "main.rs" ]
}

fuchsia_component("stressor") {
  testonly = true
  manifest = "meta/stressor.cmx"
  deps = [ ":stressor-bin" ]
}

# This test package is designed to run on infra bots.
# Infra bots have a time limit of 23 hours. The test runs
# for 22 hours, leaving an hour for the build process.
fuchsia_test_package("blobfs-stress-test") {
  test_components = [ ":stressor" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
    environments = [
      {
        dimensions = {
          device_type = "QEMU"
        }
        tags = [ "stress-tests" ]
      },
    ]
  }
}

group("tests") {
  testonly = true
  deps = [
    ":blobfs-stress-test",
    "//src/lib/isolated_devmgr/v2_component:isolated-devmgr",
    "//src/sys/component_manager:component-manager",
  ]
}
