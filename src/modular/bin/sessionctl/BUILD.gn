# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/testing/environments.gni")
import("//src/modular/build/executable_package.gni")
import("//src/modular/build/tests_package.gni")

executable("sessionctl_bin") {
  output_name = "sessionctl"
  sources = [ "main.cc" ]

  deps = [
    ":lib",
    "//sdk/fidl/fuchsia.modular",
    "//sdk/fidl/fuchsia.modular.internal",
    "//src/lib/fxl",
    "//src/modular/lib/async/cpp:future",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
    "//zircon/public/lib/fdio",
  ]
}

package("sessionctl") {
  deps = [ ":sessionctl_bin" ]
  binaries = [
    {
      name = "sessionctl"
      shell = true
    },
  ]

  meta = [
    {
      path = "meta/sessionctl.cmx"
      dest = "sessionctl.cmx"
    },
  ]
}

source_set("lib") {
  sources = [
    "logger.cc",
    "logger.h",
    "session_ctl_app.cc",
    "session_ctl_app.h",
    "session_ctl_constants.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.modular",
    "//src/lib/fxl",
    "//src/modular/lib/async/cpp:future",
    "//third_party/rapidjson",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
  ]

  deps = [
    "//sdk/fidl/fuchsia.modular.internal",
    "//src/lib/json_parser",
  ]
}

executable("sessionctl_unittest") {
  testonly = true

  sources = [ "session_ctl_app_unittest.cc" ]

  deps = [
    ":lib",
    "//sdk/fidl/fuchsia.modular",
    "//src/lib/fxl/test:gtest_main",
    "//src/modular/bin/sessionmgr/puppet_master:puppet_master_impl",
    "//src/modular/lib/testing:test_story_command_executor",
    "//src/modular/lib/testing:test_with_session_storage",
  ]
}

hermetic_tests_package("sessionctl_unittests") {
  deps = [ ":sessionctl_unittest" ]
  environments = basic_envs
}
