# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

source_set("test_settings") {
  testonly = true

  sources = [
    "test_settings.cc",
    "test_settings.h",
  ]

  deps = [ "//src/lib/fxl" ]
}

# Generic main entry point target for GTest executables.
source_set("gtest_main") {
  testonly = true

  sources = [ "run_all_unittests.cc" ]

  deps = [
    ":test_settings",
    "//src/lib/fxl",
  ]

  public_deps = [ "//third_party/googletest:gtest" ]
}

# Generic main entry point target for GTest executables that also sets up a syslog logger.
# Prefer this over gtest_main if your test component logs to syslog.
source_set("gtest_main_with_syslog") {
  testonly = true

  sources = [ "run_all_unittests_with_syslog.cc" ]

  deps = [
    ":test_settings",
    "//src/lib/fxl",
  ]

  public_deps = [ "//third_party/googletest:gtest" ]

  if (is_fuchsia) {
    deps += [ "//zircon/public/lib/zx" ]
    public_deps += [ "//src/lib/syslog/cpp" ]
  }
}
