# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/rust/rustc_binary.gni")
import("//src/sys/build/components.gni")
import("//src/sys/build/fuchsia_test_package.gni")

rustc_binary("example_bin") {
  testonly = true
  name = "rust_example_bin"
  edition = "2018"

  deps = [
    "//src/lib/fake-clock/examples/fidl:fuchsia.fakeclock.test-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-runtime",
    "//src/lib/syslog/rust:syslog",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
  ]

  sources = [ "src/main.rs" ]

  non_rust_deps = [ "//src/lib/fake-clock/lib" ]
}

fuchsia_component("rust_example") {
  testonly = true
  component_name = "rust_example"
  manifest = "meta/example.cmx"
  deps = [ ":example_bin" ]
}

rustc_test("test_bin") {
  name = "integ_test_bin"
  edition = "2018"

  sources = [ "test/test.rs" ]

  source_root = "test/test.rs"

  deps = [
    "//src/lib/fake-clock/examples/fidl:fuchsia.fakeclock.test-rustc",
    "//src/lib/fake-clock/fidl:fidl-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:futures",
  ]
}

fuchsia_component("rust_integ_test") {
  testonly = true
  component_name = "rust_integ_test"
  manifest = "meta/test.cmx"
  deps = [ ":test_bin" ]
}

fuchsia_test_package("fake-clock-rust") {
  test_components = [ ":rust_integ_test" ]
  deps = [
    ":rust_example",
    "//src/lib/fake-clock/svc",
  ]
}

group("examples") {
  deps = [ ":fake-clock-rust" ]
  testonly = true
}
