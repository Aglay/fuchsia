# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test/test_package.gni")
import("//src/graphics/lib/magma/gnbuild/magma.gni")
import("//src/lib/vulkan/compact_image.gni")
import("//src/lib/vulkan/image_pipe_swapchain.gni")
import("//src/lib/vulkan/layers.gni")

group("tests") {
  testonly = true
  deps = [
    ":compact-image-tests",
    ":image-pipe-swapchain-tests",
  ]
}

test_package("compact-image-tests") {
  deps = [ ":test_compact_image" ]

  tests = [
    {
      name = "test_compact_image"
      dest = "compact-image-tests"
      environments = [ aemu_env ] + magma_libvulkan_hardware_envs
    },
  ]
  public_deps = vulkan_validation_layers.public_deps + compact_image.public_deps

  loadable_modules =
      vulkan_validation_layers.loadable_modules + compact_image.loadable_modules

  resources = vulkan_validation_layers.resources + compact_image.resources
}

executable("test_compact_image") {
  testonly = true
  sources = [ "test_compact_image.cc" ]
  deps = [
    "//garnet/public/lib/gtest",
    "//sdk/lib/sys/cpp/testing:unit",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/vulkan",
  ]
}

test_package("image-pipe-swapchain-tests") {
  deps = [ ":test_image_pipe_swapchain" ]

  tests = [
    {
      name = "test_image_pipe_swapchain"
      dest = "image-pipe-swapchain-tests"
      environments = [ aemu_env ] + magma_libvulkan_hardware_envs
    },
  ]
  public_deps =
      vulkan_validation_layers.public_deps + image_pipe_swapchain.public_deps +
      image_pipe_swapchain_fb.public_deps

  loadable_modules = vulkan_validation_layers.loadable_modules +
                     image_pipe_swapchain.loadable_modules +
                     image_pipe_swapchain_fb.loadable_modules

  resources = vulkan_validation_layers.resources +
              image_pipe_swapchain.resources + image_pipe_swapchain_fb.resources
}

executable("test_image_pipe_swapchain") {
  testonly = true
  sources = [
    "test_extensions.cc",
    "test_imagepipe_view.cc",
    "test_surface.cc",
    "test_swapchain.cc",
  ]
  deps = [
    "//garnet/public/lib/gtest",
    "//sdk/lib/sys/cpp/testing:unit",
    "//sdk/lib/ui/scenic/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/vulkan",
    "//src/lib/vulkan/imagepipe_view",
    "//zircon/public/lib/zx",
  ]

  # TODO(fxbug.dev/58615): Fix + remove.
  deps += [ "//build/config/sanitizers:suppress-lsan.DO-NOT-USE-THIS" ]

  # TODO(fxbug.dev/58160): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-reorder-init-list" ]

  # TODO(fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}
