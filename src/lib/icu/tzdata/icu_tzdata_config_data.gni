# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")

# Provide ICU time zone resource files to the given package.
#
# Please see the `README.md` for documentation.
template("icu_tzdata_config_data") {
  config_data(target_name) {
    forward_variables_from(invoker,
                           "*",
                           [
                             "for_pkg",
                             "testonly",
                             "visibility",
                           ])

    for_pkg = invoker.for_pkg

    data_version = "44"
    if (defined(invoker.data_version)) {
      data_version = invoker.data_version
    }

    format = "le"
    if (defined(invoker.format)) {
      format = invoker.format
    }

    assert(data_version == "44" && format == "le",
           "Unsupported data version or format")

    outputs = [
      "tzdata/icu/${data_version}/${format}/{{source_file_part}}",
    ]

    sources = [
      "//third_party/icu/tzres/metaZones.res",
      "//third_party/icu/tzres/timezoneTypes.res",
      "//third_party/icu/tzres/zoneinfo64.res",
    ]
  }
}
