# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/fidl/fidl.gni")
import("//build/rust/rustc_binary.gni")
import("//src/sys/build/components.gni")

group("logs-budget") {
  testonly = true
  deps = [ ":package" ]
}

rustc_binary("test") {
  testonly = true
  name = "logs-budget"

  deps = [
    ":fidl-rustc",
    "//sdk/fidl/fuchsia.diagnostics:fuchsia.diagnostics-rustc",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger-rustc",
    "//sdk/fidl/fuchsia.sys:fuchsia.sys-rustc",
    "//src/diagnostics/archivist:lib",
    "//src/lib/diagnostics/hierarchy/rust",
    "//src/lib/diagnostics/reader/rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/syslog/rust",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]

  sources = [ "src/main.rs" ]
}

fuchsia_component("component") {
  testonly = true
  component_name = "test-logs-budget"
  manifest = "meta/test-logs-budget.cmx"
  deps = [ ":test" ]
}

fuchsia_component("archivist-with-small-caches") {
  testonly = true
  manifest = "meta/archivist-with-small-caches.cmx"
  deps = [
    ":small-caches-archivist-config",
    "//src/diagnostics/archivist:bin",
  ]
}

resource("small-caches-archivist-config") {
  sources = [ "config/small-caches-config.json" ]
  outputs = "data/{{source_file_part}}"
}

fidl("fidl") {
  testonly = true
  name = "test.logs.budget"
  sources = [ "fidl/socket_puppet.test.fidl" ]
}

rustc_binary("puppet") {
  testonly = true
  name = "socket_puppet"
  source_root = "src/puppet.rs"

  deps = [
    ":fidl-rustc",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger-rustc",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:futures",
  ]

  sources = [ "src/puppet.rs" ]
}

fuchsia_component("socket-puppet") {
  testonly = true
  manifest = "meta/socket-puppet.cmx"
  deps = [ ":puppet" ]
}

fuchsia_test_package("package") {
  package_name = "test-logs-budget"
  test_components = [ ":component" ]
  deps = [
    ":archivist-with-small-caches",
    ":socket-puppet",
  ]
}
