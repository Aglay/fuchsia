# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")

# Define a filtered ArchiveAccessor pipeline.
#
# Parameters
#
#   name
#     Type: string
#     Optional: defaults to target_name
#
#   inspect
#     Type: list of files
#     Required: inspect selector configurations for the pipeline
# TODO(fxbug.dev/60611) update public pipeline docs with this template
template("archive_pipeline") {
  assert(defined(invoker.inspect), "Must define inspect selectors")

  name = target_name
  if (defined(invoker.name)) {
    name = invoker.name
  }

  config_data_name = "${target_name}_config_data"
  config_data(config_data_name) {
    for_pkg = "archivist"
    outputs = [ "${name}/{{source_file_part}}" ]
    sources = invoker.inspect
  }

  resource_name = "${target_name}_resource"
  resource(resource_name) {
    sources = invoker.inspect
    outputs = [ "config/archivist/${name}/{{source_file_part}}" ]
  }

  group(invoker.target_name) {
    deps = [
      ":${config_data_name}",
      ":${resource_name}",
    ]
  }
}
