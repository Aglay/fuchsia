# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/sys/build/components.gni")
import("//topaz/runtime/dart_runner/dart_app.gni")

dart_app("log-validator-dart-puppet") {
  components = [
    {
      component_name = "log-validator-dart-puppet"
      component_type = "dart"
      main_dart = "lib/main.dart"
      package_root = "."
      sources = [ "main.dart" ]
      deps = [
        "//sdk/fidl/fuchsia.diagnostics.stream",
        "//sdk/fidl/fuchsia.mem",
        "//src/diagnostics/lib/streams/dart:fuchsia_diagnostic_streams",
        "//src/diagnostics/validator/logs/fidl:validate",
        "//topaz/public/dart/fidl",
        "//topaz/public/dart/fuchsia_logger",
        "//topaz/public/dart/fuchsia_services",
        "//topaz/public/dart/zircon",
      ]
    },
  ]

  meta = [
    {
      path = rebase_path("meta/puppet.cmx")
      dest = "log-validator-dart-puppet.cmx"
    },
  ]
}

fuchsia_component("log-validator-for-dart") {
  manifest = "meta/validator.cmx"
  deps = [ "../..:log-validator-bin" ]
}

fuchsia_test_package("log-validator-test-dart") {
  deps = [ ":log-validator-dart-puppet" ]
  test_components = [ ":log-validator-for-dart" ]
}

group("tests") {
  testonly = true
  deps = [
    ":log-validator-dart-puppet",
    ":log-validator-test-dart",
  ]
}
