# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_binary.gni")
import("//src/sys/build/components.gni")

rustc_binary("bin") {
  name = "log-validator"
  edition = "2018"
  with_unit_tests = true
  deps = [
    "fidl:validate-rustc",
    "//sdk/fidl/fuchsia.diagnostics:fuchsia.diagnostics-rustc",
    "//sdk/fidl/fuchsia.diagnostics.stream:fuchsia.diagnostics.stream-rustc",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger-rustc",
    "//sdk/fidl/fuchsia.mem:fuchsia.mem-rustc",
    "//sdk/fidl/fuchsia.sys:fuchsia.sys-rustc",
    "//src/lib/diagnostics/stream/rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-url",
    "//src/lib/syslog/rust:syslog",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:argh",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:pretty_assertions",
  ]

  sources = [
    "src/encoding.rs",
    "src/main.rs",
    "src/sink.rs",
  ]
}

fuchsia_unittest_package("log-validator-tests") {
  deps = [ ":bin_test" ]
  executable_path = "bin/log_validator_bin_test"
}

### C++ ###

fuchsia_component("log-validator-for-cpp") {
  manifest = "meta/validator-cpp.cmx"
  deps = [ ":bin" ]
}

fuchsia_test_package("log-encoding-validator-cpp") {
  deps = [ "puppet/cpp:component" ]
  test_components = [ ":log-validator-for-cpp" ]
}

### Dart ###

fuchsia_component("log-validator-for-dart") {
  manifest = "meta/validator-dart.cmx"
  deps = [ ":bin" ]
}

fuchsia_test_package("log-encoding-validator-dart") {
  test_components = [ ":log-validator-for-dart" ]
}

### Rust ###

fuchsia_component("log-validator-for-rust") {
  manifest = "meta/validator-rust.cmx"
  deps = [ ":bin" ]
}

fuchsia_test_package("log-encoding-validator-rust") {
  deps = [ "puppet/rust:component" ]
  test_components = [ ":log-validator-for-rust" ]
}

group("tests") {
  testonly = true

  deps = [
    ":log-encoding-validator-cpp",
    ":log-encoding-validator-dart",
    ":log-encoding-validator-rust",
    ":log-validator-tests",

    # TODO(fxbug.dev/62620) migrate dart puppet to new component templates and move to package deps
    "puppet/dart:log-validator-dart-puppet",
  ]
}
