# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# NOTE: the contents of this file have been copied into documentation located at
#       //docs/development/languages/rust/testing.md. Please update the snippets
#       in that file if the contents below are changed.

import("//build/package.gni")
import("//build/rust/rustc_binary.gni")
import("//build/test/test_package.gni")

group("tests") {
  testonly = true

  deps = [
    ":inspect_validator_tests",
    "//src/diagnostics/inspect_validator/lib/rust:rust",
  ]
}

group("inspect_validator") {
  deps = [
    ":inspect_validator_package",
  ]
}

rustc_binary("bin") {
  name = "validator"
  with_unit_tests = true
  edition = "2018"

  deps = [
    "//garnet/public/lib/fidl/rust/fidl",
    "//garnet/public/rust/fuchsia-async",
    "//garnet/public/rust/fuchsia-component",
    "//garnet/public/rust/fuchsia-syslog",
    "//garnet/public/rust/fuchsia-zircon",
    "//src/diagnostics/inspect_validator/fidl:validate-rustc",
    "//third_party/rust_crates:failure",
    "//third_party/rust_crates:log",
  ]
  test_deps = [ "//garnet/public/rust/fuchsia-async" ]
}

package("inspect_validator_package") {
  deps = [
    ":bin",
  ]

  binaries = [
    {
      name = "validator"
    },
  ]

  meta = [
    {
      path = rebase_path("meta/inspect_validator.cmx")
      dest = "inspect_validator.cmx"
    },
  ]
}

test_package("inspect_validator_tests") {
  deps = [
    ":bin_test",
  ]
  tests = [
    {
      name = "validator_bin_test"
    },
  ]
}
