# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/fidl/fidl.gni")
import("//build/images/manifest.gni")
import("//build/package.gni")
import("//build/testing/environments.gni")

executable("tests") {
  testonly = true

  output_name = "libdriver-integration-test"

  sources = [
    "action-list.cc",
    "basic-lifecycle-test.cc",
    "device-add-tests.cc",
    "integration-test.cc",
    "main.cc",
    "mock-device-hooks.cc",
    "mock-device-thread.cc",
    "mock-device.cc",
    "root-mock-device.cc",
  ]

  deps = [
    ":mock-device",
    "//garnet/public/lib/async_promise",
    "//garnet/public/lib/gtest",
    "//zircon/public/fidl/fuchsia-device:fuchsia-device",
    "//zircon/public/fidl/fuchsia-device-test:fuchsia-device-test",
    "//zircon/public/fidl/fuchsia-device-test:fuchsia-device-test_c",
    "//zircon/public/fidl/fuchsia-io:fuchsia-io",
    "//zircon/public/lib/async-cpp",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/ddk",
    "//zircon/public/lib/devmgr-integration-test",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/fit",
  ]
}

action("devmgr-manifest") {
  visibility = [ ":*" ]
  sources = zircon_boot_manifests
  outputs = [
    "$target_gen_dir/devmgr.manifest",
  ]
  script = "//build/images/manifest.py"
  args = [
    "--include=bin/devcoordinator",
    "--include=bin/fshost",
    "--output=" + rebase_path(outputs[0], root_build_dir),
  ]
  foreach(manifest, sources) {
    args += [
      "--cwd=" + rebase_path(get_path_info(manifest, "dir"), root_build_dir),
      "--manifest=" + rebase_path(manifest, root_build_dir),
    ]
  }
}

package("libdriver-integration-test") {
  testonly = true

  deps = [
    ":devmgr-manifest",
    ":tests",
  ]

  extra = [ "$target_gen_dir/devmgr.manifest" ]

  meta = [
    {
      dest = "libdriver-integration-test.cmx"
      path = dest
    },
  ]

  tests = [
    {
      name = "libdriver-integration-test"
      environments = basic_envs
    },
  ]
}

fidl("mock-device") {
  name = "fuchsia.device.mock"

  sources = [
    "//zircon/system/dev/test/mock-device/mock-device.fidl",
  ]

  deps = []
}
