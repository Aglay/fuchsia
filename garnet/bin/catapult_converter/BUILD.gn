# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/host.gni")
import("//build/package.gni")
import("//build/test.gni")
import("//build/testing/environments.gni")

source_set("converter") {
  testonly = true
  sources = [
    "converter.cc",
  ]
  deps = [
    "//src/lib/fxl",
    "//third_party/rapidjson",
  ]
}

executable("converter_bin") {
  output_name = "catapult_converter"
  testonly = true
  sources = [
    "main.cc",
  ]
  deps = [
    ":converter",
    "//third_party/rapidjson",
  ]
}

source_set("unittest") {
  testonly = true
  sources = [
    "converter_unittest.cc",
  ]
  deps = [
    ":converter",
    "//src/lib/fxl",
    "//third_party/googletest:gtest",
    "//third_party/googletest:gtest_main",
    "//third_party/rapidjson",
  ]
}

executable("unittest_bin") {
  testonly = true
  output_name = "catapult_converter_test"

  deps = [
    ":unittest",
  ]
}

package("catapult_converter") {
  testonly = true

  deps = [
    ":converter_bin",
    ":host_catapult_converter",
    ":unittest_bin",
  ]

  meta = [
    {
      path = "meta/catapult_converter.cmx"
      dest = "catapult_converter.cmx"
    },
  ]

  binaries = [
    {
      name = "catapult_converter"
      shell = true
    },
  ]

  tests = [
    {
      name = "catapult_converter_test"
      environments = basic_envs
    },
  ]
}

install_host_tools("host_catapult_converter") {
  testonly = true

  deps = [
    ":converter_bin",
  ]

  outputs = [
    "catapult_converter",
  ]
}

if (current_toolchain == host_toolchain) {
  test("host_catapult_converter_test") {
    deps = [
      ":unittest",
    ]
  }
}
