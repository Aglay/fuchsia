# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/go/go_binary.gni")
import("//build/go/go_library.gni")
import("//build/go/go_test.gni")
import("//build/host.gni")
import("//build/package.gni")

go_library("lib") {
  name = "traceutil"

  deps = [
    ":zedmon",
    "//third_party/golibs:github.com/golang/glog",
    "//third_party/golibs:github.com/google/subcommands",
    "//third_party/golibs:github.com/pkg/sftp",
    "//third_party/golibs:golang.org/x/crypto",
  ]
}

go_binary("bin") {
  output_name = "traceutil"
  gopackage = "traceutil"
  deps = [
    ":lib",
  ]
}

install_host_tools("traceutil") {
  deps = [
    ":bin",
  ]
  outputs = [
    "traceutil",
  ]
}

go_library("zedmon") {
  name = "fuchsia.googlesource.com/fuchsia/garnet/bin/traceutil/zedmon"
  source_dir = "zedmon"
}

go_library("fake_zedmon_lib") {
  name = "fake_zedmon"
  source_dir = "zedmon/fake_zedmon"
  sources = [
    "fake_zedmon.go",
  ]
}

go_binary("fake_zedmon_bin") {
  output_name = "fake_zedmon"
  gopackage = "fake_zedmon"
  deps = [
    ":fake_zedmon_lib",
  ]
}

go_test("zedmon_test") {
  gopackages =
      [ "fuchsia.googlesource.com/fuchsia/garnet/bin/traceutil/zedmon" ]
  deps = [
    ":lib",
  ]
  non_go_deps = [ ":fake_zedmon_bin" ]
}
