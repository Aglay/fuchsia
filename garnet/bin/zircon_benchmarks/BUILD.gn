# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/fidl/fidl.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

executable("bin") {
  output_name = "zircon_benchmarks"
  testonly = true
  sources = [
    "channels.cc",
    "events.cc",
    "expose.cc",
    "fdio_spawn.cc",
    "fifos.cc",
    "filesystem.cc",
    "handle.cc",
    "inspect.cc",
    "lazy_dir.cc",
    "mem_alloc.cc",
    "mmu.cc",
    "ports.cc",
    "prng.cc",
    "pseudo_dir.cc",
    "pthreads.cc",
    "random_memcpy.cc",
    "round_trips.cc",
    "sockets.cc",
    "test_runner.cc",
    "threads.cc",
    "util.cc",
    "vmo.cc",
  ]
  deps = [
    ":fidl_interface",
    "//src/lib/fsl",
    "//src/lib/fxl",
    "//src/lib/inspect_deprecated",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/async-loop-default",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/fs",
    "//zircon/public/lib/inspect",
    "//zircon/public/lib/zx",
  ]
  public_deps = [ "//zircon/public/lib/perftest" ]
  libs = [ "zircon" ]
}

# "Helper" executable used to implement the fdio_spawn benchmark.
executable("fdio_spawn_helper") {
  output_name = "fdio_spawn_helper"
  testonly = true
  sources = [ "fdio_spawn_helper.cc" ]
  deps = []
  public_deps = []
}

fidl("fidl_interface") {
  # TODO(fxb/35879): Remove lint exclusions by fixing known FIDL lint violations in this target
  excluded_checks = [ "protocol-name-includes-service" ]

  testonly = true
  name = "fuchsia.zircon.benchmarks"
  sources = [ "round_trip_service.fidl" ]
}

# Installing this package causes zircon_benchmarks to be run in unit test
# mode (fast mode) via runtests.
#
# This package requires zircon_benchmarks_perftestmode to also be installed.
#
# This package's copy of zircon_benchmarks invokes both itself
# (zircon_benchmarks) and fdio_spawn_helper as subprocesses, but it does so
# via the redirector executables in /bin (via the shell-commands package).
# Those point to the copies of those executables in
# zircon_benchmarks_perftestmode, which is why that package is required.
#
# Since Fuchsia packages cannot declare dependencies on other packages, GN
# targets that require zircon_benchmarks_unittestmode should depend on the
# zircon_benchmarks group() instead.
#
# The reason for splitting zircon_benchmarks into two packages like this is
# that we don't want to run zircon_benchmarks in unit test mode on the
# terminal-x64-release builder that runs SL4F tests, for two reasons:
#
#  1) The unit tests run on other builders, so it's unnecessary to run them
#     a second time.
#  2) Running unit tests on the terminal-x64-release builder currently does
#     not work: It results in the creation of a shard to run tests under
#     QEMU, but running Fuchsia on QEMU on that builder does not work
#     (perhaps because of missing dependencies).
test_package("zircon_benchmarks_unittestmode") {
  package_name = "zircon_benchmarks"

  deps = [ ":bin" ]

  binaries = [
    {
      name = "zircon_benchmarks"
    },
  ]

  tests = [
    {
      name = "zircon_benchmarks"
      environments = basic_envs
    },
  ]
}

# This package contains the zircon_benchmarks executable and installs a
# redirector executable for it in /bin (via the shell-commands package).
# Installing this package allows zircon_benchmarks to be run by SL4F tests
# as a performance test (i.e. outputting performance results data).  This
# package can be installed without installing the
# zircon_benchmarks_unittestmode package.
package("zircon_benchmarks_perftestmode") {
  testonly = true

  deps = [
    ":bin",
    ":fdio_spawn_helper",
  ]

  binaries = [
    {
      name = "fdio_spawn_helper"
      shell = true
    },
    {
      name = "zircon_benchmarks"
      shell = true
    },
  ]
}

# This contains all the dependencies necessary for running
# zircon_benchmarks in unit test mode.
group("zircon_benchmarks") {
  testonly = true
  deps = [
    ":zircon_benchmarks_perftestmode",
    ":zircon_benchmarks_unittestmode",
  ]
}
