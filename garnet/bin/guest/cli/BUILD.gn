# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//third_party/protobuf/proto_library.gni")

group("cli") {
  testonly = true
  deps = [
    ":bin",
    ":guest",
  ]
}

executable("bin") {
  output_name = "guest"

  sources = [
    "balloon.cc",
    "balloon.h",
    "launch.cc",
    "launch.h",
    "list.cc",
    "list.h",
    "main.cc",
    "serial.cc",
    "serial.h",
    "socat.cc",
    "socat.h",
    "vshc.cc",
    "vshc.h",
    "vshutil.cc",
  ]

  deps = [
    ":protos",
    "//garnet/public/lib/fsl",
    "//garnet/public/lib/svc/cpp",
    "//sdk/fidl/fuchsia.guest",
    "//sdk/fidl/fuchsia.sys",
    "//sdk/lib/sys/cpp",
    "//third_party/protobuf:protobuf_lite",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/virtio",
  ]

  public_deps = [
    "//zircon/public/fidl/fuchsia-hardware-pty:fuchsia-hardware-pty_c",
  ]
}

proto_library("protos") {
  sources = [
    "../pkg/biscotti_guest/third_party/protos/vsh.proto",
  ]

  generate_python = false
  cc_generator_options = "lite"

  import_dirs = [
    "//garnet/bin/guest/pkg/biscotti_guest/third_party/protos",
    "//third_party/protobuf/src",
  ]
}

package("guest") {
  deps = [
    ":bin",
  ]

  binaries = [
    {
      name = "guest"
      shell = true
    },
    {
      name = "vsh"
      shell = true
      source = "guest"
    },
  ]
}
