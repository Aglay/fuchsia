# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/testing/environments.gni")

source_set("lib") {
  sources = [
    "echo_server_app.cc",
    "echo_server_app.h",
  ]

  public_deps = [
    "//garnet/examples/fidl/services:echo",
    "//garnet/public/lib/component/cpp",
    "//sdk/lib/fidl/cpp",
  ]
}

executable("bin") {
  output_name = "echo_server_cpp"

  sources = [
    "echo_server.cc",
  ]

  deps = [
    ":lib",
    "//zircon/public/lib/async-default",
    "//zircon/public/lib/async-loop-cpp",
  ]
}

package("echo_server_cpp") {
  deps = [
    ":bin",
  ]

  binary = "echo_server_cpp"

  meta = [
    {
      path = rebase_path("../meta/echo_server.cmx")
      dest = "echo_server_cpp.cmx"
    },
  ]
}

executable("echo_server_cpp_unittests") {
  testonly = true

  sources = [
    "echo_server_app_unittest.cc",
  ]

  deps = [
    ":lib",
    "//garnet/public/lib/component/cpp/testing",
    "//garnet/public/lib/fxl/test:gtest_main",
  ]
}

package("echo_server_cpp_tests") {
  testonly = true

  deps = [
    ":echo_server_cpp_unittests",
  ]

  tests = [
    {
      name = "echo_server_cpp_unittests"
      environments = basic_envs
    },
  ]
}
