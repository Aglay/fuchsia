# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/test/test_package.gni")
import("//garnet/lib/magma/gnbuild/magma.gni")
import("//src/lib/vulkan/image_pipe_swapchain.gni")
import("//src/lib/vulkan/layers.gni")

test_package("test-vk-validation") {
  deps = [
    "tests/unit:test_vk_validation",
    "//src/lib/vulkan:validation_layers",
  ]

  tests = [
    {
      name = "test_vk_validation"
      environments = magma_libvulkan_hardware_envs
    },
  ]

  public_deps = vulkan_validation_layers.public_deps
  loadable_modules = vulkan_validation_layers.loadable_modules
  resources = vulkan_validation_layers.resources
}

package("vkcube_on_scenic") {
  testonly = true

  deps = [
    "tests/vkcube:vkcube_on_scenic",
  ]
  binary = "vkcube_on_scenic"

  meta = [
    {
      path = rebase_path("meta/vkcube_on_scenic.cmx")
      dest = "vkcube_on_scenic.cmx"
    },
  ]

  public_deps =
      vulkan_validation_layers.public_deps + image_pipe_swapchain.public_deps
  loadable_modules = vulkan_validation_layers.loadable_modules +
                     image_pipe_swapchain.loadable_modules
  resources =
      [
        {
          path = rebase_path("tests/vkcube/bbb_frame.yuv")
          dest = "bbb_frame.yuv"
        },
      ] + vulkan_validation_layers.resources + image_pipe_swapchain.resources
}

package("vkcube_on_fb") {
  testonly = true

  deps = [
    "tests/vkcube:vkcube_on_fb",
  ]
  binary = "vkcube_on_fb"

  meta = [
    {
      path = rebase_path("meta/vkcube_on_fb.cmx")
      dest = "vkcube_on_fb.cmx"
    },
  ]

  public_deps =
      vulkan_validation_layers.public_deps + image_pipe_swapchain_fb.public_deps
  loadable_modules = vulkan_validation_layers.loadable_modules +
                     image_pipe_swapchain_fb.loadable_modules
  resources =
      [
        {
          path = rebase_path("tests/vkcube/bbb_frame.yuv")
          dest = "bbb_frame.yuv"
        },
      ] + vulkan_validation_layers.resources + image_pipe_swapchain_fb.resources
}

package("vkcube_skip_present") {
  testonly = true

  deps = [
    "tests/vkcube:vkcube_skip_present",
  ]
  binary = "vkcube_skip_present"

  meta = [
    {
      path = rebase_path("meta/vkcube_skip_present.cmx")
      dest = "vkcube_skip_present.cmx"
    },
  ]

  public_deps = vulkan_validation_layers.public_deps +
                image_pipe_swapchain_fb_skip_present.public_deps
  loadable_modules = vulkan_validation_layers.loadable_modules +
                     image_pipe_swapchain_fb_skip_present.loadable_modules
  resources = [
                {
                  path = rebase_path("tests/vkcube/bbb_frame.yuv")
                  dest = "bbb_frame.yuv"
                },
              ] + vulkan_validation_layers.resources +
              image_pipe_swapchain_fb_skip_present.resources
}

package("vkprimer") {
  testonly = true

  binary = "vkprimer"

  deps = [
    "tests/vkprimer:vkprimer",
  ]

  resources = [
    {
      path = "tests/vkprimer/common/shaders/vert.spv"
      dest = "shaders/vert.spv"
    },
    {
      path = "tests/vkprimer/common/shaders/frag.spv"
      dest = "shaders/frag.spv"
    },
  ]

  if (is_fuchsia) {
    deps += [ "//src/lib/vulkan:validation_layers" ]

    meta = [
      {
        path = rebase_path("meta/vkprimer.cmx")
        dest = "vkprimer.cmx"
      },
    ]

    public_deps = vulkan_validation_layers.public_deps +
                  image_pipe_swapchain_fb.public_deps
    loadable_modules = vulkan_validation_layers.loadable_modules +
                       image_pipe_swapchain_fb.loadable_modules
    resources +=
        vulkan_validation_layers.resources + image_pipe_swapchain_fb.resources
  }
}

package("vulkaninfo") {
  deps = [
    "//src/lib/vulkan:vulkaninfo",
  ]

  meta = [
    {
      path = rebase_path("meta/vulkaninfo.cmx")
      dest = "vulkaninfo.cmx"
    },
  ]

  binary = "vulkaninfo"

  public_deps =
      vulkan_validation_layers.public_deps + image_pipe_swapchain.public_deps
  loadable_modules = vulkan_validation_layers.loadable_modules +
                     image_pipe_swapchain.loadable_modules
  resources =
      vulkan_validation_layers.resources + image_pipe_swapchain.resources
}
